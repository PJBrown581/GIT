<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesQuotationLine_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(SalesQuotationLine))]
public final class PIPSalesQuotationLine_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipACLDate</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the date to use for ACL validation.
    /// </summary>
    /// <returns>
    ///     The applicable <c>ValidFromDate</c> value.
    /// </returns>
    public display ValidFromDate pipACLDate()
    {
        ValidFromDate   ret;

        switch(smmParametersTable::find().PIPACLDateType)
        {
            case PIPACLDateType::OrderDate :
                ret = DateTimeUtil::date(this.createdDateTime);
                break;

            case PIPACLDateType::RequestedReceiptDate :
                ret = this.ReceiptDateRequested;
                break;

            case PIPACLDateType::RequestedShipDate :
                ret = this.ShippingDateRequested;
                break;

            case PIPACLDateType::Today :
                ret = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
                break;
        }

        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipIsCustomerApproved</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Determines if the sales quotation line item has a valid approved customer list entry.
    /// </summary>
    /// <param name="_fieldId">
    ///     The field id for the modified field whose new value needs to be validated; optional.
    /// </param>
    /// <returns>
    ///     true if approved; otherwise, false.
    /// </returns>
    public boolean pipIsCustomerApproved(FieldId _fieldId = 0)
    {
        SalesCalcAvailableDlvDates  calcAvailableDlvDates;
        SalesDeliveryDateControlMap deliveryDateControlMap;
        ValidFromDate               aclDate     = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        boolean                     isDateField = (_fieldId == fieldNum(SalesQuotationLine, ReceiptDateRequested)
                                                    || _fieldId == fieldNum(SalesQuotationLine, ShippingDateRequested));

        if (!#PIPApprovedCustomerListEnabled
            || (_fieldId
                && !isDateField))
        {
            return true;
        }

        if (isDateField)
        {
            calcAvailableDlvDates   = SalesCalcAvailableDlvDates::newCommonSalesDlvDateType(this, _fieldId);
            deliveryDateControlMap  = calcAvailableDlvDates.deliveryDateControlMap();
            calcAvailableDlvDates.modifiedField(this,true,false,true);
        }

        switch(smmParametersTable::find().PIPACLDateType)
        {
            case PIPACLDateType::RequestedReceiptDate :
                aclDate = deliveryDateControlMap ? deliveryDateControlMap.ReceiptDateRequested : this.ReceiptDateRequested;
                break;

            case PIPACLDateType::RequestedShipDate :
                aclDate = deliveryDateControlMap ? deliveryDateControlMap.ShippingDateRequested : this.ShippingDateRequested;
                break;
        }

        return this.type().pipApprovedCustListValidator(aclDate).validateCustomerCheckResult();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>