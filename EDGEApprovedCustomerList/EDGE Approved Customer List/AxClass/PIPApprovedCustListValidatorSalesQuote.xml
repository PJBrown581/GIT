<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPApprovedCustListValidatorSalesQuote</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This class handles the approved customer list validation for buffer type <c>SalesQuotationTable</c>.
/// </summary>
[PIPApprovedCustListValidatorAttribute(tableStr(SalesQuotationTable))]
class PIPApprovedCustListValidatorSalesQuote extends PIPApprovedCustListValidator
{
    SalesQuotationTable salesQuotationTable;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>initFromBuffer</Name>
				<Source><![CDATA[
    protected void initFromBuffer(Common _buffer)
    {
        this.initFromSalesQuotationTable(_buffer as salesQuotationTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromSalesQuotationTable</Name>
				<Source><![CDATA[
    private void initFromSalesQuotationTable(salesQuotationTable _salesQuotationTable)
    {
        salesQuotationTable     = _salesQuotationTable;
        this.parmCustAccount(salesQuotationTable.CustAccount);
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    
        this.parmStrictValidation(true);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCustomerCheckResult</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Method runs the check method and if check returns anything other than 'OK'. Either a warning or a fail message will be returned.
    /// </summary>
    /// <returns>
    ///     Returns true / false
    /// </returns>
    public boolean validateCustomerCheckResult()
    {
        boolean                         ret = true;
        PIPApprovedCustListValidator    lineValidator;
        SalesQuotationLine              salesQuotationLine;
    
        while select salesQuotationLine
            where salesQuotationLine.QuotationId == salesQuotationTable.QuotationId
        {
            lineValidator = PIPApprovedCustListValidator::newFromTable(salesQuotationLine, salesQuotationLine.pipACLDate());
            lineValidator.parmStrictValidation(this.parmStrictValidation());
            ret = lineValidator.validateCustomerCheckResult() && ret;
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PIPApprovedCustListValidatorSalesQuote construct()
    {
        return new PIPApprovedCustListValidatorSalesQuote();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>