<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesQuotationLineEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>PIPSalesQuotationLineEventHandler</c> class provides method event subscription
///     handlers for the <c>SalesQuotationLine</c> table.
/// </summary>
public class PIPSalesQuotationLineEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateFieldPost</Name>
				<Source><![CDATA[
    [PostHandlerFor(tableStr(SalesQuotationLine), tableMethodStr(SalesQuotationLine, validateField))]
    /// <summary>
    ///     Post event for the <c>SalesQuotationLine</c> validateField method.
    /// </summary>
    /// <param name="_args">
    ///     Arguments sent from the validateField method.
    /// </param>
    public static void validateFieldPost(XppPrePostArgs _args)
    {
        SalesQuotationLine  salesQuotationLine  = _args.getThis();
        boolean             ok                  = _args.getReturnValue();
        FieldId             fieldId             = _args.getArgNum(1);

        if (ok && #PIPApprovedCustomerListEnabled)
        {
            switch (fieldId)
            {
                case fieldNum(SalesQuotationLine, ShippingDateRequested):
                case fieldNum(SalesQuotationLine, ReceiptDateRequested):
                    if (salesQuotationLine.QuotationType == QuotationType::Sales)
                    {
                        ok = salesQuotationLine.pipIsCustomerApproved(fieldId);
                    }
                break;
            }
        }

        _args.setReturnValue(ok);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWritePost</Name>
				<Source><![CDATA[
    [PostHandlerFor(tableStr(SalesQuotationLine), tableMethodStr(SalesQuotationLine, validateWrite))]
    /// <summary>
    ///     Post event for the <c>SalesQuotationLine</c> validateWrite method.
    /// </summary>
    /// <param name="_args">
    ///     Arguments sent from the validateWrite method.
    /// </param>
    public static void validateWritePost(XppPrePostArgs _args)
    {
        SalesQuotationLine  salesQuotationLine  = _args.getThis();
        boolean             ok                  = _args.getReturnValue();

        if (ok && #PIPApprovedCustomerListEnabled)
        {
            ok = salesQuotationLine.pipIsCustomerApproved();
        }

        _args.setReturnValue(ok);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>