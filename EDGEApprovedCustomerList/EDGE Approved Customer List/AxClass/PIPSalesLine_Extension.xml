<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesLine_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(SalesLine))]
public final class PIPSalesLine_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipIsCustomerApproved</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Determines if the sales line item has a valid approved customer list entry.
    /// </summary>
    /// <param name="_fieldId">
    ///     The field id for the modified field whose new value needs to be validated; optional.
    /// </param>
    /// <returns>
    ///     true if approved; otherwise, false.
    /// </returns>
    public boolean pipIsCustomerApproved(FieldId _fieldId = 0)
    {
        SalesCalcAvailableDlvDates  calcAvailableDlvDates;
        SalesDeliveryDateControlMap deliveryDateControlMap;
        ValidFromDate               aclDate     = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
        boolean                     isDateField = (_fieldId == fieldNum(SalesLine, ShippingDateRequested)
                                                    || _fieldId == fieldNum(SalesLine, ReceiptDateRequested));

        if (!#PIPApprovedCustomerListEnabled
            || (_fieldId
                && !isDateField))
        {
            return true;
        }

        if (isDateField && !this.salesTable().InterCompanyOrder)
        {
            calcAvailableDlvDates   = SalesCalcAvailableDlvDates::newCommonSalesDlvDateType(this, _fieldId);
            deliveryDateControlMap  = calcAvailableDlvDates.deliveryDateControlMap();
            calcAvailableDlvDates.modifiedField(this,false,false,true);
        }

        switch(smmParametersTable::find().PIPACLDateType)
        {
            case PIPACLDateType::RequestedReceiptDate :
                aclDate = deliveryDateControlMap ? deliveryDateControlMap.ReceiptDateRequested : this.ReceiptDateRequested;
                break;

            case PIPACLDateType::RequestedShipDate :
                aclDate = deliveryDateControlMap ? deliveryDateControlMap.ShippingDateRequested : this.ShippingDateRequested;
                break;
        }

        return this.type().pipApprovedCustListValidator(aclDate).validateCustomerCheckResult();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipWHSIsCustomerApproved</Name>
				<Source><![CDATA[
    public container pipWHSIsCustomerApproved(FieldId _fieldId = 0)
    {
        return this.type().pipApprovedCustListValidator(this.pipACLDate()).validateCustomerCheckResultWHS();
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipACLDate</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the date to use for ACL validation.
    /// </summary>
    /// <param name="_deliveryDateControlMap">
    ///     The calculated ship and receipt dates for the sales line; optional.
    /// </param>
    /// <returns>
    ///     The applicable <c>ValidFromDate</c> value.
    /// </returns>
    public display ValidFromDate pipACLDate(SalesDeliveryDateControlMap _deliveryDateControlMap = null)
    {
        ValidFromDate   ret;
    
        switch(smmParametersTable::find().PIPACLDateType)
        {
            case PIPACLDateType::OrderDate :
                ret = DateTimeUtil::date(this.salesTable().CreatedDateTime);
                break;
    
            case PIPACLDateType::RequestedReceiptDate :
                ret = _deliveryDateControlMap ? _deliveryDateControlMap.ReceiptDateRequested : this.ReceiptDateRequested;
                break;
    
            case PIPACLDateType::RequestedShipDate :
                ret = _deliveryDateControlMap ? _deliveryDateControlMap.ShippingDateRequested : this.ShippingDateRequested;
                break;
    
            case PIPACLDateType::Today :
                ret = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
                break;
        }
    
        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>