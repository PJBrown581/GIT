<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPAgreementLine_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>PIPAgreementLine_Extension</c> class provides method event subscription handlers for the
/// <c>AgreementLine</c> table.
/// </summary>
[ExtensionOf(tableStr(AgreementLine))]
public final class PIPAgreementLine_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateWritePost</Name>
				<Source><![CDATA[
    [PostHandlerFor(tableStr(AgreementLine), tableMethodStr(AgreementLine, validateWrite))]
    /// <summary>
    /// Handles checking that the customer is approved when validating a record of the <c>AgreementLine</c> table.
    /// </summary>
    /// <param name="_args">
    /// The standard method event pre-/post-arguments.
    /// </param>
    public static void validateWritePost(XppPrePostArgs _args)
    {
        AgreementLine                   thisLine = _args.getThis();
        boolean                         ok       = _args.getReturnValue();
        PIPApprovedCustListValidator    aclValidator;
    
        if (#PIPApprovedCustomerListEnabled && ok && thisLine.agreementModuleType() == ModuleSalesPurch::Sales)
        {
            aclValidator = PIPApprovedCustListValidator::newFromTable(thisLine);
            aclValidator.parmFromDate(thisLine.EffectiveDate);
            aclValidator.parmToDate(thisLine.ExpirationDate);
            aclValidator.parmIgnoreError(true);
            ok = aclValidator.validateCustomerCheckResult();
        }
    
        _args.setReturnValue(ok);
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipACLFromDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the from date to use for Approved Customer List validation for this agreement line.
    /// </summary>
    /// <param name = "_header">The related record from the <c>AgreementHeader</c> table to use if the line dates are no set yet; optional.</param>
    /// <returns>The from date to use for Approved Customer List validation.</returns>
    public FromDate pipACLFromDate(AgreementHeader _header = null)
    {
        if (this.EffectiveDate && this.ExpirationDate)
        {
            return this.EffectiveDate;
        }

        AgreementHeader header = _header ? _header : this.agreementHeader();
        return header.DefaultAgreementLineEffectiveDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipACLToDate</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets the to date to use for Approved Customer List validation for this agreement line.
    /// </summary>
    /// <param name = "_header">The related record from the <c>AgreementHeader</c> table to use if the line dates are no set yet; optional.</param>
    /// <returns>The to date to use for Approved Customer List validation.</returns>
    public ToDate pipACLToDate(AgreementHeader _header = null)
    {
        if (this.EffectiveDate && this.ExpirationDate)
        {
            return this.ExpirationDate;
        }

        AgreementHeader header = _header ? _header : this.agreementHeader();
        return header.DefaultAgreementLineExpirationDate == dateNull() ? dateMax() : header.DefaultAgreementLineExpirationDate;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>