<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPApprovedCustListValidatorAgreementLn</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     This class handles the approved customer list validation for buffer type AgreementLine.
/// </summary>
[PIPApprovedCustListValidatorAttribute(tableStr(AgreementLine))]
class PIPApprovedCustListValidatorAgreementLn extends PIPApprovedCustListValidator
{
    AgreementLine           agreementLine;
    SalesAgreementHeader    salesAgreementHeader;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getSalesAgreementHeader</Name>
				<Source><![CDATA[
    private SalesAgreementHeader getSalesAgreementHeader()
    {
        if (salesAgreementHeader.RecId != agreementLine.Agreement)
        {
            salesAgreementHeader = agreementLine.agreementHeader() as SalesAgreementHeader;
        }
    
        return salesAgreementHeader;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromAgreementLine</Name>
				<Source><![CDATA[
    private void initFromAgreementLine(AgreementLine _agreementLine)
    {
        boolean overrideDefault;
    
        agreementLine = _agreementLine;
        this.parmInventTable(agreementLine.inventTable());
        this.parmInventDim(this.getInventDimFromForm(agreementLine));
    
        this.parmCustAccount(this.getSalesAgreementHeader().CustAccount);
    
        this.parmFromDate(agreementLine.pipACLFromDate(this.getSalesAgreementHeader()));
        this.parmToDate(agreementLine.pipACLToDate(this.getSalesAgreementHeader()));
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInventDimFromForm</Name>
				<Source><![CDATA[
    private InventDim getInventDimFromForm(AgreementLine _agreementLine)
    {
        InventDim inventDimLocal;

        if (FormDataUtil::isFormDataSource(_agreementLine))
        {
            // Prefer to use Global::getJoinChildFormDataSource(FormDataUtil::getFormDataSource(_buffer), tableNum(InventDim)) but that returns null for this form for some reason (the join data source cannot be found)
            FormRun formRun = _agreementLine.dataSource().formRun();
            FormDataSource inventDimDataSource = formRun.dataSource(formDataSourceStr(SalesAgreement, InventDim));
            if (inventDimDataSource)
            {
                inventDimLocal = inventDimDataSource.cursor() as InventDim;
            }
            else
            {
                inventDimLocal = _agreementLine.inventDim();
            }
        }
        else
        {
            inventDimLocal = _agreementLine.inventDim();
        }

        return inventDimLocal;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromBuffer</Name>
				<Source><![CDATA[
    protected void initFromBuffer(Common _buffer)
    {
        this.initFromAgreementLine(_buffer as AgreementLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateCustomerCheckResult</Name>
				<Source><![CDATA[
    public boolean validateCustomerCheckResult()
    {
        if (this.getCustomerCheckResult(this.parmFromDate(), this.parmToDate()) == PIPCustomerCheckResult::NotAllowed)
        {
            setPrefix(this.getSalesAgreementHeader().SalesNumberSequence);
        }
    
        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PIPApprovedCustListValidatorAgreementLn construct()
    {
        return new PIPApprovedCustListValidatorAgreementLn();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>