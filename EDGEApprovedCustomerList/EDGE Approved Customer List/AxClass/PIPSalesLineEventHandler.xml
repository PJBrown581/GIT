<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesLineEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PIPSalesLineEventHandler</c> class provides method event subscription handlers for the <c>SalesLine</c> table.
/// </summary>
public class PIPSalesLineEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateFieldPost</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Post event for the <c>SalesLine</c> validateField method.
    /// </summary>
    /// <param name="_args">
    ///     Arguments sent from the validateField method.
    /// </param>
    [PostHandlerFor(tableStr(SalesLine), tableMethodStr(SalesLine, validateField))]
    public static void validateFieldPost(XppPrePostArgs _args)
    {
        SalesLine   salesLine   = _args.getThis();
        boolean     ok          = _args.getReturnValue();
        FieldId     fieldId     = _args.getArgNum(1);

        if (ok && #PIPApprovedCustomerListEnabled)
        {
            switch (fieldId)
            {
                case fieldNum(SalesLine, ShippingDateRequested):
                case fieldNum(SalesLine, ReceiptDateRequested):
                if (salesLine.SalesType == SalesType::Sales)
                    {
                        ok = salesLine.pipIsCustomerApproved(fieldId);
                    }
                    break;
            }
        }

        _args.setReturnValue(ok);
    }

]]></Source>
			</Method>
			<Method>
				<Name>validateWritePost</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Post event for the <c>SalesLine</c> validateWrite method.
    /// </summary>
    /// <param name="_args">
    ///     Arguments sent from the validateWrite method.
    /// </param>
    [PostHandlerFor(tableStr(SalesLine), tableMethodStr(SalesLine, validateWrite))]
    public static void validateWritePost(XppPrePostArgs _args)
    {
        SalesLine   salesLine   = _args.getThis();
        boolean     ok          = _args.getReturnValue();

        if (ok && #PIPApprovedCustomerListEnabled)
        {
            ok = salesLine.pipIsCustomerApproved();
        }

        _args.setReturnValue(ok);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>