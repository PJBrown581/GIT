<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(SalesTable))]
public final class PIPSalesTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipACLDate</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Gets the date to use for ACL validation.
    /// </summary>
    /// <param name="_deliveryDateControlMap">
    ///     The calculated ship and receipt dates for the sales order; optional.
    /// </param>
    /// <returns>
    ///     The applicable <c>ValidFromDate</c> value.
    /// </returns>
    public display ValidFromDate pipACLDate(SalesDeliveryDateControlMap _deliveryDateControlMap = null)
    {
        ValidFromDate   ret;
    
        switch(smmParametersTable::find().PIPACLDateType)
        {
            case PIPACLDateType::OrderDate :
                ret = DateTimeUtil::date(this.createdDateTime);
                break;
    
            case PIPACLDateType::RequestedReceiptDate :
                ret = _deliveryDateControlMap ? _deliveryDateControlMap.ReceiptDateRequested : this.ReceiptDateRequested;
                break;
    
            case PIPACLDateType::RequestedShipDate :
                ret = _deliveryDateControlMap ? _deliveryDateControlMap.ShippingDateRequested : this.ShippingDateRequested;
                break;
    
            case PIPACLDateType::Today :
                ret = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
                break;
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipIsCustomerApproved</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Determines if the corresponding sales lines have valid approved
    ///     customer list entry if controlled by ACL functionality.
    /// </summary>
    /// <param name="_fieldId">
    ///     The field id for the modified field whose new value needs to people validated; optional.
    /// </param>
    /// <returns>
    ///     True if approved; otherwise, false.
    /// </returns>
    public boolean pipIsCustomerApproved(FieldId _fieldId = 0)
    {
        SalesCalcAvailableDlvDates  calcAvailableDlvDates;
        ValidFromDate               aclDate;
    
        if (!#PIPApprovedCustomerListEnabled)
        {
            return true;
        }
    
        if (_fieldId)
        {
            calcAvailableDlvDates = SalesCalcAvailableDlvDates::newCommonSalesDlvDateType(this, _fieldId);
            calcAvailableDlvDates.modifiedField(this,false,false,true);
            aclDate = this.pipACLDate(calcAvailableDlvDates.deliveryDateControlMap());
        }
        else
        {
            aclDate = this.pipACLDate();
        }
    
        return this.type().pipApprovedCustListValidator(aclDate).validateCustomerCheckResult();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>