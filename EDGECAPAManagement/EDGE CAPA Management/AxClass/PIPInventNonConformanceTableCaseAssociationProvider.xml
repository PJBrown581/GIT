<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventNonConformanceTableCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'NonConformance'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PIPInventNonConformanceTableCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        InventNonConformanceTable inventNonConformanceTable;

        select crosscompany DataAreaId from inventNonConformanceTable 
            where inventNonConformanceTable.RecId == _parentRecId;

        return inventNonConformanceTable.company();     
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        InventNonConformanceTable inventNonConformanceTable;

        select crosscompany InventNonConformanceId from inventNonConformanceTable
            where inventNonConformanceTable.RecId == _parentRecId;

        return inventNonConformanceTable.InventNonConformanceId;      
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        InventNonConformanceTable inventNonConformanceTable;

        select RecId from inventNonConformanceTable
            where inventNonConformanceTable.InventNonConformanceId == _naturalKey;
       
        return inventNonConformanceTable.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        InventNonConformanceTable inventNonConformanceTable;

        select crosscompany InventNonConformanceId from inventNonConformanceTable
            where inventNonConformanceTable.RecId == _parentRecId;       

        return inventNonConformanceTable.InventNonConformanceId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        InventNonConformanceTable inventNonConformanceTable;

        select crosscompany * from inventNonConformanceTable
            where inventNonConformanceTable.RecId == _association.RefRecId;
       
        _args.record(inventNonConformanceTable);
        return menuitemdisplaystr(InventNonConformanceTable);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(InventNonConformanceTable), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(InventNonConformanceTable));
        oqbds.addSortField(fieldnum(InventNonConformanceTable, InventNonConformanceID), SortOrder::Descending);

        oSysTableLookup.addLookupfield(fieldnum(InventNonConformanceTable, InventNonConformanceID));
        oSysTableLookup.addLookupfield(fieldnum(InventNonConformanceTable, ItemId));
        oSysTableLookup.addLookupfield(fieldnum(InventNonConformanceTable, NonConformanceDate));       
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(InventNonConformanceTable))
        {
            _eventArgs.parmEntityType(CaseEntityType::NonConformance);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>