<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventQuarantineOrderCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'QuarantineOrder'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PIPInventQuarantineOrderCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        InventQuarantineOrder inventQuarantineOrder;

        select crosscompany DataAreaId from inventQuarantineOrder
            where inventQuarantineOrder.RecId == _parentRecId;

        return inventQuarantineOrder.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        InventQuarantineOrder inventQuarantineOrder;

        select crosscompany QuarantineId from inventQuarantineOrder
            where inventQuarantineOrder.RecId == _parentRecId;

        return inventQuarantineOrder.QuarantineId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        InventQuarantineOrder inventQuarantineOrder;

        select RecId from inventQuarantineOrder
            where inventQuarantineOrder.QuarantineId == _naturalKey;
       
        return inventQuarantineOrder.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        InventQuarantineOrder inventQuarantineOrder;

        select crosscompany Name from inventQuarantineOrder
            where inventQuarantineOrder.RecId == _parentRecId;

        return inventQuarantineOrder.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        InventQuarantineOrder inventQuarantineOrder;

        select crosscompany * from inventQuarantineOrder
            where inventQuarantineOrder.RecId == _association.RefRecId;
       
        _args.record(inventQuarantineOrder);
        return menuitemdisplaystr(InventQuarantineOrder);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup oSysTableLookup = SysTableLookup::newParameters(tablenum(InventQuarantineOrder), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(InventQuarantineOrder));
        oqbds.addSortField(fieldnum(InventQuarantineOrder, QuarantineId), SortOrder::Descending);

        oSysTableLookup.addLookupfield(fieldnum(InventQuarantineOrder, QuarantineId));
        oSysTableLookup.addLookupfield(fieldnum(InventQuarantineOrder, ItemId));
        oSysTableLookup.addLookupfield(fieldnum(InventQuarantineOrder, Name));
        oSysTableLookup.addLookupfield(fieldnum(InventQuarantineOrder, Status));
        oSysTableLookup.addLookupfield(fieldnum(InventQuarantineOrder, Qty));
        oSysTableLookup.parmQuery(oQuery);
        oSysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(InventQuarantineOrder))
        {
            _eventArgs.parmEntityType(CaseEntityType::QuarantineOrder);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>