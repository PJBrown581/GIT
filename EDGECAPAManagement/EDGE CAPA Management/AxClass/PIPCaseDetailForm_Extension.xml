<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPCaseDetailForm_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(CaseDetailForm))]
public final class PIPCaseDetailForm_Extension
{
    private boolean capaMode;

    #PIPCAPAManagement

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupParentCase</Name>
				<Source><![CDATA[
    public client static CaseDetailBase lookupParentCase(FormReferenceControl _ctrl, CaseCategoryType _categoryType, CaseDetailBase _caseDetail)
    {
        // Determine if the caller form is running in CAPA mode
        boolean doCAPALookup = false;
        if (_caseDetail)
        {
            doCAPALookup = _caseDetail.PIPIsCAPA;
        }
        else 
        {
            FormRun formRun = _ctrl.formRun();
            var caseDetailBase_ds = formRun.dataSource(formDataSourceStr(CaseDetailCreate, CaseDetailBase));
            if (caseDetailBase_ds)
            {
                CaseDetailBase caseDetail = caseDetailBase_ds.cursor();
                doCAPALookup = caseDetail.PIPIsCAPA;
            }
            else
            {
                doCAPALookup = formRun.args().parm() == #CAPA;
            }
        }

        if (doCAPALookup)
        {
            return CaseDetailForm::pipLookupCAPAParentCase(_ctrl, _caseDetail);
        }
        else
        {
            return next lookupParentCase(_ctrl, _categoryType, _caseDetail);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipLookupCAPAParentCase</Name>
				<Source><![CDATA[
    public static CaseDetailBase pipLookupCAPAParentCase(FormReferenceControl _ctrl, CaseDetailBase _caseDetail = null)
    {
        SysReferenceTableLookup sysTableLookup = SysReferenceTableLookup::newParameters(tableNum(CaseDetailBase), _ctrl, true);
        Query                   query;
        QueryBuildDataSource    queryBuildDataSource;
        QueryBuildRange         queryBuildRange;
    
        sysTableLookup.addLookupfield(fieldNum(CaseDetailBase, CaseId));
        sysTableLookup.addLookupfield(fieldNum(CaseDetailBase, Description));
        sysTableLookup.addLookupfield(fieldNum(CaseDetailBase, CategoryRecId));
    
        query                   = new Query();
        queryBuildDataSource    = query.addDataSource(tableNum(CaseDetailBase));
    
        if (_caseDetail.RecId)
        {
            queryBuildRange = queryBuildDataSource.addRange(fieldNum(CaseDetailBase, RecId));
            queryBuildRange.value(SysQuery::valueNot(_caseDetail.RecId));
        }
    
        sysTableLookup.parmQuery(query);
        return sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmPIPCAPAMode</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the boolean value to indicate whether the form should be run for a CAPA case.
    /// </summary>
    /// <param name = "_capaMode"></param>
    /// <returns></returns>
    public boolean parmPIPCAPAMode(boolean _capaMode = capaMode)
    {
        capaMode = _capaMode;
    
        return capaMode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>PIPCaseDetailForm_checkForCallerRedirect</Name>
				<Source><![CDATA[
    [SubscribesTo(classStr(CaseDetailForm), staticDelegateStr(CaseDetailForm, checkForCallerRedirect))]
    public static void PIPCaseDetailForm_checkForCallerRedirect(Args _args, CaseDetailFormEventArgs _caseDetailFormEventArgs)
    {
        if (_args.parmEnumType() == enumNum(CaseCategoryType) && _args.parmEnum() == CaseCategoryType::PIPCAPA)
        {
            new MenuFunction(menuitemDisplayStr(PIPCAPACaseDetail), MenuItemType::Display).run(_args);
            _caseDetailFormEventArgs.parmRedirectCaller(true);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>