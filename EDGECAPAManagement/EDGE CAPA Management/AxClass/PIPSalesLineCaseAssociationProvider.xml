<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSalesLineCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'SalesLine'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PIPSalesLineCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        SalesLine salesLine;

        select crosscompany DataAreaId from salesLine
            where salesLine.RecId == _parentRecId;

        return salesLine.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        SalesLine salesLine;

        select crosscompany SalesId from salesLine
            where salesLine.RecId == _parentRecId;

        return salesLine.SalesId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        SalesLine salesLine = SalesLine::find(_naturalKey);

        if (!salesLine)
        {
            // Since this table doesn't have a single field natural key
            // RecId might be sent instead.  See performLookup().
            salesLine = SalesLine::findRecId(str2Int64(_naturalKey));
        }

        return salesLine.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        SalesLine salesLine;

        select crosscompany Name from salesLine
            where salesLine.RecId == _parentRecId;

        return salesLine.Name;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        SalesLine salesLine;

        select crosscompany * from salesLine
            where salesLine.RecId == _association.RefRecId;
       
        _args.record(salesLine);
        return menuitemdisplaystr(SalesLine);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(SalesLine), callerControl);
        
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(SalesLine));
        sysTableLookup.addLookupfield(fieldNum(SalesLine, RecId), true);
        sysTableLookup.addLookupfield(fieldnum(SalesLine, SalesId));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, ItemId));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, SalesQty));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, SalesPrice));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, LineAmount));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, CurrencyCode));
        sysTableLookup.addLookupfield(fieldnum(SalesLine, SalesStatus));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(SalesLine))
        {
            _eventArgs.parmEntityType(CaseEntityType::SalesLine);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>