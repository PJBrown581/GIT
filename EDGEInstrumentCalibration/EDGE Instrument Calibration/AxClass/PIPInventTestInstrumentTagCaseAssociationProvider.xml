<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventTestInstrumentTagCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'PIPTestInstrumentTag'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PIPInventTestInstrumentTagCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        PIPInventTestInstrumentTag inventTestInstrumentTag;

        select crosscompany DataAreaId from inventTestInstrumentTag
            where inventTestInstrumentTag.RecId == _parentRecId;

        return inventTestInstrumentTag.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        PIPInventTestInstrumentTag inventTestInstrumentTag;

        select crosscompany TagNumber from inventTestInstrumentTag
            where inventTestInstrumentTag.RecId == _parentRecId;

        return inventTestInstrumentTag.TagNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        PIPInventTestInstrumentTag inventTestInstrumentTag;

        select RecId from inventTestInstrumentTag
            where inventTestInstrumentTag.TagNumber == _naturalKey;
       
        return inventTestInstrumentTag.RecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        PIPInventTestInstrumentTag inventTestInstrumentTag;

        select crosscompany TagNumber from inventTestInstrumentTag
            where inventTestInstrumentTag.RecId == _parentRecId;

        return inventTestInstrumentTag.TagNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        PIPInventTestInstrumentTag inventTestInstrumentTag;

        select crosscompany * from inventTestInstrumentTag
            where inventTestInstrumentTag.RecId == _association.RefRecId;
       
        _args.record(inventTestInstrumentTag);
        return menuitemdisplaystr(PIPInventTestInstrumentTag);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tablenum(PIPInventTestInstrumentTag), callerControl);
        
        Query query = new Query();
        QueryBuildDataSource qbds = query.addDataSource(tablenum(PIPInventTestInstrumentTag));
        sysTableLookup.addSelectionField(fieldNum(PIPInventTestInstrumentTag, TagNumber));       
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, TagNumber));
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, TestInstrumentId));
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, Description));
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, CalibrationCheck));
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, UsageStatus));
        sysTableLookup.addLookupfield(fieldnum(PIPInventTestInstrumentTag, IsForRestrcitedUse));        
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(PIPInventTestInstrumentTag))
        {
            _eventArgs.parmEntityType(CaseEntityType::PIPTestInstrumentTag);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>