<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventTestInstrumentCalibrationCaseAssociationProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
using System.ComponentModel.Composition;

/// <summary>
/// Plug in for case associations.
/// </summary>
[ExportMetadataAttribute('CaseIAssociation', 'PIPInstrumentCalibrationRecord'), ExportAttribute('Dynamics.AX.Application.CaseIAssociation')]
class PIPInventTestInstrumentCalibrationCaseAssociationProvider implements CaseIAssociation
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>insertCaseAssociation</Name>
				<Source><![CDATA[
    public void insertCaseAssociation(CaseAssociation _caseAssociation)
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedCompany</Name>
				<Source><![CDATA[
    public SelectableDataArea getLinkedCompany(RefRecId _parentRecId)
    {
        PIPInventTestInstrumentCalibration inventTestInstrumentCalibration;

        select crosscompany DataAreaId from inventTestInstrumentCalibration
            where inventTestInstrumentCalibration.RecId == _parentRecId;

        return inventTestInstrumentCalibration.company();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedId</Name>
				<Source><![CDATA[
    public CaseAssociationLinkId getLinkedId(RefRecId _parentRecId)
    {
        return int642Str(_parentRecId);       
    }

]]></Source>
			</Method>
			<Method>
				<Name>getRecIdWithNaturalKey</Name>
				<Source><![CDATA[
    public RefRecId getRecIdWithNaturalKey(CaseAssociationLinkId _naturalKey)
    {
        return str2Int64(_naturalKey);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getLinkedName</Name>
				<Source><![CDATA[
    public CaseLinkedName getLinkedName(RefRecId _parentRecId)
    {
        PIPInventTestInstrumentCalibration inventTestInstrumentCalibration;

        select crosscompany TestInstrumentTagNumber from inventTestInstrumentCalibration
            where inventTestInstrumentCalibration.RecId == _parentRecId;

        return inventTestInstrumentCalibration.TestInstrumentTagNumber;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJumpRefMenuItem</Name>
				<Source><![CDATA[
    public str getJumpRefMenuItem(args _args, CaseAssociation _association)
    {
        PIPInventTestInstrumentCalibration inventTestInstrumentCalibration;

        select crosscompany * from inventTestInstrumentCalibration
            where inventTestInstrumentCalibration.RecId == _association.RefRecId;
       
        _args.record(inventTestInstrumentCalibration);
        return menuitemdisplaystr(PIPInventInstrumentCalibrationDetailMain);
    }

]]></Source>
			</Method>
			<Method>
				<Name>performLookup</Name>
				<Source><![CDATA[
    public void performLookup(FormStringControl callerControl, CaseAssociation _caseAssociation)
    {
        SysFieldGroupLookup oSysFieldGroupLookup = SysFieldGroupLookup::newParameters(tablenum(PIPInventTestInstrumentCalibration), callerControl);

        Query oQuery = new Query();
        QueryBuildDataSource oqbds = oQuery.addDataSource(tablenum(PIPInventTestInstrumentCalibration));
        oqbds.addSortField(fieldnum(PIPInventTestInstrumentCalibration, InventTestInstrument), SortOrder::Descending);
        oqbds.addSortField(fieldnum(PIPInventTestInstrumentCalibration, TestInstrumentTagNumber), SortOrder::Descending);

        oSysFieldGroupLookup.parmFieldGroupNameForLookup('AutoReport');
        oSysFieldGroupLookup.parmCloseSelectFieldId(fieldNum(PIPInventTestInstrumentCalibration, RecId));
        oSysFieldGroupLookup.parmQuery(oQuery);
        oSysFieldGroupLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCaseEntityType</Name>
				<Source><![CDATA[
    /// <summary>
    /// Subscribes to the <c>CaseAssociation.getCaseEntityType()</c> event.
    /// </summary>
    /// <param name="_refTableId">
    /// The table ID of the table associated with the case.
    /// </param>
    /// <param name = "_eventArgs">
    /// The arguments for the event.
    /// </param>
    [SubscribesTo(tableStr(CaseAssociation), delegatestr(CaseAssociation, getCaseEntityType))]
    public static void getCaseEntityType(TableId _refTableId, CaseAssociationGetEntityTypeEventArgs _eventArgs)
    {
        if (_refTableId == tablenum(PIPInventTestInstrumentCalibration))
        {
            _eventArgs.parmEntityType(CaseEntityType::PIPInstrumentCalibrationRecord);
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>