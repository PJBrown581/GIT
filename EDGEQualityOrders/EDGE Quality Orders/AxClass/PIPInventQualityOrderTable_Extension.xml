<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventQualityOrderTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(InventQualityOrderTable))]
public final class PIPInventQualityOrderTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>priorityDescription</Name>
				<Source><![CDATA[
    public display Description priorityDescription()
    {
        return PIPInventQualityOrderPriority::find(this.PIPPriorityRecId).Description;
    }

]]></Source>
			</Method>
			<Method>
				<Name>initFromInventTestAssociationTable</Name>
				<Source><![CDATA[
    public void initFromInventTestAssociationTable(InventTestAssociationTable _inventTestAssociationTable)
    {
        next initFromInventTestAssociationTable(_inventTestAssociationTable);

        this.PIPAssignedTo = _inventTestAssociationTable.PIPAssignedTo;
        this.PIPPriorityRecId = _inventTestAssociationTable.PIPPriorityRecId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipFindOpenQualityOrderForAssociationIncludeDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a quality order that matches the parameters and that is still open.
    /// </summary>
    /// <param name="_associationTable">
    /// The required <c>InventTestAssociationTable</c> record to match.
    /// </param>
    /// <param name="_inventRefId">
    /// The required <c>InventRefID</c> field for the quality order.
    /// </param>
    /// <param name="_inventTransId">
    /// The required <c>InventTransID</c> field for the quality order.
    /// </param>
    /// <param name="_itemId">
    /// The required item for the quality order.
    /// </param>
    /// <param name="_referenceType">
    /// The required reference type for the quality order.
    /// </param>
    /// <param name="_routeOprId">
    /// The required route operation for the quality order.
    /// </param>
    /// <param name="_oprNum">
    /// The required operation num for the quality order.
    /// </param>
    /// <param name="_forUpdate">
    /// A Boolean value that indicates whether to select found record for update.
    /// </param>
    /// <returns>
    /// An <c>InventQualityOrderTable</c> record; otherwise, an empty record.
    /// </returns>
    public static InventQualityOrderTable pipFindOpenQualityOrderForAssociationIncludeDim(
        InventTestAssociationTable  _associationTable,
        InventRefId                 _inventRefId,
        InventTransId               _inventTransId,
        ItemId                      _itemId,
        InventTestReferenceType     _referenceType,
        InventDimId                 _inventDimid,
        RouteOprId                  _routeOprId       = '',
        OprNum                      _oprNum           = minInt(),
        boolean                     _forUpdate       = false)
    {
        Query query = InventQualityOrderTable::buildQueryOpenQualityOrderForAssociation(_associationTable, _inventRefId, _inventTransId, _itemId, _referenceType, _routeOprId, _oprNum, _forUpdate);
        query.dataSourceTable(tableNum(InventQualityOrderTable)).addRange(fieldNum(InventQualityOrderTable, InventDimId)).value(queryValue(_inventDimid));

        QueryRun queryRun = new QueryRun(query);

        if (queryRun.next())
        {
            return queryRun.get(tableNum(InventQualityOrderTable));
        }

        return null;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipFindLastClosedQualityOrderForAssociationIncludeDim</Name>
				<Source><![CDATA[
    /// <summary>
    /// Finds a quality order that matches the specified parameters and that was closed last.
    /// </summary>
    /// <param name="_associationTable">
    /// The required <c>InventTestAssociationTable</c> record to match.
    /// </param>
    /// <param name="_inventRefId">
    /// The required <c>InventRefID</c> field for the quality order.
    /// </param>
    /// <param name="_inventTransId">
    /// The required <c>InventTransID</c> field for the quality order.
    /// </param>
    /// <param name="_itemId">
    /// The required item for the quality order.
    /// </param>
    /// <param name="_referenceType">
    /// The required reference type for the quality order.
    /// </param>
    /// <param name="_routeOprId">
    /// The required route operation for the quality order.
    /// </param>
    /// <param name="_oprNum">
    /// The required <c>OprNum</c> field for the quality order.
    /// </param>
    /// <returns>
    /// An <c>InventQualityOrderTable</c> record; otherwise, an empty record.
    /// </returns>
    public static InventQualityOrderTable pipFindLastClosedQualityOrderForAssociationIncludeDim(
        InventTestAssociationTable  _associationTable,
        InventRefId                 _inventRefId,
        InventTransId               _inventTransId,
        ItemId                      _itemId,
        InventTestReferenceType     _referenceType,
        InventDimId                 _inventDimid,
        RouteOprId                  _routeOprId       = '',
        OprNum                      _oprNum           = minInt())
    {
        Query query = InventQualityOrderTable::buildQueryLastClosedQualityOrderForAssociation(_associationTable, _inventRefId, _inventTransId, _itemId, _referenceType, _routeOprId, _oprNum);
        query.dataSourceTable(tableNum(InventQualityOrderTable)).addRange(fieldNum(InventQualityOrderTable, InventDimId)).value(queryValue(_inventDimid));

        QueryRun queryRun = new QueryRun(query);

        if (queryRun.next())
        {
            return queryRun.get(tableNum(InventQualityOrderTable));
        }

        return null;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>