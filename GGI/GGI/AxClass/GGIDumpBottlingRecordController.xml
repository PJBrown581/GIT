<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGIDumpBottlingRecordController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Controller class for <c>GGIDumpBottlingReport</c> report.
/// </summary>
public class GGIDumpBottlingRecordController extends SrsReportRunController
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    protected void prePromptModifyContract()
    {
        this.setQueryRange();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQueryRange</Name>
				<Source><![CDATA[
    private void setQueryRange()
    {
        FormDataSource  formDataSource = null;
        int             maxRemaining = 15;
        str             queryString;
        Query query = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = query.dataSourceTable(tableNum(GGIDumpAndBottlingRecord));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(GGIDumpAndBottlingRecord,DBRId));
        GGIDumpAndBottlingRecord GGIDumpAndBottlingRecordCaller = this.parmArgs().record() as GGIDumpAndBottlingRecord;

        if (FormDataUtil::getFormDataSource(GGIDumpAndBottlingRecordCaller))
        {
            formDataSource = FormDataUtil::getFormDataSource(GGIDumpAndBottlingRecordCaller);
        }
        if (!formDataSource && GGIDumpAndBottlingRecordCaller.RecId)
        {
            queryBuildRange.value(queryValue(GGIDumpAndBottlingRecordCaller.DBRId));
        }
        else if (formDataSource)
        {
            for (GGIDumpAndBottlingRecordCaller = formDataSource.getFirst(true) ? formDataSource.getFirst(true) : formDataSource.cursor();
            GGIDumpAndBottlingRecordCaller; GGIDumpAndBottlingRecordCaller = formDataSource.getNext())
            {
                if (maxRemaining == 0)
                {
                    warning(strFmt("@SYS114377", '15'));
                    break;
                }
                maxRemaining--;
                queryString = queryRangeConcat(queryString, queryValue(GGIDumpAndBottlingRecordCaller.DBRId));
            }
            queryBuildRange.value(queryString);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static GGIDumpBottlingRecordController construct()
    {
        return new GGIDumpBottlingRecordController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>SRSReportRun</c> class, assigns range values, and runs the
    ///    <c>SRSReportRun</c> object.
    /// </summary>
    /// <param name="_args">
    ///    The args object.
    /// </param>
    /// <remarks>
    ///    This method assigns the <c>GGIDumpBottlingRecordController</c> range with the specified values.
    /// </remarks>
    public static void main(Args _args)
    {
        GGIDumpBottlingRecordController controller = GGIDumpBottlingRecordController::newFromArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a new instance of the <c>GGIDumpBottlingRecordController</c> class.
    /// </summary>
    /// <param name="_args">
    /// Any input arguments.
    /// </param>
    /// <returns>
    /// A new instance of the <c>GGIDumpBottlingRecordController</c> class.
    /// </returns>
    public static GGIDumpBottlingRecordController newFromArgs(Args _args)
    {
        if (!_args || !(_args.record() is GGIDumpAndBottlingRecord))
        {
            throw(error(strFmt("@SYS113735", classStr(GGIDumpBottlingRecordController), tableStr(GGIDumpAndBottlingRecord))));
        }

        GGIDumpBottlingRecordController controller = GGIDumpBottlingRecordController::construct();
        controller.parmReportName(ssrsReportStr(GGIDumpBottlingReport, Report));
        controller.parmArgs(_args);
        return controller;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>