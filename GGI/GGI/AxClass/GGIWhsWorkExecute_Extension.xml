<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGIWhsWorkExecute_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Customization for override of sellable days.
/// </summary>
[ExtensionOf(classStr(WhsWorkExecute))]
final class GGIWhsWorkExecute_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>checkBatchSellableDays</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Verifies that the batch number assigned to the work has a valid expiration date.
    /// </summary>
    /// <param name = "_inventBatch">
    ///     The batch dimension to move from.
    /// </param>
    /// <param name = "_workLine">
    ///     The work line to check against.
    /// </param>
    protected static void checkBatchSellableDays(InventBatch _inventBatch, WHSWorkLine _workLine)
    {
        next checkBatchSellableDays(null, null);

        if (_inventBatch
            && _workLine.WorkType == WHSWorkType::Pick
            && InventTable::find(_workLine.ItemId).whsBatchActive()
            && _workLine.whsWorkTable().WorkTransType == WHSWorkTransType::Sales
            && !WHSWorkLine::isPutBefore(_workLine.WorkId, _workLine.LineNum))
        {
            SalesLine   salesLine   = SalesLine::findInventTransId(_workLine.InventTransId);
            CustAccount custAccount = salesLine.GGIOverrideSellableDays ? '' : SalesTable::find(_workLine.whsWorkTable().OrderNum).CustAccount;

            InventDim productInventDim = _workLine.inventDim();

            productInventDim.clearNotProductDim(InventDimGroupSetup::newItemId(_workLine.ItemId));
            productInventDim = InventDim::findOrCreate(productInventDim);

            if (!InventBatch::pdsCheckExpiryDate(_inventBatch.inventBatchId, _workLine.ItemId, GGIDateTimeManager::construct().getSystemDate(), false, custAccount, false, productInventDim))
            {
                throw error("@WAX:BatchIsNotSellable");
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>