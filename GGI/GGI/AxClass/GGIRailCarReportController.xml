<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGIRailCarReportController</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Controller class for <c>GGIRailCarReport</c> report.
/// </summary>
public class GGIRailCarReportController extends SrsReportRunController
{
    #define.maxRemaining('15')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    protected void prePromptModifyContract()
    {
        this.setQueryRange();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQueryRange</Name>
				<Source><![CDATA[
    private void setQueryRange()
    {
        FormDataSource  formDataSource = null;
        int             maxRemaining = 15;
        str             queryString;
        Query query = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = query.dataSourceTable(tableNum(TTBRailCarTable));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(TTBRailCarTable,RailCarRecordNumber));
        TTBRailCarTable TTBRailCarTableCaller = this.parmArgs().record() as TTBRailCarTable;

        if (FormDataUtil::getFormDataSource(TTBRailCarTableCaller))
        {
            formDataSource = FormDataUtil::getFormDataSource(TTBRailCarTableCaller);
        }
        if (!formDataSource && TTBRailCarTableCaller.RecId)
        {
            queryBuildRange.value(queryValue(TTBRailCarTableCaller.RailCarRecordNumber));
        }
        else if (formDataSource)
        {
            for (TTBRailCarTableCaller = formDataSource.getFirst(true) ? formDataSource.getFirst(true) : formDataSource.cursor();
            TTBRailCarTableCaller; TTBRailCarTableCaller = formDataSource.getNext())
            {
                if (maxRemaining == 0)
                {
                    warning(strFmt("@SYS114377", #maxRemaining));
                    break;
                }
                maxRemaining--;
                queryString = queryRangeConcat(queryString, queryValue(TTBRailCarTableCaller.RailCarRecordNumber));
            }
            queryBuildRange.value(queryString);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static GGIRailCarReportController construct()
    {
        return new GGIRailCarReportController();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>SRSReportRun</c> class, assigns range values, and runs the
    ///    <c>SRSReportRun</c> object.
    /// </summary>
    /// <param name="_args">
    ///    The args object.
    /// </param>
    /// <remarks>
    ///    This method assigns the <c>GGIRailCarReportController</c> range with the specified values.
    /// </remarks>
    public static void main(Args _args)
    {
        GGIRailCarReportController controller = GGIRailCarReportController::newFromArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a new instance of the <c>GGIRailCarReportController</c> class.
    /// </summary>
    /// <param name="_args">
    /// Any input arguments.
    /// </param>
    /// <returns>
    /// A new instance of the <c>GGIRailCarReportController</c> class.
    /// </returns>
    public static GGIRailCarReportController newFromArgs(Args _args)
    {
        if (!_args || !(_args.record() is TTBRailCarTable))
        {
            throw(error(strFmt("@SYS113735", classStr(GGIRailCarReportController), tableStr(TTBRailCarTable))));
        }

        GGIRailCarReportController controller = GGIRailCarReportController::construct();
        controller.parmReportName(ssrsReportStr(GGIRailCarReport, Report));
        controller.parmArgs(_args);
        return controller;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>