<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGIGaugeRecord</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Controller class for <c>GGIGaugeRecordRecord</c> report.
/// </summary>
public class GGIGaugeRecord extends SrsReportRunController
{
    #define.maxRemaining('15')
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    protected void prePromptModifyContract()
    {
        this.setQueryRange();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQueryRange</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets ranges for a query for the <c>GGIGaugeRecord</c> report.
    /// </summary>
    private void setQueryRange()
    {
        FormDataSource  formDataSource = null;
        int             maxRemaining = 15;
        str             queryString;

        Query query = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = query.dataSourceTable(tableNum(TTBInventGaugeTable));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(TTBInventGaugeTable,InventGaugeId));
        TTBInventGaugeTable TTBInventGaugeTableCaller = this.parmArgs().record() as TTBInventGaugeTable;

        if (FormDataUtil::getFormDataSource(TTBInventGaugeTableCaller))
        {
            formDataSource = FormDataUtil::getFormDataSource(TTBInventGaugeTableCaller);
        }
        if (!formDataSource && TTBInventGaugeTableCaller.RecId)
        {
            queryBuildRange.value(queryValue(TTBInventGaugeTableCaller.InventGaugeId));
        }
        else if (formDataSource)
        {
            for (TTBInventGaugeTableCaller = formDataSource.getFirst(true) ? formDataSource.getFirst(true) : formDataSource.cursor();
            TTBInventGaugeTableCaller; TTBInventGaugeTableCaller = formDataSource.getNext())
            {
                if (maxRemaining == 0)
                {
                    warning(strFmt("@SYS114377", #maxRemaining));
                    break;
                }
                maxRemaining--;
                queryString = queryRangeConcat(queryString, queryValue(TTBInventGaugeTableCaller.InventGaugeId));
            }
            queryBuildRange.value(queryString);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static GGIGaugeRecord construct()
    {
        return new GGIGaugeRecord();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>SRSReportRun</c> class, assigns range values, and runs the
    ///    <c>SRSReportRun</c> object.
    /// </summary>
    /// <param name="_args">
    ///    The args object.
    /// </param>
    /// <remarks>
    ///    This method assigns the <c>GGIGaugeRecord</c> range with the specified values.
    /// </remarks>
    public static void main(Args _args)
    {
        GGIGaugeRecord controller = GGIGaugeRecord::newFromArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a new instance of the <c>GGIGaugeRecord</c> class.
    /// </summary>
    /// <param name="_args">
    /// Any input arguments.
    /// </param>
    /// <returns>
    /// A new instance of the <c>GGIGaugeRecord</c> class.
    /// </returns>
    public static GGIGaugeRecord newFromArgs(Args _args)
    {
        if (!_args || !(_args.record() is TTBInventGaugeTable))
        {
            throw(error(strFmt("@SYS113735", classStr(GGIGaugeRecord), tableStr(TTBInventGaugeTable))));
        }

        GGIGaugeRecord controller = GGIGaugeRecord::construct();
        controller.parmReportName(ssrsReportStr(GGIGaugeRecordReport, Report));
        controller.parmArgs(_args);
        return controller;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>