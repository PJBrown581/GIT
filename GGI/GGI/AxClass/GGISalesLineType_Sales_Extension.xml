<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGISalesLineType_Sales_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Sales Line Type Extenstion for Excise Tax 
/// </summary>
[ExtensionOf(classStr(SalesLineType_Sales))]
final class GGISalesLineType_Sales_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>inserted</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Handle an inserted record - Excise Tax
    /// </summary>
    public void inserted()
    {
        next inserted();
        if (this.salesLine.itemid)
        { 
            GGI_ExciseTaxManager exciseTaxManager = GGI_ExciseTaxManager::construct();
            exciseTaxManager.SalesOrder_SalesLineAdded(this.salesLine);
            FormObjectSet fos =  FormDataUtil::getFormDataSource(salesLine);
            if (fos)
            {
                fos.research(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updated</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Handle a updated record - Excise Tax
    /// </summary>
    /// <param name = "_salesLineOrig">
    ///     The record deleted
    /// </param>
    public void updated(SalesLine _salesLineOrig)
    {
        boolean valuesChanged = _salesLineOrig.ItemId != this.salesLine.ItemId || _salesLineOrig.SalesUnit != this.salesLine.SalesUnit || _salesLineOrig.SalesQty != this.salesLine.SalesQty;
        next updated(_salesLineOrig);
        if (this.salesLine.itemid && salesLine.SalesStatus != SalesStatus::Canceled && valuesChanged)
        {
            GGI_ExciseTaxManager exciseTaxManager = GGI_ExciseTaxManager::construct();
            exciseTaxManager.SalesOrder_SalesLineUpdated(this.salesLine);
            FormObjectSet fos =  FormDataUtil::getFormDataSource(salesLine);
            if (fos)
            {
                fos.research(true);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleted</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Handle a deleted record - Excise Tax
    /// </summary>
    public void deleted()
    {
        next deleted();
        if (this.salesLine.itemId)
        {
            // BUG 5914
            GGI_ExciseTaxManager::reCalcLineTax(this.salesTable.SalesId);
            FormObjectSet fos =  FormDataUtil::getFormDataSource(salesLine);
            if (fos)
            {
                fos.research(true);
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>