<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TTBInventGuageClosingProcedure</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The <c>TTBInventGuageClosingProcedure</c> handles the signing of the Dump and bottling record closing process.
/// </summary>
public class TTBInventGuageClosingProcedure extends SIGProcedureProvider
{
    TTBInventGaugeTable    inventGaugeTable;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getAuditFieldList</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Gets the list of signature controlled fields for this procedure.
    /// </summary>
    /// <returns>
    ///   The list of fields required for this bill of materials approve procedure.
    /// </returns>
    public container getAuditFieldList()
    {
        return [
                    [DatabaseLogType::Update, tablenum(TTBInventGaugeTable), fieldnum(TTBInventGaugeTable, Status)],
                    [DatabaseLogType::Update, tableNum(TTBInventGaugeTable), fieldNum(TTBInventGaugeTable, ClosedByWorker)],
                    [DatabaseLogType::Update, tableNum(TTBInventGaugeTable), fieldNum(TTBInventGaugeTable, ClosedDate)],
                    [DatabaseLogType::Update, tableNum(TTBInventGaugeTable), fieldNum(TTBInventGaugeTable, ClosedTime)]
        ];
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventGuageTable</Name>
				<Source><![CDATA[
    public TTBInventGaugeTable parmInventGuageTable(TTBInventGaugeTable _inventGaugeTable = null)
    {
        inventGaugeTable = _inventGaugeTable;

        return inventGaugeTable;
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Runs the actual signing of the documents.
    /// </summary>
    public void run()
    {
        super();

        if (signed)
        {   
            ttsbegin;
            inventGaugeTable.selectForUpdate(true);
            inventGaugeTable.update();
            ttscommit;     
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>sign</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Starts the signature process.
    /// </summary>
    /// <returns>
    ///   True if the documents of the provider have been signed; otherwise, false.
    /// </returns>
    public boolean sign()
    {
        SIGBaseDocument doc;

        docList = new List(Types::Class);
        inventGaugeTable = TTBInventGaugeTable::find(inventGaugeTable.InventGaugeId);
    
        if (inventGaugeTable.Status == TTBInventGaugeTableStatus::Open)
        {
            inventGaugeTable.Status          = TTBInventGaugeTableStatus::Closed;
            inventGaugeTable.ClosedByWorker  = HcmWorker::userId2Worker(curUserId());
            inventGaugeTable.ClosedDate      = DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone());
            inventGaugeTable.ClosedTime      = DateTimeUtil::getTimeNow(DateTimeUtil::getUserPreferredTimeZone());
        }

        doc = SIGBaseDocument::newDatabaseLog(DatabaseLogType::Update, inventGaugeTable);
        docList.addEnd(doc);

        return super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>signatureRequired</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Indicates whether the signature for this procedure is required.
    /// </summary>
    /// <returns>
    ///   True if the built-in signature requirement is enabled or one of the fields requires a signature.
    /// </returns>
    public boolean signatureRequired()
    {
        ClassId classId = classNum(TTBInventGuageClosingProcedure);
        boolean ret = SIGProcSetup::classIdEnabled(classId)
                      || SIGProcSetupField::classIdFieldsEnabled(classId);        
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setupAllowed</Name>
				<Source><![CDATA[
    /// <summary>
    ///   Indicates whether the procedure should be included in the signature setup.
    /// </summary>
    /// <returns>
    ///   Always returns true.
    /// </returns>
    public boolean setupAllowed()
    {
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static TTBInventGuageClosingProcedure construct()
    {
        return new TTBInventGuageClosingProcedure();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the descrription for the e-sig process.
    /// </summary>
    /// <returns>
    /// A descrription of the e-sig process.
    /// </returns>
    static public ClassDescription description()
    {
        return "@GGI:CloseGaugeRec";
    }

]]></Source>
			</Method>
			<Method>
				<Name>newIGR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates a new instance of the class from a TTBInventGaugeTable record.
    /// </summary>
    /// <param name = "_inventGaugeTable">
    /// The <c>TTBInventGaugeTable</c> record to be signed.
    /// </param>
    /// <returns>
    /// An instance of class <c>TTBInventGuageClosingProcedure</c>.
    /// </returns>
    public static TTBInventGuageClosingProcedure newIGR(TTBInventGaugeTable _inventGaugeTable)
    {
        TTBInventGuageClosingProcedure  procedure = TTBInventGuageClosingProcedure::construct();
        procedure.parmInventGuageTable(_inventGaugeTable);
        return procedure;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>