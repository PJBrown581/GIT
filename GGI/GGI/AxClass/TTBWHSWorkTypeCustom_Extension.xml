<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>TTBWHSWorkTypeCustom_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// TTB006_GaugeReport - TTB customizations for the <c>WHSWorkTypeCustom</c> table.
/// </summary>
[ExtensionOf(tableStr(WHSWorkTypeCustom))]
public final class TTBWHSWorkTypeCustom_Extension
{
    #define.TTBCustomWorkType('TTBCustomWorkType')
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>validateField</Name>
				<Source><![CDATA[
    /// <summary>
    ///		Validates the record upon change of fields to ensure TTB framework setup is correct
    /// </summary>
    /// <param name = "_fieldIdToCheck">
    ///		Id of field that is changed
    /// </param>
    /// <returns>
    ///		true if validation passes, false otherwise
    /// </returns>
    public boolean validateField(fieldId _fieldIdToCheck)
    {
        WHSWorkTypeCustom whsWorkTypeCustom;
        boolean     ret;

        ret = next validateField(_fieldIdToCheck);

        if (ret)
        {
            switch (_fieldIdToCheck)
            {
                case fieldNum(WHSWorkTypeCustom, CaptureData):
                    if (!this.CaptureData && this.TTBWHSCustomTypeMap)
                    {
                        ret = checkFailed("@GGI:TTB006_035");
                    }
                    break;
                case fieldNum(WHSWorkTypeCustom, TTBWHSCustomTypeMap):
                    if (this.TTBWHSCustomTypeMap)
                    {
                        if (!this.CaptureData)
                        {
                            ret = checkFailed("@GGI:TTB006_035");
                        }

                        if (this.WorkTypeCustomMethod != #TTBCustomWorkType)
                        {
                            ret = checkFailed("@GGI:TTB006_036");
                        }

                        select firstonly whsWorkTypeCustom
                            where whsWorkTypeCustom.TTBWHSCustomTypeMap == this.TTBWHSCustomTypeMap
                                && whsWorkTypeCustom.RecId != this.RecId;

                        if (whsWorkTypeCustom)
                        {
                            ret = checkFailed("@GGI:TTB006_037");
                        }
                    }
                    break;
            }
        }

        return ret;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>