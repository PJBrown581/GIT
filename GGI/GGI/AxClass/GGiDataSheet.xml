<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>GGiDataSheet</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>GGIDataSheet</c> class is a controller class for the
///    <c>GGiDataSheet</c> report.
/// </summary>
public class GGiDataSheet extends SrsReportRunController
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
        super();
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    protected void prePromptModifyContract()
    {
        this.setQueryRange();
    }

]]></Source>
			</Method>
			<Method>
				<Name>setQueryRange</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Sets ranges for a query for the <c>GGIDataSheet</c> report.
    /// </summary>
    private void setQueryRange()
    {
        FormDataSource  formDataSource = null;
        int             maxRemaining = 15;
        str             queryString;

        Query query = this.getFirstQuery();
        QueryBuildDataSource queryBuildDataSource = query.dataSourceTable(tableNum(GGIDataSheetTable));
        QueryBuildRange queryBuildRange = SysQuery::findOrCreateRange(queryBuildDataSource, fieldNum(GGIDataSheetTable,DataSheetId));
        GGIDataSheetTable GGIDataSheetTableCaller = this.parmArgs().record() as GGIDataSheetTable;

        if (FormDataUtil::getFormDataSource(GGIDataSheetTableCaller))
        {
            formDataSource = FormDataUtil::getFormDataSource(GGIDataSheetTableCaller);
        }
        if (!formDataSource && GGIDataSheetTableCaller.RecId)
        {
            queryBuildRange.value(queryValue(GGIDataSheetTableCaller.DataSheetId));
        }
        else if (formDataSource)
        {
            for (GGIDataSheetTableCaller = formDataSource.getFirst(true) ? formDataSource.getFirst(true) : formDataSource.cursor();
                    GGIDataSheetTableCaller; GGIDataSheetTableCaller = formDataSource.getNext())
            {
                if (maxRemaining == 0)
                {
                    warning(strFmt("@SYS114377", '15'));
                    break;
                }
                maxRemaining--;
                queryString = queryRangeConcat(queryString, queryValue(GGIDataSheetTableCaller.DataSheetId));
            }
            queryBuildRange.value(queryString);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static GGIDataSheet construct()
    {
        return new GGIDataSheet();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Initializes an instance of the <c>SRSReportRun</c> class, assigns range values, and runs the
    ///    <c>SRSReportRun</c> object.
    /// </summary>
    /// <param name="_args">
    ///    The args object.
    /// </param>
    /// <remarks>
    ///    This method assigns the <c>GGiDataSheet</c> range with the specified values.
    /// </remarks>
    public static void main(Args _args)
    {
        GGiDataSheet controller = GGiDataSheet::newFromArgs(_args);
        controller.startOperation();
    }

]]></Source>
			</Method>
			<Method>
				<Name>newFromArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Creates and initializes a new instance of the <c>GGiDataSheet</c> class.
    /// </summary>
    /// <param name="_args">
    /// Any input arguments.
    /// </param>
    /// <returns>
    /// A new instance of the <c>GGiDataSheet</c> class.
    /// </returns>
    public static GGiDataSheet newFromArgs(Args _args)
    {
        if (!_args || !(_args.record() is GGIDataSheetTable))
        {
            throw(error(strFmt("@SYS113735", classStr(GGiDataSheet), tableStr(GGIDataSheetTable))));
        }

        GGiDataSheet controller = GGiDataSheet::construct();
        controller.parmReportName(ssrsReportStr(GGIDataSheetReport, Report));
        controller.parmArgs(_args);
        return controller;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>