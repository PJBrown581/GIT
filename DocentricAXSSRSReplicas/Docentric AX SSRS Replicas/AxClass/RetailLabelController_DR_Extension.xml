<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>RetailLabelController_DR_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class RetailLabelController_DR_Extension is an extension of the class RetailLabelController.
/// </summary>
[ExtensionOf(classStr(RetailLabelController))]
final class RetailLabelController_DR_Extension
{
    private SRSPrintMediumType targetPrintDestination_DR;
    private boolean targetPrintDestinationStored_DR;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>storeTargetPrintDestination_DR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Sets the target print destination.
    /// </summary>
    /// <param name = "_targetPrintDestination">A target print destinations</param>
    public void storeTargetPrintDestination_DR(SRSPrintMediumType _targetPrintDestination)
    {
        targetPrintDestination_DR = _targetPrintDestination;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmTargetPrintDestinationStored_DR</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets the flag indicating whether the target print destination has beem set.
    /// </summary>
    /// <param name = "_targetPrintDestinationStored">Specifies whether the target print destination has been set</param>
    /// <returns>True if the target print destination has been set; otherwise false</returns>
    public boolean parmTargetPrintDestinationStored_DR(boolean _targetPrintDestinationStored = targetPrintDestinationStored_DR)
    {
        targetPrintDestinationStored_DR = _targetPrintDestinationStored;
        return targetPrintDestinationStored_DR;
    }

]]></Source>
			</Method>
			<Method>
				<Name>prePromptModifyContract</Name>
				<Source><![CDATA[
    /// <summary>ummary>
    /// Modifies the contract before the dialog is shown to the user.
    /// </summary>
    protected void prePromptModifyContract()
    {
        next prePromptModifyContract();

        this.parmShowDialog(true);

        SrsReportDataContract dataContract = this.parmReportContract();
        if (dataContract)
        {
            SRSPrintDestinationSettings printSettings = dataContract.parmPrintSettings();
            if (printSettings)
            {
                printSettings.printMediumType(targetPrintDestination_DR);
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmArgs</Name>
				<Source><![CDATA[
    /// <summary>
    /// Gets/Sets an instance of the Args class.
    /// </summary>
    /// <param name = "_args">An instance of the Args class</param>
    /// <returns>An instance of the Args class</returns>
    public Args parmArgs(Args _args)
    {
        Args args = next parmArgs(_args);

        if (!this.parmTargetPrintDestinationStored_DR())
        {
            this.parmTargetPrintDestinationStored_DR(true);
            SrsReportDataContract dataContract = this.parmReportContract();
            if (dataContract)
            {
                SRSPrintDestinationSettings printSettings = dataContract.parmPrintSettings();
                if (printSettings)
                {
                    this.storeTargetPrintDestination_DR(printSettings.printMediumType());
                }
            }
        }
        return args;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>