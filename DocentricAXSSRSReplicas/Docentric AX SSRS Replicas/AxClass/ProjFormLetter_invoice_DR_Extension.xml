<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>ProjFormLetter_invoice_DR_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// The class ProjFormLetter_invoice_DR_Extension is an extension of the class ProjFormLetter_invoice.
/// </summary>
[ExtensionOf(classStr(ProjFormLetter_invoice))]
final class ProjFormLetter_invoice_DR_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>printMgmtDocumentType</Name>
				<Source><![CDATA[
    /*** Compilable only for Platform9 and up. ***/

    /// <summary>
    /// Identifies the print management document type.
    /// </summary>
    /// <returns>
    /// The print management document type for project invoice.
    /// </returns>
    protected PrintMgmtDocumentType printMgmtDocumentType()
    {
        PrintMgmtDocumentType pmdt = next printMgmtDocumentType();
        
        if (isConfigurationkeyEnabled(configurationKeyNum(Project)))
        {
            PSAInvoiceFormats psaInvoiceFormat;
            ProjInvoiceJour projInvoiceJour = this.journal();
            if (projInvoiceJour)
            {
                psaInvoiceFormat = projInvoiceJour.PSAInvoiceFormats;
            }
            else
            {
                str projProposalId = this.projProposalId();
                ProjProposalJour projProposalJour = ProjProposalJour::find(projProposalId);
                psaInvoiceFormat = projProposalJour.PSAInvoiceFormats;
            }

            if (psaInvoiceFormat == PSAInvoiceFormats::ManageInvoice)
            {
                pmdt = PrintMgmtDocumentType::SIManagedProjInvoice;

                //if (this.isBillingRule())
                //{
                //    pmdt = PrintMgmtDocumentType::SIManagedProjInvoiceWithBR;
                //}
            }
            else if (this.isRelease())
            {
                pmdt = PrintMgmtDocumentType::ProjCustRetentionReleaseInvoice;
            }
            else
            {
                switch (psaInvoiceFormat)
                {
                    case PSAInvoiceFormats::SummaryByCategory :
                        pmdt = PrintMgmtDocumentType::SIProjInvoice;
                        break;

                    case PSAInvoiceFormats::DetailInvoice :
                    case PSAInvoiceFormats::SummaryInvoice :
                        if (this.isBillingRule())
                        {
                            pmdt = PrintMgmtDocumentType::SIProjInvoiceWithBR;
                        }
                        else
                        {
                            pmdt = PrintMgmtDocumentType::SIProjInvoice;
                        }
                        break;

                    default :
                        pmdt = PrintMgmtDocumentType::SIProjInvoice;
                        break;
                }
            }
        }
        else
        {
            pmdt = PrintMgmtDocumentType::ProjectInvoice;
        }

        return pmdt;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>