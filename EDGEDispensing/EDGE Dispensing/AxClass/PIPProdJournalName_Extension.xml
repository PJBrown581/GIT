<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPProdJournalName_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(ProdJournalName))]
public final class PIPProdJournalName_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipUsedInAnyParameter</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Gets a value indicating if this production journal name is used in any production control parameters.
    /// </summary>
    /// <returns>
    ///    true if this production journal name is used in at least one production control parameter record; otherwise, false.
    /// </returns>
    public boolean pipUsedInAnyParameter()
    {
        return (select ProdParametersDim
            where ProdParametersDim.BOMJournalNameId            == this.JournalNameId
                || ProdParametersDim.PIPDispensingJournalNameId == this.JournalNameId).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipLookupJournalName</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Creates a lookup for the journal name.
    /// </summary>
    /// <param name="_control">
    ///    The form string control that is requesting the lookup.
    /// </param>
    /// <param name="_journalType">
    ///    The journal type to filter the journal names on.
    /// </param>
    /// <param name="_dispensing">
    ///    Determines if the journal name shown should be for a dispensing controlled journal.
    /// </param>
    /// <remarks>
    ///    This lookup shouldn't be necessary as the relations on the table should be sufficient, but the kernal seems to have a bug
    ///    when there are multiple fixed relations and doesn't properly limit the rows shown using all the relations.
    /// </remarks>
    public static void pipLookupJournalName(FormStringControl _control, ProdJournalType _journalType, PIPIsDispensingJournal _dispensing = NoYes::No)
    {
        SysTableLookup          tableLookup = SysTableLookup::newParameters(tableNum(ProdJournalName), _control);
        Query                   query       = new Query();
        QueryBuildDataSource    dsJournalName;
    
        dsJournalName = query.addDataSource(tableNum(ProdJournalName));
        dsJournalName.addRange(fieldNum(ProdJournalName, JournalType)).value(queryValue(_journalType));
        if (#PIPDispensingEnabled && _journalType == ProdJournalType::Picklist)
        {
            dsJournalName.addRange(fieldNum(ProdJournalName, PIPIsDispensingJournal)).value(queryValue(_dispensing));
        }
    
        tableLookup.addLookupfield(fieldNum(ProdJournalName, JournalNameId));
        tableLookup.addLookupfield(fieldNum(ProdJournalName, Description));
    
        tableLookup.parmQuery(query);
        tableLookup.performFormLookup();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>