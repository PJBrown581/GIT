<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPSIGJournalSigningProvider</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///    The <c>PIPSIGJournalSigningProvider</c> class provides definitions and values for
///    the production journal signature provider.
/// </summary>
class PIPSIGJournalSigningProvider extends SIGSigningProvider_SQLServer
{
    SIGPassphrase       passphrase;
    PIPSIGPrintedName   printedName;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>signDataList</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Signs a list of documents with the specified certificate.
    /// </summary>
    /// <param name="_documentList">
    ///    The list of documents to sign.
    /// </param>
    /// <param name="_id">
    ///    The <c>CertificateId</c> value of the certificate to use to sign the document.
    /// </param>
    /// <returns>
    ///    true if documents were signed; otherwise, false.
    /// </returns>
    /// <exception cref="M:Exception::Error">
    ///    The <paramref name="_id" /> parameter could not be converted to an integer.
    /// </exception>
    /// <remarks>
    ///     When posting a journal, the time that a signature is obtained from a user is not the time the journal is posted. There is a delay. Therefore, this method is to be called twice. The
    ///     first time is to get the password and printed name from the user. The second time is when the journal is actually posted. At that time, the previously obtained password/printed name will
    ///     be applied to creating a signature of the journal.
    /// </remarks>
    public boolean signDataList(List _documentList, SIGCertificateId _id)
    {
        SIGPassphraseDialog dialog;
        SIGSignableDocument document;
        ListEnumerator      iterator;
        boolean             ret = false;
        str                 signature;
    
        if (!str2IntOk(_id))
        {
            throw error("@SYS138112");
        }
    
        if (!passphrase)
        {
            dialog = SIGPassphraseDialog::construct();
            if (dialog.prompt())
            {
                // Immediately attempt to sign a test value to ensure the passphrase is valid
                signature = provider.signDataSQL('Test value', _id, dialog.getPassphrase());
                if (signature)
                {
                    passphrase  = dialog.getPassphrase();
                    printedName = dialog.getPrintedName();
                    ret = true;
                }
            }
        }
        else
        {
            iterator = _documentList.getEnumerator();
            ret = true;
            while (ret && iterator.moveNext())
            {
                document = iterator.current();
                signature = provider.signDataSQL(document.getSignableDocument(), _id, passphrase);
                if (signature)
                {
                    document.parmSignature(signature);
                    document.parmSigningCertificateId(_id);
                    document.parmSignatureDateTime(DateTimeUtil::getSystemDateTime());
                    document.setSigningProviderId(classidget(this));

                    // Note: The original implementation we added setPrintedName to the SIGSignableDocument interface but with extensions this is not possible; currently (as of AX7 U3) the only known implementation is SIGBaseDocument which we have extended with the new setter method
                    SIGBaseDocument baseDoc = document as SIGBaseDocument;
                    if (baseDoc)
                    {
                        baseDoc.setPrintedName(printedName);
                    }
                }
                else
                {
                    ret = false;
                }
            }
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PIPSIGJournalSigningProvider construct()
    {
        return new PIPSIGJournalSigningProvider();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>