<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPCOAInventQualityOrderLine_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(InventQualityOrderLine))]
public final class PIPCOAInventQualityOrderLine_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipCopyCOACustReq</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Copies all customer specific Certificate of analysis requirements from the related <c>InventTestGroupMember</c> record.
    /// </summary>
    /// <remarks>
    ///    The copies are marked as related to this record.
    /// </remarks>
    public void pipCopyCOACustReq()
    {
        InventTestGroupMember inventTestGroupMember = this.inventTestGroupMember();
    
        if (inventTestGroupMember)
        {
            PIPInventTestCertOfAnalysisCustReq::copyFromTestGroupMember(inventTestGroupMember, this);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipRemoveCustSpecificCOAReq</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Deletes all customer-specific COA requirements attached to current record.
    /// </summary>
    public void pipRemoveCustSpecificCOAReq()
    {
        PIPInventTestCertOfAnalysisCustReq inventTestCOACustReq;
    
        ttsBegin;
        delete_from inventTestCOACustReq
            where inventTestCOACustReq.RefTableId   == tableNum(InventQualityOrderLine)
                && inventTestCOACustReq.RefRecId    == this.RecId;
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>copyDocuments</Name>
				<Source><![CDATA[
    // TODO revert when kernel fixed; this should execute inside the insert() method transaction but in PU20/PU21 this cause skipDataMethods(true)+insert_recordset to execute the insert() method but that cause duplicate record errors
    /// <summary>
    ///
    /// </summary>
    /// <param name="sender"></param>
    /// <param name="e"></param>
    //[DataEventHandler(tableStr(InventQualityOrderLine), DataEventType::Inserted)]
    //public static void InventQualityOrderLine_onInserted(Common sender, DataEventArgs e)
    //{
    //    InventQualityOrderLine record = sender as InventQualityOrderLine;

    //    if (#PIPCustSpecificCOAEnabled)
    //    {
    //        record.pipCopyCOACustReq();
    //    }
    //}
    /// <summary>
    ///
    /// </summary>
    /// <param name="args"></param>
    /// 
    public void copyDocuments()
    {
        if (#PIPCustSpecificCOAEnabled)
        {
            this.pipCopyCOACustReq();
        }

        next copyDocuments();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifiedFieldPost</Name>
				<Source><![CDATA[
    [PostHandlerFor(tableStr(InventQualityOrderLine), tableMethodStr(InventQualityOrderLine, modifiedField))]
    /// <summary>
    ///     Executes logic in the post event for the <c>InventQualityOrderLine</c> <see cref="modifiedField method"/> method.
    /// </summary>
    /// <param name="_args">
    ///     The arguments passed by the event hanlder.
    /// </param>
    public static void modifiedFieldPost(XppPrePostArgs _args)
    {
        InventQualityOrderLine  inventQualityOrderLine  = _args.getThis();
        InventQualityOrderLine  qualityOrderLineOrig    = inventQualityOrderLine.orig();
        FieldId                 fieldId                 = _args.getArg('_fieldId');
    
        switch (fieldId)
        {
            case fieldNum(InventQualityOrderLine, CertificateOfAnalysisReport):
                if (!inventQualityOrderLine.CertificateOfAnalysisReport && PIPInventTestCertOfAnalysisCustReq::existReference(inventQualityOrderLine))
                {
                    if (Box::okCancel("@PIP1006", DialogButton::Cancel) == DialogButton::Ok)
                    {
                        inventQualityOrderLine.pipRemoveCustSpecificCOAReq();
                    }
                    else
                    {
                        inventQualityOrderLine.CertificateOfAnalysisReport = NoYes::Yes;
                    }
                }
                break;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>