<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPCustSpecificCOAAssignmentDataService</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     The <c>PIPCustSpecificCOAAssignmentDataService</c> class creates or updates customer-specific COA
///     requirements for all test group members as determined by user selection.
/// </summary>
class PIPCustSpecificCOAAssignmentDataService
{
    InventTestTable                     inventTestTable;
    InventTestGroupMember               inventTestGroupMember;

    TableGroupAll                       customerCode;
    CustVendRel                         customerRelation;
    boolean                             excludeFromCOA;
    boolean                             useCustBatchAttribRange;
    boolean                             testGroupsUpdated;
    PIPReplacePassResultsOnCOA          replacePassResult;
    PIPReplaceFailResultsOnCOA          replaceFailResult;
    boolean                             suppressMinMaxValues;
    boolean                             hasBatchAttrib;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>assignCOARequirements</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Updates or creates customer specific COA requirements.
    ///     requirements.
    /// </summary>
    /// <param name="_custSpecificCOAAssignmentDataContract">
    ///     An instance of the <c>PIPCustSpecificCOAAssignmentDataContract</c> class.
    /// </param>
    /// <exception cref="Exception::Error">
    ///     Thrown when no records can be determined from user selection.
    /// </exception>
    public void assignCOARequirements(PIPCustSpecificCOAAssignmentDataContract _custSpecificCOAAssignmentDataContract)
    {
        QueryRun                            queryRun;
        PIPInventTestCertOfAnalysisCustReq  inventTestCOACustReq;
    
        TableId                             tableIdTestGrpMember = tableNum(InventTestGroupMember);
    
        queryRun = new queryRun(_custSpecificCOAAssignmentDataContract.getQuery());
    
        if (!SysQuery::countTotal(queryRun))
        {
            throw error("@PIP772");
        }
    
        customerCode                = _custSpecificCOAAssignmentDataContract.parmCustomerCode();
        customerRelation            = _custSpecificCOAAssignmentDataContract.parmCustomerRelation();
        excludeFromCOA              = _custSpecificCOAAssignmentDataContract.parmExcludeFromCOA();
        useCustBatchAttribRange     = _custSpecificCOAAssignmentDataContract.parmUseCustBatchAttribRange();
        replacePassResult           = _custSpecificCOAAssignmentDataContract.parmReplacePassResult();
        replaceFailResult           = _custSpecificCOAAssignmentDataContract.parmReplaceFailResult();
        suppressMinMaxValues        = _custSpecificCOAAssignmentDataContract.parmSupressMinMaxValues();
    
        ttsBegin;
        while(queryRun.next())
        {
            inventTestGroupMember   = queryRun.get(tableNum(InventTestGroupMember));    
            inventTestCOACustReq    = PIPInventTestCertOfAnalysisCustReq::findReqForGrpMemberAndCustomer(customerCode, customerRelation, inventTestGroupMember, true);    
            hasBatchAttrib          = (inventTestGroupMember.PdsBatchAttribId)? true : false;
    
            if (inventTestCOACustReq)
            {
                this.updateCOAReq(inventTestCOACustReq);
            }
            else
            {
                this.createCOAReq();
            }
           
            testGroupsUpdated = true;
        }
        ttsCommit;
    
        if (testGroupsUpdated)
        {
            info("@PIP1010");
        }
        else
        {
            info("@PIP1011");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>createCOAReq</Name>
				<Source><![CDATA[
    private void createCOAReq()
    {
        PIPInventTestCertOfAnalysisCustReq coaCustReq;
    
        coaCustReq.RefRecId                 = inventTestGroupMember.RecId;
        coaCustReq.RefTableId               = inventTestGroupMember.TableId;
        coaCustReq.CustomerCode             = customerCode;
        coaCustReq.CustomerRelation         = customerRelation;
        coaCustReq.ExcludeFromCOA           = excludeFromCOA;
        coaCustReq.UseCustBatchAttribRange  = useCustBatchAttribRange && hasBatchAttrib;
        coaCustReq.SupressMinMaxValues      = suppressMinMaxValues;
        coaCustReq.ReplacePassResults       = replacePassResult;
        coaCustReq.ReplaceFailResults       = replaceFailResult;
    
        coaCustReq.insert();
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateCOAReq</Name>
				<Source><![CDATA[
    private void updateCOAReq(PIPInventTestCertOfAnalysisCustReq _coaCustReq)
    {
        _coaCustReq.RefRecId                 = inventTestGroupMember.RecId;
        _coaCustReq.RefTableId               = inventTestGroupMember.TableId;
        _coaCustReq.CustomerCode             = customerCode;
        _coaCustReq.CustomerRelation         = customerRelation;
        _coaCustReq.ExcludeFromCOA           = excludeFromCOA;
        _coaCustReq.UseCustBatchAttribRange  = useCustBatchAttribRange && hasBatchAttrib;
        _coaCustReq.SupressMinMaxValues      = suppressMinMaxValues;
        _coaCustReq.ReplacePassResults       = replacePassResult;
        _coaCustReq.ReplaceFailResults       = replaceFailResult;
    
        _coaCustReq.update();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>