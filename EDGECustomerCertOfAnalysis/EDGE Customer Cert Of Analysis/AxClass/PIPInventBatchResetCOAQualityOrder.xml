<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPInventBatchResetCOAQualityOrder</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
///     Builds the dialog for resetting the COA quality order reference on the selected <c>InventBatch</c> record.
/// </summary>
class PIPInventBatchResetCOAQualityOrder extends RunBase
{
    Dialog                  dialog;
    InventBatch             inventBatch;
    InventQualityOrderId    qualityOrderId;

    DialogField             qualityOrderField;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        dialog = super();
    
        dialog.caption("@PIP1004");
        dialog.allowUpdateOnSelectCtrl(true);
    
        qualityOrderField = dialog.addFieldValue(extendedTypeStr(InventQualityOrderId), qualityOrderId, "@PIP991");
    
        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialogPostRun</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Registers overide methods.
    /// </summary>
    /// <param name="_dialog">
    ///     An instance of the <c>DialogRunBase</c> class.
    /// </param>
    public void dialogPostRun(DialogRunbase _dialog)
    {
        super(_dialog);
    
        qualityOrderField.registerOverrideMethod('lookup', 'qualityOrderLookup', this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFromDialog</Name>
				<Source><![CDATA[
    public boolean getFromDialog()
    {
        boolean ret;
    
        ret = super();
    
        qualityOrderId = qualityOrderField.value();
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>parmInventBatch</Name>
				<Source><![CDATA[
    public InventBatch parmInventBatch(InventBatch _inventBatch = inventBatch)
    {
        inventBatch = _inventBatch;
        return inventBatch;
    }

]]></Source>
			</Method>
			<Method>
				<Name>qualityOrderLookup</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Overrides the lookup method on quality order Id field.
    /// </summary>
    /// <param name="_callerControl">
    ///     A reference to the caller control.
    /// </param>
    public void qualityOrderLookup(Object _callerControl)
    {
        SysTableLookup          sysTableLookup      = SysTableLookup::newParameters(tableNum(InventQualityOrderTable),_callerControl);
        Query                   query               = new Query();
        QueryBuildDataSource    qbdsQualityOrder;
        QueryBuildDataSource    qbdsInventDim;
    
        sysTableLookup.addLookupfield(fieldNum(InventQualityOrderTable,QualityOrderId));
    
        qbdsQualityOrder    = query.addDataSource(tableNum(InventQualityOrderTable));
        qbdsInventDim       = qbdsQualityOrder.addDataSource(tableNum(InventDim));
        qbdsInventDim.joinMode(JoinMode::ExistsJoin);
        qbdsInventDim.relations(true);
    
        qbdsQualityOrder.addRange(fieldNum(InventQualityOrderTable, ItemId)).value(inventBatch.itemId);
        qbdsInventDim.addRange(fieldNum(InventDim, InventBatchId)).value(inventBatch.inventBatchId);
    
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        inventBatch.selectForUpdate(true);
    
        ttsBegin;
        inventBatch.PIPCertOfAnalysisQualityOrderId = qualityOrderId;
        inventBatch.update();
        ttsCommit;
    }

]]></Source>
			</Method>
			<Method>
				<Name>validate</Name>
				<Source><![CDATA[
    public boolean validate(Object calledFrom = null)
    {
        boolean ret;
    
        ret = super(calledFrom);
    
        if (!inventBatch)
        {
            ret = checkFailed(strFmt("@SYS134996",tableStr(InventBatch)));
        }
    
        return ret;
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static PIPInventBatchResetCOAQualityOrder construct()
    {
        return new PIPInventBatchResetCOAQualityOrder();
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args args)
    {
        InventBatch                         inventBatch;
        PIPInventBatchResetCOAQualityOrder  inventBatchResetCOAQualityOrder = new PIPInventBatchResetCOAQualityOrder();
    
        if (args.dataset() != tableNum(InventBatch))
        {
            throw error(error::missingRecord(tableStr(InventBatch)));
        }
    
        inventBatch = args.record();
    
        inventBatchResetCOAQualityOrder.parmInventBatch(inventBatch);
    
        if (inventBatchResetCOAQualityOrder.prompt())
        {
            inventBatchResetCOAQualityOrder.runOperation();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>canRunInNewSession</Name>
				<Source><![CDATA[
    protected boolean canRunInNewSession()
    {
        return false;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>