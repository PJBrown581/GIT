<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>PIPEBRInventQualityOrderTable_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(InventQualityOrderTable))]
public final class PIPEBRInventQualityOrderTable_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>pipExistByReferenceDimension</Name>
				<Source><![CDATA[
    /// <summary>
    ///     Determines whether any record in the <c>InventQualityOrderTable</c> table exists
    ///     based on the item and inventory dimension references.
    /// </summary>
    /// <param name="_inventDim">
    ///     The <c>InventDim</c> table record to be compared to.
    /// </param>
    /// <param name="_itemId">
    ///     The item ID on the quality order to find.
    /// </param>
    /// <returns>
    ///     true if the specified record exists; otherwise, false.
    /// </returns>
    public static boolean pipExistByReferenceDimension(InventDim _inventDim, ItemId _itemId)
    {
        InventQualityOrderTable inventQualityOrderTable;
        InventDim               inventDim;
    
        if (_itemId && (_inventDim.inventBatchId || _inventDim.inventSerialId))
        {
            select firstOnly RecId from inventQualityOrderTable
                where inventQualityOrderTable.ItemId    == _itemId
                join RecId from inventDim
                where inventDim.configId                == _inventDim.configId
                    && inventDim.InventSizeId           == _inventDim.InventSizeId
                    && inventDim.InventColorId          == _inventDim.inventColorId
                    && inventDim.InventStyleId          == _inventDim.InventStyleId
                    && inventDim.inventBatchId          == _inventDim.inventBatchId
                    && inventDim.inventSerialId         == _inventDim.inventSerialId;
        }
    
        return inventQualityOrderTable.RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipExistByReferenceProduction</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the specified record in the <c>InventQualityOrderTable</c> table exists.
    /// </summary>
    /// <param name="_inventRefId">
    ///    The reference number on the quality order to find.
    /// </param>
    /// <param name="_itemId">
    ///    The item ID on the quality order to find.
    /// </param>
    /// <returns>
    ///    true if the specified record exists; otherwise, false.
    /// </returns>
    public static boolean pipExistByReferenceProduction(InventRefId _inventRefId, ItemId _itemId)
    {
        return _inventRefId
            && _itemId
            && (select firstonly inventQualityOrderTable
                    where inventQualityOrderTable.InventRefId   == _inventRefId
                        &&  inventQualityOrderTable.ItemId      == _itemId).RecId != 0;
    }

]]></Source>
			</Method>
			<Method>
				<Name>pipExistByReferenceProductionRAF</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Determines whether the specified record in the <c>InventQualityOrderTable</c> table exists.
    /// </summary>
    /// <param name="_inventRefId">
    ///    The reference number on the quality order to find.
    /// </param>
    /// <param name="_itemId">
    ///    The item ID on the quality order to find.
    /// </param>
    /// <returns>
    ///    true if the specified record exists; otherwise, false.
    /// </returns>
    public static boolean pipExistByReferenceProductionRAF(InventRefId _inventRefId, ItemId _itemId)
    {
        return _inventRefId
            && _itemId
            && (select firstonly inventQualityOrderTable
                    where (inventQualityOrderTable.InventRefId   == _inventRefId
                            || InventQualityOrderTable.ReferenceType == InventTestReferenceType::Inventory)
                        &&  inventQualityOrderTable.ItemId      == _itemId).RecId != 0;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>