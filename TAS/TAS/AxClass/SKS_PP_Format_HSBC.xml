<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_HSBC</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_HSBC Extends SKS_PP_Format_ChkType
{
    int mChk_Count;
    real mChk_Amt;
    SKS_PP_Bank mSKS_PP_Bank;
    BankAccount mBankAcct;
    // HSBC BANK FORMAT
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    // Pos 01-13    13       Bank Number/Account - RJ-LZF
    // Pos 14-23    10       Check No. - RJ-LZF
    // Pos 24-33    10       Check Amount - LZF -
    // Pos 34-41    8        Issue Date (MMDDYYYY)
    // Pos 42-49    8        Additional data 1
    // Pos 50-54    5        Additional data 2
    // Pos 55-55    1        Void Indicator 'V'
    // Pos 56-80    25       Blank
    protected str txtDetail()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str ItemType;
        int Mult = 1;
        ;
    
        // Convert negative void numbers to positives
        if (mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = 'V';
            if (mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = ' ';
        }
    
        if(mSKS_PP_Bank.BankAccountId != mSKS_PP_FileDetail.ChkBankAcct)
            mSKS_PP_Bank = SKS_PP_Bank::find(mSKS_PP_FileDetail.ChkBankAcct, mSKS_PP_FileDetail.ChkCpnyId);
    
        mChk_Count++;
        mChk_Amt = mChk_Amt + mSKS_PP_FileDetail.ChkCuryAmt;
        mBankAcct = mSKS_PP_FileDetail.BnkBankAcct; // Used in trailer
    
        sb.Append(SKS_PP_StringFormats::strFill(4,mSKS_PP_Bank.PP01,SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(9,strRtrim(strLtrim(mSKS_PP_FileDetail.BnkBankAcct)),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10,strRtrim(strLtrim(mSKS_PP_FileDetail.ChkRefNbr)),SKS_PP_FillCharacter::Zero,SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 10));
        sb.Append(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append(substr(SKS_PP_StringFormats::strFill(13,strRtrim(strLtrim(mSKS_PP_FileDetail.vendname)),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right),0,13));
        sb.Append(ItemType);
        sb.Append(SKS_PP_StringFormats::strFill(25,'',SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Left));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    // HSBC BANK FORMAT
    
    // Trailer Record
    // Pos 01-01    1        Trailer Record Indicator
    // Pos 02-14    13       Bank Number/Account - RJ-LZF
    // Pos 15-19    5        Spaces
    // Pos 20-29    10       Total Items (RJ-LZF)
    // Pos 30-34    5        Spaces
    // Pos 35-46    12       Total Dollar Amount (RJ-LZF)
    // Pos 47-80    34       Spaces
    protected str txtGroupTrailer()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('T');
        sb.Append(SKS_PP_StringFormats::strFill(4,mSKS_PP_Bank.PP01,SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(9,mBankAcct,SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(5,'',SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10,int2str(mChk_Count),SKS_PP_FillCharacter::Zero,SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(5,'',SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(mChk_Amt, 12));
        sb.Append(SKS_PP_StringFormats::strFill(34,'',SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Left));
    
        mChk_Count = 0;
        mChk_Amt = 0;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>