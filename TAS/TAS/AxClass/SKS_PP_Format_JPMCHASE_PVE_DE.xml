<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_JPMCHASE_PVE_DE</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_JPMCHASE_PVE_DE Extends SKS_PP_Format_BankGroup
{
    int mChkCount;
    real mChkSum;
    // Detail record
        // JPMChase - PVE
        // Courtesy of:Premier Retail Networks
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    // Pos 01-10    10       Account number - LJ-RBF
        // Pos 11-20    10       Check number - RJ-LZF
        // Pos 21-30    10       Amount (zeroes for void)
        // Pos 31-36    6        Issue date (mmddyy)
        // Pos 37-37    1        Transaction code (2-Cancel/Void,S=Stop, blank-Issue)
        // Pos 38-52    15       Vendor ID(customer usage field)
        // Pos 53-102   50       Payee Name
    protected str txtDetail()
    {
        int Mult = 1;
        str ItemType;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = '2';
            if(mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = ' ';
        mChkCount++;
        mChkSum = mChkSum + mSKS_PP_FileDetail.ChkCuryAmt;
        }
    
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(strRTrim(StrLTrim(SKS_PP_StringFormats::strFill(10, strRem(Num2Str(mSKS_PP_FileDetail.ChkCuryAmt * Mult,1, 2, 1,0),'.'), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left))));
        sb.Append(SKS_PP_StringFormats::strDateMMDDYY(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append(ItemType);
        sb.Append(SKS_PP_StringFormats::strFill(15, mSKS_PP_FileDetail.VendorAccount, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(50, mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupHeader</Name>
				<Source><![CDATA[
    // Header record
        // JPMChase - PVE
        // Courtesy of:Premier Retail Networks
    
        // Pos 01-03    3        Record ID - HDR
        // Pos 04-13    10       Account number - (RJLZF)
        // Pos 14-102   89       Spaces
    protected str txtGroupHeader()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('HDR');
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(89, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    // Trailer record
        // JPMChase - PVE
        // Courtesy of:Premier Retail Networks
    
        // Pos 01-03     3       Record ID - EOF
        // Pos 04-15     12      Total Issue Amount (Issues only)
        // Pos 16-23     8       Total Issue Count (Issues only)
        // Pos 24-102    79      Filler
    protected str txtGroupTrailer()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('EOF');
        sb.Append(SKS_PP_StringFormats::strMoney(mChkSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(8, int2str(mChkCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(79, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        mChkSum = 0;
        mChkCount = 0;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>