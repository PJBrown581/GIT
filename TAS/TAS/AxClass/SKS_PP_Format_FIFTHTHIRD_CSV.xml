<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_FIFTHTHIRD_CSV</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_FIFTHTHIRD_CSV  extends SKS_PP_Format_BankGroup
{
    #define.Separator(',')
    #define.DoubleQuote('"')
        
    int chkCount;
    AmountMST mCheckAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        //Fifth Third Bank Detail Spec
        //  Record Type
        //  Routing Number
        //  Account Number
        //  Serial/Check Number
        //  Issue Date/Void/ Stop Date For Check
        //  Amount
        //  Payee Name
        //  Additional Data
        
        str     itemType;
        int     mult = 1;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        
        if (mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            itemType = 'V';
            if(mSKS_PP_FileDetail.chkcuryamt < 0)
            {
                mult = -1;
            }
        }
        else
        {
            itemType = 'I';
        }
        mCheckAmount = mCheckAmount + mSKS_PP_FileDetail.ChkCuryAmt;
        
        sb.Append(this.wrapField(itemType));
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.BnkBankTransit, 1, 9)));
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.BnkBankAcct, 1, 10)));
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.ChkRefNbr, 1, 10)));
        sb.Append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_PP_FileDetail.ChkDocDate, DateSeparator::Slash)));
        sb.Append(this.wrapField(num2str(mSKS_PP_FileDetail.ChkCuryAmt * mult, 0, 2, 1, 0)));
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.VendName, 1, 80), true));
        sb.Append(this.wrapField('', false, false));
        
        chkCount++;
        
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    public str txtGroupTrailer()
    {
        //  Record Type
        //  Comma Separation
        //  Number Of Check For This File
        //  Comma Separation
        //  File Date
        //  Amount Of Check For This File
        
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        sb.Append(this.wrapField('T'));
        sb.Append(this.wrapField(''));
        sb.Append(this.wrapField(num2str(chkCount, 0, 0, 0, 0)));
        sb.Append(this.wrapField(''));
        sb.Append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone()), DateSeparator::Slash)));
        sb.Append(this.wrapField(num2str(mCheckAmount, 0, 2, 1, 0), false, false));
        
        chkCount = 0;
        mCheckAmount = 0;
        
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '',  boolean _Quote = false, boolean _addSeparator = true)
    {
        if(_addSeparator)
        {
            if(_Quote && _field && strFind(_field, ',', 1, strLen(_field)))
            {
                return #DoubleQuote + _field + #DoubleQuote + #Separator;
            }
            else
            {
                return _field + #Separator;
            }
        }
        else
        {
            if(_Quote && _field != '')
            {
                return #DoubleQuote + _field + #DoubleQuote;
            }
            else
            {
                return _field;
            }
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>