<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_US_BOA_PAYMODEX_FIXED</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_US_BOA_PAYMODEX_FIXED extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_US_BOA_PAYMODEX_FIXED extends SKS_EFT_Format
{
    #define.FormatVersion('20220614')
    int mPaymentCount;
    int mRemittanceCount;
    Amount mPaymentAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckNestedLoopinCode","Selecting a local table buffer based on a global table buffer attribute)")]
    protected void formatStructure()
    {
        SKS_EFT_FileDetail invSKS_EFT_FileDetail;
    
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_Bank
            this.getSKS_EFT_Bank();
    
            // Get SKS_EFT_VendorPaym
            this.getSKS_EFT_VendorPaym();
    
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();
    
            // Get Remittance Address
            this.getRemittanceAddress(mSKS_EFT_FileFormatPaymSpec.UseCheckNumber);
    
            //Payment Data
            this.rec_PM();
    
            //Remittance Data
            while select invSKS_EFT_FileDetail where invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
            {
                this.rec_REM(invSKS_EFT_FileDetail);
            }
            next mSKS_EFT_FileDetail;
        }
    
        //Control total Data
        this.rec_CD();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInvoiceJournalPaymentId</Name>
				<Source><![CDATA[
    private str getInvoiceJournalPaymentId(SKS_EFT_FileDetail _invSKS_EFT_FileDetail)
    {
        LedgerJournalTrans ledgerJournalTrans;
    
        select crossCompany PaymId from ledgerJournalTrans where
            ledgerJournalTrans.VendTransId == _invSKS_EFT_FileDetail.VendTransRecId;
    
        return ledgerJournalTrans.PaymId;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_CD</Name>
				<Source><![CDATA[
    boolean rec_CD()
    {
        //------------------------------------------------------------------------------------------------------
        //Each Field separated by pipe Delimiter
        //Nbr   Description                     Position    Length
        // 1    Record type                         1- 7         7
        // 2    Total Payment Amount                8-19        12
        // 3    Total Number of Payments           20-28         9
        // 4    Total Number of Remittances        29-40        12
        // 5    Filler                             41-43         3
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
    
        // 1    Record type                         1- 7         7
        sb.Append('GAP700C');
    
        // 2    Total Payment Amount                8-19        12
        sb.Append(SKS_PP_StringFormats::strFill(12, num2str(-1 * mPaymentAmount,0,2,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 3    Total Number of Payments           20-28         9
        sb.Append(SKS_PP_StringFormats::strFill(9, int2str(mPaymentCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 4    Total Number of Remittances        29-40        12
        sb.Append(SKS_PP_StringFormats::strFill(12, int2str(mRemittanceCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 5    Filler                             41-43         3
        sb.Append(SKS_PP_StringFormats::strFill(3, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_PM</Name>
				<Source><![CDATA[
    boolean rec_PM()
    {
        //------------------------------------------------------------------------------------------------------
        //Each Field separated by pipe delimiter
        //Nbr   Description               Position     Length
        // 1    Record type                  1-  7          7
        // 2    Payment type                 8- 10          3
        // 3    Disbursing Paymode-X ID     11- 35         25
        // 4    Transit Number              36- 44          9
        // 5    Bank Account #              45- 61         17
        // 6    Vendor Account ID           62- 86         25
        // 7    Check Number                87-107         21
        // 8    Check Amount               108-118         11
        // 9    Vendor Name 1              119-153         35
        //10    Vendor Number              154-163         10
        //11    Vendor Address 1           164-193         30
        //12    Vendor Address 2           194-223         30
        //13    Vendor Address 3           224-253         30
        //14    Vendor City                254-270         17
        //15    Vendor State               271-272          2
        //16    Vendor Zip                 273-281          9
        //17    Vendor Country             282-302         21
        //18    Divert Indicator           303-307          5
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str checknum = '';
    
        mPaymentCount++;
    
        // 1    Record type                  1-  7          7
        sb.Append('GAP700P');
    
        // 2    Payment type                 8- 10          3
        if(mSKS_EFT_FileFormatPaymSpec.UseCheckNumber)
        {
            sb.Append('CK ');
        }
        else
        {
            sb.Append('AC ');
        }
    
        // 3    Disbursing Paymode-X ID     11- 35         25
        sb.Append(SKS_PP_StringFormats::strFill(25, this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('PaymodexId', mSKS_EFT_FileDetail.RecId, SKS_EFT_CodeLookupType::CodeLookupBank)), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 4    Transit Number              36- 44         9
        sb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 5    Bank Account #              45- 61         17
        sb.Append(SKS_PP_StringFormats::strFill(17, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 6    Vendor Account ID           62- 86         25
        sb.Append(SKS_PP_StringFormats::strFill(25, this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('CollctngPaymodexId', mSKS_EFT_FileDetail.RecId)), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 7    Check Number                87-107         21
        checknum = SKS_PP_StringFormats::strFill(12, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left);
    
        sb.Append(SKS_PP_StringFormats::strFill(21, checknum, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 8    Check Amount               108-118         11
        mPaymentAmount += mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;
        sb.Append(SKS_PP_StringFormats::strFill(11, num2str(-1 * mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur,0,2,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 9    Vendor Name 1              119-153         35
        sb.Append(SKS_PP_StringFormats::strFill(35, mRemittanceName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //10    Vendor Number              154-163          10
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail.AccountNum, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //11    Vendor Address 1           164-193         30
        sb.Append(SKS_PP_StringFormats::strFill(30, mRemittanceAdrline1, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //12    Vendor Address 2           194-223        30
        sb.Append(SKS_PP_StringFormats::strFill(30, mRemittanceAdrline2, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //13    Vendor Address 3           224-253         30
        sb.Append(SKS_PP_StringFormats::strFill(30, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //14    Vendor City                254-270         17
        sb.Append(SKS_PP_StringFormats::strFill(17, mRemittanceCity, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //15    Vendor State               271-272          2
        sb.Append(SKS_PP_StringFormats::strFill(2, mRemittanceState, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //16    Vendor Zip                 273-281          9
        sb.Append(SKS_PP_StringFormats::strFill(9, mRemittanceZipCode, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //17    Vendor Country             282-302         21
        sb.Append(SKS_PP_StringFormats::strFill(21, LogisticsAddressCountryRegion::findByISOCode(mRemittanceCountryRegionId).displayName(), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //18    Divert Indicator           303-307         5
        sb.Append(SKS_PP_StringFormats::strFill(5, this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('DivertIndicator', mSKS_EFT_FileDetail.RecId)), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_REM</Name>
				<Source><![CDATA[
    boolean rec_REM(SKS_EFT_FileDetail _invSKS_EFT_FileDetail)
    {
        //------------------------------------------------------------------------------------------------------
        //Each Field separated by pipe Delimiter
        //Nbr   Description                 Position    Length
        // 1    Record type                    1-  7         7
        // 2    Check Number                   8- 27        10
        // 3    Entity ID                     28- 29         2
        // 4    Invoice Number                30- 41        12
        // 5    Invoice Date                  42- 49         8
        // 6    Paid Invoice Amount           50- 61        12
        // 7    Original Invoice Amount       62- 73        12
        // 8    Discount Amount               74- 85        12
        // 9    Comment                       86-135        50
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder   sb = new System.Text.StringBuilder();
        str checknum = '';
        AmountMST amt;
    
        mRemittanceCount++;
    
        // 1    Record type                    1-  7         7
        sb.Append(SKS_PP_StringFormats::strFill(7, 'GAP700R', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 2    Check Number                   8- 27        20
        checknum = SKS_PP_StringFormats::strFill(12, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left);
    
        sb.Append(SKS_PP_StringFormats::strFill(20, checknum, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 3    Entity ID                     28- 29         2
        if(_invSKS_EFT_FileDetail.InvoiceNumber != '')
        {
            sb.Append('IN');
        }
        else
        {
            sb.Append('  ');
        }
        // 4    Invoice Number                30- 41        12
        sb.Append(SKS_PP_StringFormats::strFill(12, _invSKS_EFT_FileDetail.InvoiceNumber, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 5    Invoice Date                  42- 49         8
        sb.Append(SKS_PP_StringFormats::strDateYYYYMMDD(_invSKS_EFT_FileDetail.InvoiceDate));
    
        // 6    Paid Invoice Amount           50- 61        12
        sb.Append(SKS_PP_StringFormats::strFill(12, num2str(-1 * _invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur,0,2,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 7    Original Invoice Amount       62- 73        12
        amt = _invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur + _invSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur;
        sb.Append(SKS_PP_StringFormats::strFill(12, num2str(-1 * amt,0,2,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 8    Discount Amount               74- 85        12
        sb.Append(SKS_PP_StringFormats::strFill(12, num2str(-1 * _invSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur,0,2,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 9    Comment                       86-135        50
        sb.Append(SKS_PP_StringFormats::strFill(50, this.getInvoiceJournalPaymentId(_invSKS_EFT_FileDetail), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>