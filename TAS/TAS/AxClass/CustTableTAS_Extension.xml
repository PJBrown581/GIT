<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>CustTableTAS_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tableStr(CustTable))]
final class CustTableTAS_Extension
{

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>custLastPayLinkStatus</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethod]
    public display SKS_CC_PayLinkProcessStatus custLastPayLinkStatus()
    {
        SKS_CC_PayLinkProcessStatus lastPayLinkStatus = SKS_CC_PayLinkProcessStatus::NA;
        
        if (this.AccountNum)
        {
            SKS_CC_PayLinkQueue payLinkQueue;

            select firstonly1 RecId, Status from payLinkQueue
                order by RecId desc
                where payLinkQueue.AccountNum == this.AccountNum
                && payLinkQueue.TransType == SKS_CC_PayLinkTransactionType::PayLink                
                && (payLinkQueue.Entity == SKS_CC_PayLinkEntityType::SalesOrderPrePayment ||
                    payLinkQueue.Entity == SKS_CC_PayLinkEntityType::SalesOrderPreAuth);

            if (payLinkQueue.RecId)
            {
                lastPayLinkStatus = payLinkQueue.Status;
            }
        }
          
        return lastPayLinkStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>custHasActivePFCreditCard</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethod]
    public display NoYes custHasActivePFCreditCard()
    {
        NoYes hasActivePFCreditCard;
        
        if (this.AccountNum)
        {
            CreditCardCust creditCardCust_SKS;

            while select ExpiryDate from creditCardCust_SKS
                order by RecId desc
                where creditCardCust_SKS.CustAccount == this.AccountNum
            {
                if (creditCardCust_SKS.ExpiryDate && CreditCard::checkExpireDate(creditCardCust_SKS.ExpiryDate))
                {
                    hasActivePFCreditCard = NoYes::Yes;
                    break;
                }
            }
        }
          
        return hasActivePFCreditCard;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>