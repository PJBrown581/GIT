<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_BOM_80</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_BOM_80 extends SKS_PP_Format_BankGroup
{   
    int  totalNumOfChque;
    real totalAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtGroupHeader</Name>
				<Source><![CDATA[
    protected str txtGroupHeader()
    {
        // Start    001    13      Account No
        // Start    014    67      Filler
        
        System.Text.StringBuilder sb = new System.Text.StringBuilder();       

        totalNumOfChque = 0;
        totalAmount     = 0;

        //  Account No
        sb.Append(SKS_PP_StringFormats::strFill(13, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //  Filler
        sb.Append(SKS_PP_StringFormats::strFill(67, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        // Start    001    13      Account No
        // Start    014    10      Cheque Serial Number
        // Start    024    11      Cheque Amount
        // Start    035    06      Paid Date
        // Start    041    40      Filler
                
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
                        
        totalNumOfChque++;

        // Account Number
        sb.Append(SKS_PP_StringFormats::strFill(13, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //  Cheque Serial Number
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //  Cheque Amount
        sb.Append(SKS_PP_StringFormats::strMoney(mSKS_PP_FileDetail.Chkcuryamt, 11));

        totalAmount = totalAmount + mSKS_PP_FileDetail.ChkCuryAmt;

        //  Paid  Date
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(mSKS_PP_FileDetail.ChkDocDate, DateSeparator::None));

        //  Filler
        sb.Append(SKS_PP_StringFormats::strFill(40, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    protected str txtGroupTrailer()
    {
        // Start    001    10      Total Number Cheques
        // Start    011    12      Total Dollar Amount
        // Start    023    58      Filler
        
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
      
        //  Total Number Cheques
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(totalNumOfChque), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //  Total Dollar Amount
        sb.Append(SKS_PP_StringFormats::strMoney(totalAmount, 12));

        //  Filler
        sb.Append(SKS_PP_StringFormats::strFill(58, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>