<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_BankAccountTableEventHandler</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_BankAccountTableEventHandler
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>AccountNum_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    ///
    /// </summary>
    /// <param name="sender"></param>
    /// <param name="e"></param>    
    [FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, AccountNum), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, BankCodeType), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, IBAN), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, Name), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, SWIFTNo), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, RegistrationNum), FormDataFieldEventType::Modified),
    FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, BankGroupId), FormDataFieldEventType::Modified)]
    public static void AccountNum_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        SKS_EFT_setup mSKS_EFT_Setup;
        BankAccountTable bankAccountTable = sender.datasource().cursor();
        FormControl bankAccountTable_SKS_EFT_AcctStatus = sender.datasource().formRun().design(0).controlName(formControlStr(BankAccountTable, BankAccountTable_SKS_EFT_AcctStatus));
        
        changeCompany(bankAccountTable.dataAreaId)
        {
            select mSKS_EFT_Setup;
        }
        
        if(msks_eft_setup.APBankAcctApproval == NoYes::Yes)
        {
            bankAccountTable.SKS_EFT_AcctStatus = SKS_EFT_AcctApprovalStatus::Pending;
            bankAccountTable.SKS_EFT_AcctModifier = curuserid();
            bankAccountTable.SKS_EFT_AcctModifiedDate = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone());
            bankAccountTable.SKS_EFT_AcctApprover = '';
            bankAccountTable.SKS_EFT_AcctApprovalDate = dateNull();
    
            // Disable field in case this user was allowed to approve and has now changed a field
            bankAccountTable_SKS_EFT_AcctStatus.enabled(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>BankAccountTable_OnActivated</Name>
				<Source><![CDATA[
    /// <summary>
    ///
    /// </summary>
    /// <param name="sender"></param>
    /// <param name="e"></param>
    [FormDataSourceEventHandler(formDataSourceStr(BankAccountTable, BankAccountTable), FormDataSourceEventType::Activated)]
    public static void BankAccountTable_OnActivated(FormDataSource sender, FormDataSourceEventArgs e)
    {
        SKS_EFT_setup mSKS_EFT_Setup;
        UserGroupList usergrouplist;
        BankAccountTable bankAccountTable = sender.cursor();
        FormDesign formDesign = sender.formRun().design();
        FormControl approval = formDesign.controlName(formControlStr(BankAccountTable, Approval));
        FormControl modified = formDesign.controlName(formControlStr(BankAccountTable, Modified));
        FormControl bankAccountTable_SKS_EFT_AcctStatus = formDesign.controlName(formControlStr(BankAccountTable, BankAccountTable_SKS_EFT_AcctStatus));
        FormControl isVisibleValue = formDesign.controlName(formControlStr(BankAccountTable, IsVisibleValue));
        
        changeCompany(bankAccountTable.dataAreaId)
        {
            select mSKS_EFT_Setup;
        }

        if(msks_eft_setup.APBankAcctApproval == NoYes::No)
        {
            approval.visible(false);
            modified.visible(false);
            bankAccountTable_SKS_EFT_AcctStatus.visible(false);
        }
        else
        {
            select RecId from usergrouplist 
                where usergrouplist.userid == curuserid() && 
                    usergrouplist.groupId == msks_eft_setup.APAccountApprGroup; //Sks_Bug_7538
                                
            if(msks_eft_setup.APApproverChangeInfo == NoYes::Yes)
            {
                //Sks_Bug_7538 --->
                if(usergrouplist.RecId)
                {
                    //Sks_Bug_7538 <---
                    sender.object(fieldnum(BankAccountTable, Name)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, BankGroupID)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, BankCodeType)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, RegistrationNum)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, AccountNum)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, SWIFTNo)).allowedit(false);
                    sender.object(fieldnum(BankAccountTable, IBAN)).allowedit(false);
                    //Sks_Bug_7538 --->
                }
                //Sks_Bug_7538 <---
            }
    
            approval.visible(true);
            modified.visible(true);
            bankAccountTable_SKS_EFT_AcctStatus.visible(true);
            
            if(bankAccountTable.SKS_EFT_AcctStatus != SKS_EFT_AcctApprovalStatus::Approved
                && bankAccountTable.SKS_EFT_AcctModifier != curuserid()
                && usergrouplist.RecId)
            {
                bankAccountTable_SKS_EFT_AcctStatus.enabled(true);
            }
            else
            {
                bankAccountTable_SKS_EFT_AcctStatus.enabled(false);
            }
        }

        if(!approval.isVisible())
        {
            isVisibleValue.visible(true);
        }
        else
        {
            isVisibleValue.visible(false);
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>SKS_EFT_AcctStatus_OnModified</Name>
				<Source><![CDATA[
    /// <summary>
    ///
    /// </summary>
    /// <param name="sender"></param>
    /// <param name="e"></param>
    [FormDataFieldEventHandler(formDataFieldStr(BankAccountTable, BankAccountTable, SKS_EFT_AcctStatus), FormDataFieldEventType::Modified)]
    public static void SKS_EFT_AcctStatus_OnModified(FormDataObject sender, FormDataFieldEventArgs e)
    {
        FormDataSource BankAccountTable_ds = sender.datasource();
        BankAccountTable BankAccountTable = BankAccountTable_ds.cursor();

        ttsbegin;
        if(BankAccountTable.SKS_EFT_AcctStatus == SKS_EFT_AcctApprovalStatus::Approved)
        {
            BankAccountTable.SKS_EFT_AcctApprover = curuserid();
            BankAccountTable.SKS_EFT_AcctApprovalDate = DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone());
        }
        else
        {
            BankAccountTable.SKS_EFT_AcctApprover = '';
            BankAccountTable.SKS_EFT_AcctApprovalDate = dateNull();
        }
        ttscommit;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>