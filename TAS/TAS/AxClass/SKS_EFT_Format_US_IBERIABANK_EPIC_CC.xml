<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_US_IBERIABANK_EPIC_CC</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_US_IBERIABANK_EPIC_CC extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_US_IBERIABANK_EPIC_CC extends SKS_EFT_Format
{
    #define.FormatVersion('20220701')
    #define.Seperator(',')

    SKS_EB_Vendor       mSKS_EB_Vendor;
    SKS_EFT_FileDetail  invSKS_EFT_FileDetail;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckNestedLoopinCode","Necessary to allow the processing of every invoice(inner loop) for every relevant parent file detail record(outer loop)")]
    protected void formatStructure()
    {
        while(mSKS_EFT_FileDetail)
        {
            //Get SKS_EFT_VendorPaym
            this.getSKS_EFT_VendorPaym();

            while select InvoiceDate,InvoiceSettleAmountPmtCur,InvoiceNumber from invSKS_EFT_FileDetail
                where  invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
            {
                
                this.rec_Details();
            }
            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Details</Name>
				<Source><![CDATA[
    public boolean rec_Details()
    {
        SKS_EFT_FileDetail  totInvSKS_EFT_FileDetail;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        //  1   Vendor ID                          10    (1-10)
        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(13, mSKS_EFT_VendorPaym.VendAccount, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right)));//SKS_VSTS_13289

        //  2   Invoice number                     15    (12-26)
        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(15, invSKS_EFT_FileDetail.InvoiceNumber, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right)));

        //  3   Invoice Date                       08    (28- 35)
        sb.append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(invSKS_EFT_FileDetail.InvoiceDate, DateSeparator::Slash)));//SKS_VSTS_13289
        
        //  4  Invoice Amount                      15    (37-51)
        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(15, num2Str(invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1, 0, 2, 1, 0), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left )));

        //  5  Total paid Amount                   15    (53-67)
        select  SUM(InvoiceSettleAmountPmtCur) from totInvSKS_EFT_FileDetail
            where
                totInvSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                totInvSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                totInvSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary;

        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(15, num2Str(totInvSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur *-1, 0, 2, 1, 0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left ),false));
        
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '', boolean _Delimit = true)
    {
        if(_Delimit)
        {
            return _field + #Seperator;
        }
        else
        {
            return _field;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>