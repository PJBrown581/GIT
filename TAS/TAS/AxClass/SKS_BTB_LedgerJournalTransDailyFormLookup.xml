<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_BTB_LedgerJournalTransDailyFormLookup</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_BTB_LedgerJournalTransDailyFormLookup
{
 

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>new</Name>
				<Source><![CDATA[
    protected void new()
    {
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SKS_BTB_LedgerJournalTransDailyFormLookup construct()
    {
        return new SKS_BTB_LedgerJournalTransDailyFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>TransferMode_OnLookup</Name>
				<Source><![CDATA[
    public void TransferMode_OnLookup(FormStringControl _callingControl)
    {
        LedgerJournalTrans trans = _callingControl.formRun().dataSource().cursor();
        str bankAccountId = LedgerDimensionFacade::getDisplayValueForLedgerDimension(trans.OffsetLedgerDimension);

        Query query = new Query();

        QueryBuildDataSource qbds = query.addDataSource(tableNum(SKS_BTB_PaymModeTable));        

        QueryBuildRange qbrType = qbds.addRange(fieldNum(SKS_BTB_PaymModeTable, TransferType));
        if(trans.AmountCurCredit)
        {
            qbrType.value(SysQuery::value(SKS_BTB_TransferType::Transfer_in));
        }
        else if(trans.AmountCurDebit)
        {
            qbrType.value(SysQuery::value(SKS_BTB_TransferType::Transfer_out));
        }
        else
        {
            qbrType.value(SysQuery::valueEmptyString());
            QueryBuildRange qbrBlank = qbds.addRange(fieldNum(SKS_EFTBTB_Bank, RecId));
            qbrBlank.value(queryValue(0));
        }

        QueryBuildDataSource qbds1 = qbds.addDataSource(tableNum(SKS_EFTBTB_Bank));
        qbds1.relations(true);
        qbds1.joinMode(JoinMode::ExistsJoin);

        QueryBuildRange qbrBanckAccount = qbds1.addRange(fieldNum(SKS_EFTBTB_Bank, BankAccountId));
        qbrBanckAccount.value(SysQuery::value(bankAccountId));

        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(SKS_BTB_PaymModeTable), _callingControl);
 
        sysTableLookup.addLookupfield(fieldNum(SKS_BTB_PaymModeTable, PaymMode));
        sysTableLookup.addLookupfield(fieldNum(SKS_BTB_PaymModeTable, TransferType));
        sysTableLookup.addLookupfield(fieldNum(SKS_BTB_PaymModeTable, Description));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>TransferSpec_OnLookup</Name>
				<Source><![CDATA[
    public void TransferSpec_OnLookup(FormStringControl _callingControl)
    {
        LedgerJournalTrans trans = _callingControl.formRun().dataSource().cursor();
        Query query = new Query();

        QueryBuildDataSource qbds = query.addDataSource(tableNum(SKS_BTB_BankPaymModeSpec));
        QueryBuildRange qbr = qbds.addRange(fieldNum(SKS_BTB_BankPaymModeSpec, PaymMode));
        qbr.value(SysQuery::value(trans.SKS_BTB_TransferMethod));

        QueryBuildDataSource qbdsFFSpec = qbds.addDataSource(tableNum(SKS_EFT_FileFormatPaymSpec));
        qbdsFFSpec.relations(true);
        qbdsFFSpec.joinMode(JoinMode::ExistsJoin);

        QueryBuildRange qbr1 = qbdsFFSpec.addRange(fieldNum(SKS_EFT_FileFormatPaymSpec, Selected));
        qbr1.value(SysQuery::value(NoYes::Yes));

        QueryBuildRange qbrFundType = qbdsFFSpec.addRange(fieldNum(SKS_EFT_FileFormatPaymSpec, SKS_BTB_PaymSpecFundTransferType));
        if(trans.AmountCurDebit)
        {
            qbrFundType.value(SysQuery::valueNot(SKS_BTB_PaymSpecFundTransferType::Transfer_in));
        }
        else
        {
            qbrFundType.value(SysQuery::valueNot(SKS_BTB_PaymSpecFundTransferType::Transfer_out));
        }

        SysTableLookup sysTableLookup = SysTableLookup::newParameters(tableNum(SKS_BTB_BankPaymModeSpec), _callingControl);
        sysTableLookup.addLookupfield(fieldNum(SKS_BTB_BankPaymModeSpec, Specification));
        sysTableLookup.addLookupfield(fieldNum(SKS_BTB_BankPaymModeSpec, Name));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>TransferSpec_OnJumpref</Name>
				<Source><![CDATA[
    public void TransferSpec_OnJumpref(FormDataObject _callingControl)
    {
        Args            args;
        ;

        args = new Args();

        args.record(_callingControl.dataSource().cursor());

        args.caller(_callingControl.datasource().formRun());

        args.name('LedgerJournalTrans');

        args.menuItemName(menuitemDisplayStr(SKS_BTB_BankPaymModeSpec));

        new MenuFunction(menuitemDisplayStr(SKS_BTB_BankPaymModeSpec), MenuItemType::Display).run(args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>