<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_AU_COMMONWEALTHBANK_120</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// class SKS_EFT_Format_AU_COMMONWEALTHBANK_120 which extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_AU_COMMONWEALTHBANK_120 extends SKS_EFT_Format
{
    #define.FormatVersion('20220425')
    int         mTotalTransaction;
    AmountMST   mTotalAmount, creditAmount, debitAmount, mContraTotal;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckNestedLoopinCode","Necessary to allow the processing of every invoice(inner loop) for every relevant parent file detail record(outer loop)")]
    protected void formatStructure()
    {
        // Header Record
        this.rec_Header();

        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();

            // Detail Record
            this.rec_Detail();

            // Contra Record
            this.rec_Contra();

            next mSKS_EFT_FileDetail;
        }
        // File Total Record
        this.rec_FileTotal();
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Header</Name>
				<Source><![CDATA[
   /// <summary>
   /// Header Record
   /// </summary>
   /// <returns>true</returns>
    public boolean rec_Header()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Description                                                     Length      Position
        //1	    Record type         	                                            1	    (1  - 1)
        //2	    BLANKS	                                                            17	    (2  - 18)
        //3	    Reel Sequence Number	                                            2	    (19  - 20)
        //4	    Name Of User Bank	                                                3	    (21 - 23)
        //5	    BLANKS	                                                            7	    (24 - 30)
        //6	    User Name (Preferred Name)                                          26	    (31	- 56)
        //7	    User Number (as allocated by APCA)The CommBiz default is 301500.    6	    (57	- 62)
        //8	    Description of entries	                                            12	    (63	- 74)
        //9	    Date for processing	                                                6	    (75	- 80)
        //10	Unused 	                                                            40	    (81	- 120)
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();

        msks_EFT_FileDetail_PaymentLine = sks_EFT_FileDetail_PaymentLine::find(mSKS_EFT_FileDetail.RecId);

        //1	    Record type         	                                            1	    (1  - 1)
        sb.Append('0');
        
        //2	    BLANKS	                                                            17	    (2  - 18)
        sb.Append(SKS_PP_StringFormats::strFill(17, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //3	    Reel Sequence Number	                                            2	    (19  - 20)
        sb.Append('01');

        //4	    Name Of User Bank	                                                3	    (21 - 23)
        sb.Append(SKS_PP_StringFormats::strFill(3, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHImmDestName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //5	    BLANKS	                                                            7	    (24 - 30)
        sb.Append(SKS_PP_StringFormats::strFill(7, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //6	    User Name (Preferred Name)                                          26	    (31	- 56)
        sb.Append(SKS_PP_StringFormats::strFill(26, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHImmOriginName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //7	    User Number (as allocated by APCA)The CommBiz default is 301500.    6	    (57	- 62)
        sb.Append(SKS_PP_StringFormats::strFill(6, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHImmOrigin), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //8	    Description of entries	                                            12	    (63	- 74)
        sb.Append(SKS_PP_StringFormats::strFill(12, this.CharacterReplacement(msks_EFT_FileDetail_PaymentLine.TransactionTxt), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //9	    Date for processing	                                                6	    (75	- 80)
        sb.Append(date2str(mSKS_EFT_FileDetail.EffectiveDate, 123, DateDay::Digits2, DateSeparator::None, DateMonth::Digits2, DateSeparator::None, DateYear::Digits2));

        //10	Unused 	                                                            40	    (81	- 120)
        sb.Append(SKS_PP_StringFormats::strFill(40, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        this.fileWrite(sb.ToString());

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Detail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Detail Record
    /// </summary>
    /// <returns>true</returns>
    public boolean rec_Detail()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Description                                                     Length      Position
        //1	    Record type         	                                            1	    (1  - 1)
        //2	    Bank/State/Branch number	                                        7	    (2  - 8)
        //3	    Account number to be credited or debited	                        9	    (9 - 17)
        //4	    Indicator                                                           1	    (18 - 18)
        //5	    Transaction Code	                                                2	    (19 - 20)
        //6	    Amount	                                                            10	    (21	- 30)
        //7	    Title of account to be CREDITED or DEBITED	                        32	    (31	- 62)
        //8	    Lodgement Reference                                                 18	    (63	- 80)
        //9	    Trace Bank/State/Branch number	                                    7	    (81	- 87)
        //10	Trace Account Number                                                9	    (88	- 96)
        //11	Name of  Remitter                                                   16	    (97 - 112)
        //12    AMOUNT OF WITHHOLDING TAX                                           8       (113- 120)
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str transactionCode;
        
        mTotalTransaction++;

        //1	    Record type         	                                            1	    (1  - 1)
        sb.Append('1');

        //2	    Bank/State/Branch number	                                        7	    (2  - 8)
        sb.Append(SKS_PP_StringFormats::strFill(7, mSKS_EFT_FileDetail.DepBankTransit, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //3	    Account number to be credited or debited	                        9	    (9 - 17)
        sb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));

        //4	    Indicator                                                           1	    (18 - 18)
        sb.Append(SKS_PP_StringFormats::strFill(1, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //5	    Transaction Code	                                                2	    (19 - 20)
        transactionCode = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('TransactionCode', mSKS_EFT_FileDetail.RecId));
        sb.Append(SKS_PP_StringFormats::strFill(2, transactionCode, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //6	    Amount	                                                            10	    (21	- 30)
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 10));
        mTotalAmount += mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;

        //7	    Title of account to be CREDITED or DEBITED	                        32	    (31	- 62)
        sb.Append(SKS_PP_StringFormats::strFill(32, this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //8	    Lodgement Reference                                                 18	    (63	- 80)        
        sb.Append(SKS_PP_StringFormats::strFill(18, this.CharacterReplacement(mSKS_EFT_FileDetail.VendYourAccountNum), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //9	    Trace Bank/State/Branch number	                                    7	    (81	- 87)
        sb.Append(SKS_PP_StringFormats::strFill(7, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //10	Trace Account Number                                                9	    (88	- 96)
        sb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //11	Name of  Remitter                                                   16	    (97 - 112)
        sb.Append(SKS_PP_StringFormats::strFill(16, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHImmOriginName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //12    AMOUNT OF WITHHOLDING TAX                                           8       (113- 120)
        sb.Append(SKS_PP_StringFormats::strFill(8, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        
        this.fileWrite(sb.ToString());

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Contra</Name>
				<Source><![CDATA[
    /// <summary>
    /// Contra Record
    /// </summary>
    /// <returns>true</returns>
    public boolean rec_Contra()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Description                                                     Length      Position
        //1	    Record type         	                                            1	    (1  - 1)
        //2	    Bank/State/Branch number	                                        7	    (2  - 8)
        //3	    Account number to be credited or debited	                        9	    (9 - 17)
        //4	    Indicator                                                           1	    (18 - 18)
        //5	    Transaction Code	                                                2	    (19 - 20)
        //6	    Amount	                                                            10	    (21	- 30)
        //7	    Title of account to be CREDITED or DEBITED	                        32	    (31	- 62)
        //8	    Lodgement Reference                                                 18	    (63	- 80)
        //9	    Trace Bank/State/Branch number	                                    7	    (81	- 87)
        //10	Trace Account Number                                                9	    (88	- 96)
        //11	Name of  Remitter                                                   16	    (97 - 112)
        //12    AMOUNT OF WITHHOLDING TAX                                           8       (113- 120)
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        mTotalTransaction++;

        //1	    Record type         	                                            1	    (1  - 1)
        sb.Append('1');

        //2	    Bank/State/Branch number	                                        7	    (2  - 8)
        sb.Append(SKS_PP_StringFormats::strFill(7, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //3	    Account number to be credited or debited	                        9	    (9 - 17)
        sb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //4	    Indicator                                                           1	    (18 - 18)
        sb.Append(SKS_PP_StringFormats::strFill(1, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //5	    Transaction Code	                                                2	    (19 - 20)
        sb.Append('13');

        //6	    Amount	                                                            10	    (21	- 30)
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 10));
        mContraTotal += mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;

        //7	    Title of account to be CREDITED or DEBITED	                        32	    (31	- 62)
        sb.Append(SKS_PP_StringFormats::strFill(32, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHCompanyName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //8	    Lodgement Reference                                                 18	    (63	- 80)
        sb.Append(SKS_PP_StringFormats::strFill(18, 'PAYMENT', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //9	    Trace Bank/State/Branch number	                                    7	    (81	- 87)
        sb.Append(SKS_PP_StringFormats::strFill(7, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //10	Trace Account Number                                                9	    (88	- 96)
        sb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //11	Name of  Remitter                                                   16	    (97 - 112)
        sb.Append(SKS_PP_StringFormats::strFill(16, this.CharacterReplacement(mSKS_EFT_FileDetail.FileACHImmOriginName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //12    AMOUNT OF WITHHOLDING TAX                                           8       (113- 120)
        sb.Append(SKS_PP_StringFormats::strFill(8, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_FileTotal</Name>
				<Source><![CDATA[
    /// <summary>
    /// Trailer Record
    /// </summary>
    /// <returns>true</returns>
    public boolean rec_FileTotal()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Description                                                     Length      Position
        //1	    Record type         	                                            1	    (1  - 1)
        //2	    Bank/State/Branch number     	                                    7	    (2  - 8)
        //3	    Unused                    	                                        12	    (9  - 20)
        //4	    File net total amount	                                            10	    (21 - 30)
        //5	    File credit total amount	                                        10	    (31 - 40)
        //6	    File debit total amount                                             10	    (41	- 50)
        //7	    Unused	                                                            24	    (51	- 74)
        //8	    File total count of detail records (record type "1")                 6	    (75	- 80)
        //9	    Unused                	                                            40	    (81	- 120)
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();

        //1	    Record type         	                                            1	    (1  - 1)
        sb.Append('7');

        //2	    Bank/State/Branch number     	                                    7	    (2  - 8)
        sb.Append('999-999');

        //3	    Unused                    	                                        12	    (9  - 20)
        sb.Append(SKS_PP_StringFormats::strFill(12, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //4	    File net total amount	                                            10	    (21 - 30)
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * (mTotalAmount - mContraTotal), 10));

        //5	    File credit total amount	                                        10	    (31 - 40)
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * mTotalAmount, 10));

        //6	    File debit total amount                                             10	    (41	- 50)
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * mContraTotal, 10));
        
        //7	    Unused	                                                        24	    (51	- 74)
        sb.Append(SKS_PP_StringFormats::strFill(24, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //8	    File total count of detail records (record type "1")                 6	    (75	- 80)
        sb.Append(SKS_PP_StringFormats::strFill(6, int2str(mTotalTransaction), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //9	    Unused                	                                        40	    (81	- 120)
        sb.Append(SKS_PP_StringFormats::strFill(40, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    [SuppressBPWarning('BPErrorSelectUsingFirstOnly', 'The buffer is used in next statement or in a datasource query loop.')]
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
            join mSKS_EFT_FileDetail_VendAddress
              order by mSKS_EFT_FileDetail.PaymentCompany,
                   mSKS_EFT_FileDetail.RecordSection,
                   mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_VendAddress.RefRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>