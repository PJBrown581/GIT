<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_AU_ANZ_CSV</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_AU_ANZ_CSV extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_AU_ANZ_CSV extends SKS_EFT_Format
{
    #define.FormatVersion('20220411')
    #define.DOM_WIRE('DOM')
    #define.Separator(',')
    #define.DoubleQuote('"')

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {
        while(mSKS_EFT_FileDetail)
        {
            //  Detail Record
            this.rec_Detail();

            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Detail</Name>
				<Source><![CDATA[
    public boolean rec_Detail()
    {
        //---------------------------------------------------------------------------------
        //Nbr   Description
        //1     Transaction Amount
        //2     Account Number
        //3     Other Party Name
        //4     Originator Reference
        //5     Originator Analysis Code
        //6     Originator Particulars
        //7     Other Party Reference
        //8     Other Party Analysis Code
        //9     Other Party Particulars
        //---------------------------------------------------------------------------------

        System.Text.StringBuilder sb = new System.Text.StringBuilder();

        //1     Transaction Amount
        sb.Append(this.wrapField(num2str(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1, 0, 2, 1, 0), 11));

        //2     Account Number
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.DepBankAcct, 20));

        //3     Other Party Name
        sb.Append(this.wrapField(this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneName), 32));

        //4     Originator Reference
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.PaymentReference, 12));

        //5     Originator Analysis Code
        sb.Append(this.wrapField());

        //6     Originator Particulars
        sb.Append(this.wrapField());

        //7     Other Party Reference
        sb.Append(this.wrapField());

        //8     Other Party Analysis Code
        sb.Append(this.wrapField());

        //9     Other Party Particulars
        sb.Append(this.wrapField('', 0, false));

        this.fileWrite(sb.ToString());
        return true;

    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '', int _fieldLength = 0,  boolean _addSeparator = true)
    {
        str text;

        if(_addSeparator)
        {
            if(_field != '' && strContains(_field, ',') && _fieldLength > 0)
            {
                text = #DoubleQuote + subStr(_field, 1, _fieldLength) + #DoubleQuote + #Separator;
            }
            else
            {
                text = subStr(_field, 1, _fieldLength) + #Separator;
            }
        }
        else
        {
            text = subStr(_field, 1, _fieldLength);
        }

        return text;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    [SuppressBPWarning('BPErrorSelectUsingFirstOnly', 'The buffer is used in next statement or in a datasource query loop.')]
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
            join mSKS_EFT_FileDetail_VendAddress
              order by mSKS_EFT_FileDetail.PaymentCompany, 
                   mSKS_EFT_FileDetail.RecordSection,
                   mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_VendAddress.RefRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>