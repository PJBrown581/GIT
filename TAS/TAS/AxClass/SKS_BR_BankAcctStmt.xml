<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_BR_BankAcctStmt</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class SKS_BR_BankAcctStmt
{
    CompanyBankAccountId AccountID;
    TransDate AccountStatementDate;
    TransDate ReconcileDate;
    BankAccountStatementNum AccountStatementNum;
    AmountCur EndingBalance;
    CurrencyCode CurrCode;
    str gErrorMessage;
    SKS_BR_BankTran     gSKS_BR_BankTran;//SKS_BR1

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>createBRStatement</Name>
				<Source><![CDATA[
public BankAccountStatement createBRStatement(BankAccountStatement _BankAccountStatement, RefRecId _SKS_BR_BankHeaderRecId, boolean _CreateRegardlessOfAccountSettings = false)
    {
        #SKS_Global
        SKS_PP_Bank     locSKS_PP_Bank;
        SKS_PP_Setup    locSKS_PP_Setup;
        BankAccountStatement locBankAccountStatement;
        SKS_BR_BankTran locSKS_BR_BankTran;
        BankAccountStatement BankAccountStatement;
        BankAccountStatement = _BankAccountStatement;

        void createStatement()
        {
            //if another file has already created a statement for this date, statement number, and account, set warning message and create HistoryTable event.
            select firstonly locBankAccountStatement where locBankAccountStatement.AccountStatementNum == BankAccountStatement.AccountStatementNum
                                                    && locBankAccountStatement.AccountId == BankAccountStatement.AccountID
                                                    && locBankAccountStatement.AccountStatementDate == BankAccountStatement.AccountStatementDate
                                                    && locBankAccountStatement.SKS_BR_BankHeaderRecId != _SKS_BR_BankHeaderRecId;

            if(locBankAccountStatement)
            {
                gErrorMessage = strFmt("@SKS2961", date2str(BankAccountStatement.AccountStatementDate,-1,-1,-1,-1,-1,-1,DateFlags::None), BankAccountStatement.AccountStatementNum, locSKS_PP_Bank.company(), BankAccountStatement.AccountId);
                SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoCreationFailed,
                                                    _SKS_BR_BankHeaderRecId,
                                                    BankAccountStatement.AccountId,
                                                    BankAccountStatement.AccountStatementDate,
                                                    BankAccountStatement.AccountStatementNum,
                                                    '',
                                                    gErrorMessage);
                BankAccountStatement = null;
               //BankAccountStatement = locBankAccountStatement; // commented for 7506 - To return null
            }
            else
            {
                select firstonly forUpdate locBankAccountStatement where locBankAccountStatement.AccountStatementNum == BankAccountStatement.AccountStatementNum
                                                && locBankAccountStatement.AccountId == BankAccountStatement.AccountID
                                                && locBankAccountStatement.AccountStatementDate == BankAccountStatement.AccountStatementDate
                                                && locBankAccountStatement.SKS_BR_BankHeaderRecId == _SKS_BR_BankHeaderRecId;
                //if this file has already created a statement, pull it back into memory so new records can be created on this statement
                if(locBankAccountStatement)
                {
                    BankAccountStatement = null;
                    BankAccountStatement = locBankAccountStatement;
                }
                //if the file doesn't already exist, create it.
                else
                {
                    // Copy to resolve cross company issue.
                    buf2Buf(BankAccountStatement, locBankAccountStatement);
                    locBankAccountStatement.SKS_BR_BankHeaderRecId = _SKS_BR_BankHeaderRecId;
                    locBankAccountStatement.insert();
                    BankAccountStatement = null;
                    BankAccountStatement = locBankAccountStatement;
                    SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoCreated,
                                                        _SKS_BR_BankHeaderRecId,
                                                        BankAccountStatement.AccountId,
                                                        BankAccountStatement.AccountStatementDate,
                                                        BankAccountStatement.AccountStatementNum);
                }
            }
        }

        select firstonly BRCreateBankStatements, DataAreaId from locSKS_PP_Bank where locSKS_PP_Bank.BankAccountId == BankAccountStatement.AccountId;
        select firstonly BREdition from locSKS_PP_Setup;
        if(locSKS_PP_Setup.BREdition == SKS_BR_Edition::Advanced && (locSKS_PP_Bank.BRCreateBankStatements == NoYes::Yes || _CreateRegardlessOfAccountSettings))
        {
            select firstOnly OriginalStatementRecId, RecId from locSKS_BR_BankTran where locSKS_BR_BankTran.AccountId == BankAccountStatement.AccountId
                                                        && locSKS_BR_BankTran.TranDate == BankAccountStatement.AccountStatementDate
                                                        && locSKS_BR_BankTran.AccountStatementNum == BankAccountStatement.AccountStatementNum //SKS_FDD_15121
                                                        && locSKS_BR_BankTran.BankHeaderRecId == _SKS_BR_BankHeaderRecId;

            if(locSKS_BR_BankTran)
            {
                select firstOnly locBankAccountStatement where locBankAccountStatement.RecId == locSKS_BR_BankTran.OriginalStatementRecId;
                if(locBankAccountStatement)
                {
                    BankAccountStatement = null;
                    BankAccountStatement = locBankAccountStatement;
                }
                else
                {
                    createStatement();
                }
            }
            else
            {
                createStatement();

            }
        }
        return BankAccountStatement;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountId</Name>
				<Source><![CDATA[
    public CompanyBankAccountId getAccountId()
    {
        return AccountID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountStatementDate</Name>
				<Source><![CDATA[
    public TransDate getAccountStatementDate()
    {
        return AccountStatementDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountStmtNum</Name>
				<Source><![CDATA[
    public BankAccountStatementNum getAccountStmtNum()
    {
        return AccountStatementNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCurrencyCode</Name>
				<Source><![CDATA[
    public CurrencyCode getCurrencyCode()
    {
        return CurrCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getEndingBalance</Name>
				<Source><![CDATA[
    public AmountCur  getEndingBalance()
    {
        return EndingBalance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getError</Name>
				<Source><![CDATA[
    public str getError()
    {
        return gErrorMessage;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getReconcileDate</Name>
				<Source><![CDATA[
    public TransDate getReconcileDate()
    {
        return ReconcileDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSKS_BR_BankTran</Name>
				<Source><![CDATA[
    //SKS_BR1
    public SKS_BR_BankTran getSKS_BR_BankTran()
    {
        return gSKS_BR_BankTran;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAccountId</Name>
				<Source><![CDATA[
    public void setAccountId(CompanyBankAccountId _accountID)
    {
        AccountID = _accountID;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAccountStatementDate</Name>
				<Source><![CDATA[
    public void setAccountStatementDate(TransDate _accountStmtDate)
    {
        AccountStatementDate = _accountStmtDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setAccountStmtNum</Name>
				<Source><![CDATA[
    public void setAccountStmtNum(BankAccountStatementNum _accountStatementNum)
    {
        AccountStatementNum = _accountStatementNum;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setCurrencyCode</Name>
				<Source><![CDATA[
    public void setCurrencyCode(CurrencyCode _currencyCode)
    {
        CurrCode = _currencyCode;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setEndingBalance</Name>
				<Source><![CDATA[
    public void setEndingBalance(AmountCur _endingBalance)
    {
        EndingBalance = _endingBalance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setReconcileDate</Name>
				<Source><![CDATA[
    public void setReconcileDate(TransDate _reconcileDate)
    {
        ReconcileDate = _reconcileDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setSKS_BR_BankTranRecord</Name>
				<Source><![CDATA[
    //SKS_BR1
    public void setSKS_BR_BankTranRecord(SKS_BR_BankTran _sKS_BR_BankTran)
    {
        gSKS_BR_BankTran = _sKS_BR_BankTran;
    }

]]></Source>
			</Method>
			<Method>
				<Name>deleteBankStatements</Name>
				<Source><![CDATA[
    public static void deleteBankStatements(SKS_BR_BankHeaderRecId _SKS_BR_BankHeaderRecId)
    {
        BankAccountStatement lBankAccountStatement;
        BankAccountTrans lBankAccountTrans;
        SKS_BR_BankTran lSKS_BR_BankTran;

        while Select forupdate crosscompany lBankAccountStatement
                where lBankAccountStatement.SKS_BR_BankHeaderRecId == _SKS_BR_BankHeaderRecId
                && lBankAccountStatement.SKS_BR_BankHeaderRecId != 0
        {
            changeCompany(lBankAccountStatement.company())
            {
                ttsBegin;
                if(lBankAccountStatement.ReconcileDate != dateNull())
                {
                    info(strFmt("@SKS:SKGBRNoDelReconciledStmt"
                                    ,lBankAccountStatement.company(), lBankAccountStatement.AccountId, lBankAccountStatement.AccountStatementNum, lBankAccountStatement.AccountStatementDate));
                }
                else
                {


                    lSKS_BR_BankTran = null;

                    while select AccountId, AccountStatementDate, AccountStatementNum from lSKS_BR_BankTran
                        group by lSKS_BR_BankTran.AccountId, lSKS_BR_BankTran.AccountStatementDate, lSKS_BR_BankTran.AccountStatementNum
                        where lSKS_BR_BankTran.AccountId == lBankAccountStatement.AccountId
                            && lSKS_BR_BankTran.AccountStatementDate == lBankAccountStatement.AccountStatementDate
                            && lSKS_BR_BankTran.AccountStatementNum == lBankAccountStatement.AccountStatementNum
                    {

                        // Reset fields so transactions are marked cleared for the statement that is being deleted
                        lBankAccountTrans = null;
                        lBankAccountTrans.skipDataMethods(true);
                        update_recordSet lBankAccountTrans setting
                            AccountStatement = '',
                            AccountStatementDate = dateNull(),
                            Included = NoYes::No,
                            SKS_BR_AlreadyMarked = NoYes::No,
                            SKS_BR_IsSelected = NoYes::No
                                where   lBankAccountTrans.AccountId == lBankAccountStatement.AccountId &&
                                        lBankAccountTrans.AccountStatement == lBankAccountStatement.AccountStatementNum &&
                                        lBankAccountTrans.AccountStatementDate == lBankAccountStatement.AccountStatementDate &&
                                        lBankAccountTrans.Reconciled == NoYes::No;
                    }
                }
                lBankAccountStatement.delete();
                ttsCommit;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>importUnmatchedAmount</Name>
				<Source><![CDATA[
    public static AmountMST importUnmatchedAmount(BankAccountStatement _bankAccountStatement)
    {
        #SKS_Global
        SKS_BR_BankTran lSKS_BR_BankTran;

        select sum(TranAmount) from lSKS_BR_BankTran
            where lSKS_BR_BankTran.OriginalStatementRecId == _bankAccountStatement.RecId &&
                  (lSKS_BR_BankTran.ManualReconciliationID == str2guid(#EmptyGuidString) || // Not matched
                  (lSKS_BR_BankTran.ManualReconciliationID != str2guid(#EmptyGuidString) && lSKS_BR_BankTran.AccountStatementNum == '')); // Pre-matched

        return lSKS_BR_BankTran.TranAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>importUnmatchedCount</Name>
				<Source><![CDATA[
    public static SKS_EB_Count importUnmatchedCount(BankAccountStatement _bankAccountStatement)
    {
        #SKS_Global
        SKS_BR_BankTran lSKS_BR_BankTran;

        select count(RecId) from lSKS_BR_BankTran
            where lSKS_BR_BankTran.OriginalStatementRecId == _bankAccountStatement.RecId &&
                  (lSKS_BR_BankTran.ManualReconciliationID == str2guid(#EmptyGuidString) || // Not matched
                  (lSKS_BR_BankTran.ManualReconciliationID != str2guid(#EmptyGuidString) && lSKS_BR_BankTran.AccountStatementNum == '')); // Pre-matched

        SKS_EB_Count recIdCnt = lSKS_BR_BankTran.RecId;

        return recIdCnt;
    }

]]></Source>
			</Method>
			<Method>
				<Name>lastStmtDate</Name>
				<Source><![CDATA[
    // Gets last statement date
    // Code is similar to openingBalance method in BankAccountStatement table
    public static Date lastStmtDate(BankAccountStatement _BankAccountStatement)
    {
        BankAccountStatement    bankAccountStatement;
        date                    openingDate;
        ;

        select reverse EndingBalance from bankAccountStatement
            order by AccountStatementDate, AccountStatementNum
            where bankAccountStatement.AccountId            == _BankAccountStatement.AccountId               &&
                  bankAccountStatement.AccountStatementDate <= _BankAccountStatement.AccountStatementDate    &&
                  bankAccountStatement.RecId                != _BankAccountStatement.RecId;
        if (bankAccountStatement.AccountStatementDate == _BankAccountStatement.AccountStatementDate)
        {
            while (bankAccountStatement.AccountStatementNum > _BankAccountStatement.AccountStatementNum)
            {
                next bankAccountStatement;
            }
        }
        if (bankAccountStatement)
        {
            openingDate = bankAccountStatement.AccountStatementDate;
        }

        return openingDate;
    }

]]></Source>
			</Method>
			<Method>
				<Name>ReconcileStatement</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckSelectForUpdateAbsent","Buffer locBankAccountStatement is selected forUpdate and is passed to other methods where it is modified.")]
    Public static void ReconcileStatement(SKS_BR_BankHeaderRecId _SKS_BR_BankHeaderRecId)
    {
        #SKS_Global

        str                                     locErrorMessage;
        SKS_BR_BankTran                         locSKS_BR_BankTran, locZBAParentSKS_BR_BankTran;
        BankAccountStatement                    locBankAccountStatement;
        BankAccountReconcile                    locBankAccountReconcile;
        SKS_PP_Bank                             locSKS_PP_Bank;
        boolean                                 reconciled = false; // 7647

        //Reconcile statements that have transactions
        while select crossCompany DataAreaId, OriginalStatementRecId, ZBAParentRecid from locSKS_BR_BankTran
                    group by locSKS_BR_BankTran.OriginalStatementRecId, ZBAParentRecid, locSKS_BR_BankTran.dataAreaId
                    where locSKS_BR_BankTran.BankHeaderRecId == _SKS_BR_BankHeaderRecId
        {
            changeCompany(locSKS_BR_BankTran.company())
            {
                locBankAccountStatement = null;
                select forUpdate locBankAccountStatement where locBankAccountStatement.RecId == locSKS_BR_BankTran.OriginalStatementRecId;

                //deteremine if AutoReconcile is on.
                locSKS_PP_Bank = null;
                //if not ZBA, then reconcile using this' account's settings (only SKS_BR_BANKTRANS blonging to a ZBA account will have a ZBA parent RecID)
                if(locSKS_BR_BankTran.ZBAParentRecid == 0)
                    Select firstfast firstonly BRAutoReconcile from locSKS_PP_Bank where locSKS_PP_Bank.BankAccountId == locBankAccountStatement.AccountId;
                else
                {
                    locZBAParentSKS_BR_BankTran = null;
                    select crossCompany AccountID, DataAreaId from  locZBAParentSKS_BR_BankTran where locZBAParentSKS_BR_BankTran.RecId == locSKS_BR_BankTran.ZBAParentRecid;
                    changeCompany(locZBAParentSKS_BR_BankTran.company())
                    {
                        Select firstfast firstonly BRAutoReconcile from locSKS_PP_Bank where locSKS_PP_Bank.BankAccountId == locZBAParentSKS_BR_BankTran.AccountId;
                    }
                }

                if(locSKS_PP_Bank.BRAutoReconcile)
                {
                    SKS_BR_BankAcctStmt::reconcileStatementForAcct(locBankAccountStatement);
                }
            }
        }

        // vvv 7647,10909,12958 vvv
        locBankAccountStatement = null;
        locSKS_PP_Bank = null;
        locSKS_BR_BankTran = null;
        while select forupdate crossCompany locBankAccountStatement where 
            locBankAccountStatement.ReconcileDate == dateNull() &&
            locBankAccountStatement.SKS_BR_BankHeaderRecId == _SKS_BR_BankHeaderRecId
            join BRAutoReconcile, RecId from locSKS_PP_Bank where
                    locSKS_PP_Bank.BRAutoReconcile == NoYes::Yes &&
                    locSKS_PP_Bank.BankAccountId == locBankAccountStatement.AccountId
            notexists join locSKS_BR_BankTran where
                    locSKS_BR_BankTran.OriginalStatementRecId == locBankAccountStatement.RecId
        {
            changecompany(locBankAccountStatement.company())
            {
                SKS_BR_BankAcctStmt::reconcileStatementForAcct(locBankAccountStatement);
            }
        }
        // ^^^ 7647,10909,12958 ^^^
    }

]]></Source>
			</Method>
			<Method>
				<Name>reconcileStatementForAcct</Name>
				<Source><![CDATA[
    public static void reconcileStatementForAcct(BankAccountStatement   _bankAccountStatement)
    {
        #SKS_Global

        str                                     locErrorMessage;
        BankAccountReconcile                    locBankAccountReconcile;
        if(SKS_BR_BankAcctStmt::importUnmatchedCount(_bankAccountStatement) > 0)
        {

            SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoReconcileFailed,
                                                _bankAccountStatement.SKS_BR_BankHeaderRecId, //Bug 10909
            _bankAccountStatement.AccountId
                                                ,_bankAccountStatement.AccountStatementDate,
                                                _bankAccountStatement.AccountStatementNum,
                                                '',
                                                "@SKS3047");
            return;
        }
        //Verify everything on the current statement balances
        if(SKS_BR_BankAcctStmt::unreconciledAmount(_bankAccountStatement) == 0)
        {
            try
            {
                //RECONCILE...
                info(strfmtLB(strFmt("@SKS3048",
                    _bankAccountStatement.AccountStatementNum,
                    date2Str(_bankAccountStatement.AccountStatementDate,-1,-1,-1,-1,-1,-1,DateFlags::None),
                    _bankAccountStatement.AccountId,
                    _bankAccountStatement.company())));

                locBankAccountReconcile = null;
                locBankAccountReconcile = new BankAccountReconcile();
                locBankAccountReconcile.setParameters(_bankAccountStatement);
                locBankAccountReconcile.run();

                info(strfmtLB(strFmt("@SKS3049",
                    _bankAccountStatement.AccountStatementNum,
                    date2Str(_bankAccountStatement.AccountStatementDate,-1,-1,-1,-1,-1,-1,DateFlags::None),
                    _bankAccountStatement.AccountId,
                    _bankAccountStatement.company())));

                SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoReconciled,
                                                _bankAccountStatement.SKS_BR_BankHeaderRecId, //Bug 10909
                _bankAccountStatement.AccountId
                                                ,_bankAccountStatement.AccountStatementDate,
                                                _bankAccountStatement.AccountStatementNum);
            }
            catch
            {
                locErrorMessage = strfmtLB(strFmt("@SKS3050",
                        _bankAccountStatement.AccountStatementNum,
                        SKS_PP_StringFormats::strDateMMDDYYYY(_bankAccountStatement.AccountStatementDate),
                        _bankAccountStatement.AccountId,
                        _bankAccountStatement.company()));
                SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoReconcileFailed,
                                                _bankAccountStatement.SKS_BR_BankHeaderRecId, //Bug 10909
                _bankAccountStatement.AccountId
                                                ,_bankAccountStatement.AccountStatementDate,
                                                _bankAccountStatement.AccountStatementNum,
                                                '',
                                                locErrorMessage);

                info(locErrorMessage);
            }
        }
        else
        {
            SKS_BR_HistoryTable::insertEvent(SKS_BR_Action::BankStatementAutoReconcileFailed,
                                                _bankAccountStatement.SKS_BR_BankHeaderRecId, //Bug 10909
            _bankAccountStatement.AccountId
                                                ,_bankAccountStatement.AccountStatementDate,
                                                _bankAccountStatement.AccountStatementNum,
                                                '',
                                                "@SKS3057");
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>unreconciledAmount</Name>
				<Source><![CDATA[
    public static AmountCur unreconciledAmount(BankAccountStatement _bankAccountStatement)
    {
        AmountCur                   clearedAmount;
        AmountCur                   missingAmount;
        AmountCur                   totalAmount;
        // 7653 -->
        BankAccountTrans            bankAccountTrans;
        BankTransTypeGroupDetails   bankTransGroupDetails;

        BankTransTypeGroup bankTransTypeGroup = new BankTransTypeGroup(_bankAccountStatement);

        //SKS_bug_8687 -->
        select sum(AmountCur), sum(AmountCorrect), count(AmountMST) from bankAccountTrans
            where bankAccountTrans.AccountId  == _bankAccountStatement.AccountId
                && bankAccountTrans.AccountStatementDate == _bankAccountStatement.AccountStatementDate
                && bankAccountTrans.AccountStatement == _bankAccountStatement.AccountStatementNum;

        clearedAmount += (bankAccountTrans.AmountCur + bankAccountTrans.AmountCorrect);
        bankTransTypeGroup.insertBankTransTypeSum(bankAccountTrans, bankTransGroupDetails.BankTransactionTypeGroup);
        missingAmount = (_bankAccountStatement.EndingBalance - _bankAccountStatement.openingBalance() - clearedAmount);
        //SKS_bug_8687 <--
        return missingAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>unreconciledAmountAggregate</Name>
				<Source><![CDATA[
    // 7542 - CR
    public static AmountCur unreconciledAmountAggregate(BankAccountStatement _bankAccountStatement)
    {
        AmountCur           clearedAmount;
        AmountCur           missingAmount;

        BankAccountStatement bankAccountStatement;

        while select bankAccountStatement
            order by AccountStatementDate
            where bankAccountStatement.AccountStatementDate <= _bankAccountStatement.AccountStatementDate
             && bankAccountStatement.AccountId == _bankAccountStatement.AccountId
        {
            BankTransTypeGroup bankTransTypeGroup = new BankTransTypeGroup(bankAccountStatement);
            clearedAmount = bankTransTypeGroup.createBankGroupSums();
            missingAmount += (bankAccountStatement.EndingBalance - bankAccountStatement.openingBalance() - clearedAmount);
        }
        return missingAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateReconcileStatement</Name>
				<Source><![CDATA[
    /// <summary>
    /// update the transaction which is reconciled through reconcile statement button
    /// </summary>
    /// CR-9689
    public static void updateReconcileStatement(BankAccountStatement _bankAccountStatement)
    {
        changecompany(_bankAccountStatement.DataAreaId)
        {
            BankAccountTrans bankAccountTrans;
            bankAccountTrans.skipDataMethods(true);

            ttsbegin;
            update_recordset bankAccountTrans
                setting SKS_BR_MarkedFromReconcileStatement = NoYes::Yes
                where bankAccountTrans.AccountId == _bankAccountStatement.AccountId
                    && bankAccountTrans.AccountStatement == _bankAccountStatement.AccountStatementNum
                    && bankAccountTrans.AccountStatementDate == _bankAccountStatement.AccountStatementDate
                    && bankAccountTrans.Included == NoYes::Yes
                    && bankAccountTrans.Reconciled == NoYes::Yes;

            ttscommit;
        }
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>