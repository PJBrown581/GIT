<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>WorkflowQueueDocumentViewTAS_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Extension class of WorkflowQueueDocumentView.
/// </summary>
[ExtensionOf(classStr(WorkflowQueueDocumentView))]
public final class WorkflowQueueDocumentViewTAS_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// This code checks if workflow record is a customer reimb journal or not.
    /// If so, then opens SKS cust reimb journal not the standard D365 cust payment.
    /// It changes "MenuItemName" field as a temporary.
    /// Task: 20279
    /// </summary>
    /// <param name = "_args">Arguments</param>
    public static void main(Args _args)
    {
        WorkflowWorkItemTable   workitemTable;

        if (_args.record().TableId == tableNum(WorkflowWorkItemTable))
        {
            workitemTable = WorkflowWorkItemTable::findRecId(_args.record().RecId);
        }
        else if (_args.record().TableId == tableNum(WorkflowTmpWorkList))
        {
            WorkflowTmpWorkList worklist = _args.record();
            workitemTable = WorkflowWorkItemTable::findRecId(worklist.WorkItemRecId);
        }

        if (workitemTable.RefRecId)
        {
            // SysWorkflowHelper::getCompanyId() was marked as an internal use only. I wrote the same logic directly to avoid BP warnings.
            CompanyId companyId = workitemTable.CompanyId != '' && strCmp(workitemTable.CompanyId, curext()) != 0 ? workitemTable.CompanyId : curext();

            changecompany(companyId)
            {
                Common rec = workitemTable.getRecord(false);

                // checks cust reimb journal
                if (rec.TableId == tableNum(LedgerJournalTable))
                {
                    LedgerJournalTable jourTable = rec;
                        
                    if (jourTable.JournalType == LedgerJournalType::CustPayment
                        && jourTable.ledgerJournalName().SKS_CNP_CustReimbursementJournal)
                    {
                        workitemTable.MenuItemName = menuItemDisplayStr(SKS_CNP_CustReimbursementJour);
                        _args.record(workitemTable);
                    }
                }
            }
        }

        // standard flow
        next main(_args);
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>