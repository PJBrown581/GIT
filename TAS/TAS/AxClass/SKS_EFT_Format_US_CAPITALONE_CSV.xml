<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_US_CAPITALONE_CSV</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_US_CAPITALONE_CSV
/// </summary>
class SKS_EFT_Format_US_CAPITALONE_CSV extends SKS_EFT_Format
{
    #define.FormatVersion('20221111')
    #define.Separator(',')

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '',  boolean _addSeparator = true)
    {
        str text;

        if(_addSeparator)
        {
            text = _field + #Separator;
        }
        else
        {
            text = _field;
        }

        return text;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckNestedLoopinCode","Necessary to allow the processing of every invoice(inner loop) for every relevant parent file detail record(outer loop)")]
    protected void formatStructure()
    {
        SKS_EFT_FileDetail invSKS_EFT_FileDetail;
        
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();

            while select invSKS_EFT_FileDetail where invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
            {
                //Invoice Record
                this.rec_InvoiceRecord(invSKS_EFT_FileDetail);
            }

            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_InvoiceRecord</Name>
				<Source><![CDATA[
    /// <summary>
    /// rec_InvoiceRecord
    /// </summary>
    /// <param name = "tmpSKS_EFT_FileDetail">tmpSKS_EFT_FileDetail</param>
    /// <returns>true</returns>
    boolean rec_InvoiceRecord(SKS_EFT_FileDetail tmpSKS_EFT_FileDetail)
    {
        AmountMST   gross = tmpSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur + tmpSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;

        System.Text.StringBuilder msb = new System.Text.StringBuilder();

        //  VENDOR NUMBER
        msb.Append(this.wrapField(mSKS_EFT_FileDetail.AccountNum));

        //  INVOICE NUMBER
        msb.Append(this.wrapField(this.CharacterReplacement(tmpSKS_EFT_FileDetail.InvoiceNumber)));

        //  INVOICE DATE
        msb.Append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(tmpSKS_EFT_FileDetail.InvoiceDate, DateSeparator::Slash)));

        //  PAYMENT AMOUNT
        msb.Append(this.wrapField(num2str(gross * -1,0,2,1,0)));

        //  DISCOUNT TAKEN
        msb.Append(this.wrapField(num2str(tmpSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur * -1,0,2,1,0)));

        //  NET PAYMENT AMOUNT
        msb.Append(this.wrapField(num2str(tmpSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1,0,2,1,0)));

        //  CHECK NUMBER
        msb.Append(this.wrapField(mSKS_EFT_FileDetail.PaymentReference, false));
        
        this.fileWrite(msb.ToString());

        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    [SuppressBPWarning('BPErrorSelectUsingFirstOnly', 'The buffer is used in next statement or in a datasource query loop.')]
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
            join mSKS_EFT_FileDetail_VendAddress
              order by mSKS_EFT_FileDetail.RecordSection,
                   mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_VendAddress.RefRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>