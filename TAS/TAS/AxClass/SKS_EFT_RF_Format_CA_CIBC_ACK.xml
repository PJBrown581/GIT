<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_RF_Format_CA_CIBC_ACK</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// Inbound ACK file format for the Outbound format SKS_EFT_format_CA_CIBC_1464
/// </summary>
class SKS_EFT_RF_Format_CA_CIBC_ACK extends SKS_EFT_RF_Format
{
    //Class Variables
    //primities
    boolean gErrorsExist;
    str mFileName;
    str mFileID;
    str mFieldDelimiter;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFile</Name>
				<Source><![CDATA[
    public SKS_EFT_RF_File getFile(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        TextStreamIo locTextIo;
        container locRecord;
        System.String locString;
        int i;  //index of the current location in the container
        SKS_EB_FileHub fileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        
        //Setup filestream
        fileHub.setFileName(_filename);
        fileHub.runFileLoad(_filename);
        fileHub.streamInit();
        locTextIo = TextStreamIo::ConstructForRead(fileHub.StreamGet());
        
        locTextIo.inFieldDelimiter(',');
        locTextIo.inRecordDelimiter('\n');

        if(!mSKS_EFT_RF_File)
        {
            mSKS_EFT_RF_File = new SKS_EFT_RF_File();
        }

        mSKS_EFT_RF_SentFile = new SKS_EFT_RF_SentFile();

        while(locTextIo.status() == IO_Status::Ok)
        {
            locRecord = locTextIo.read();

            if(conLen(locRecord) > 0)
            {
                //Remove any carriage Returns
                for(i = 1; i <= conLen(locRecord); i++)
                {
                    locRecord = conPoke(locRecord, i, strRem(conPeek(locRecord,i), '\r\n'));
                }

                locString = strLRTrim(conPeek(locRecord, 1));

                if(!System.String::IsNullOrEmpty(locString))
                {
                    if(!locString.StartsWith('Initiating Party Name'))
                    {
                        if(conPeek(locRecord,6) != '' &&
                            (conPeek(locRecord,15) == 'PDNG' ||
                             conPeek(locRecord,15) == 'RJCT'||
                             conPeek(locRecord,15) == 'ACSC'||
                             conPeek(locRecord,15) == 'ACSP'||
                             conPeek(locRecord,15) == 'ACCP'))
                        {
                            this.processNode(locRecord);
                        }
                    }
                }
            }
        }

        if(locTextIo)
        {
            locTextIo.finalize();
            locTextIo = null;
        }
    
        if(gErrorsExist)
        {
            mSKS_EFT_RF_File = null;
        }

        return this.getSKS_EFT_RF_File();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFormatId</Name>
				<Source><![CDATA[
    public str getFormatId(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        str rtnval = '';
        int classId;
        CommaStreamIo locCommaIo;
        int i;  //index of the current location in the container
        container locRecord;
        boolean FirstRecordFound = false;
        boolean SecondRecordFound = false;
        System.String locString;
        SKS_EB_FileHub fileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        
        //Setup filestream
        fileHub.setFileName(_filename);
        fileHub.runFileLoad(_filename);
        fileHub.streamInit();
        locCommaIo = CommaStreamIo::ConstructForRead(fileHub.StreamGet());
        mFieldDelimiter = ',';
        locCommaIo.inFieldDelimiter(mFieldDelimiter);
        locCommaIo.inRecordDelimiter('\n');
        while(locCommaIo.status() == IO_Status::Ok)
        {
            locRecord = locCommaIo.read();

            if(conLen(locRecord) > 0)
            {
                for(i = 1; i <= conLen(locRecord); i++)
                {
                    locRecord = conPoke(locRecord, i, strRem(conPeek(locRecord,i), '\r\n'));
                }

                locString = strLRTrim(conPeek(locRecord, 1));

                if(!System.String::IsNullOrEmpty(locString))
                {
                    if(!locString.StartsWith('Initiating Party Name'))
                    {
                        if(conPeek(locRecord,6) != '')
                        {
                            FirstRecordFound = true;
                        }

                        if(conPeek(locRecord,15) == 'PDNG' ||
                            conPeek(locRecord,15) == 'RJCT'||
                            conPeek(locRecord,15) == 'ACSC'||
                            conPeek(locRecord,15) == 'ACSP'||
                            conPeek(locRecord,15) == 'ACCP')
                        {
                            SecondRecordFound = true;
                        }
                    }
                }
                if(FirstRecordFound && SecondRecordFound)
                {
                    break;
                }
            }
        }

        if(FirstRecordFound && SecondRecordFound)
        {
            //ALl Qualifers show this a
            classId = classIdGet(this);
            rtnval = classId2Name(ClassId);
        }
        if(locCommaIo)
        {
            locCommaIo.finalize();
            locCommaIo = null;
        }

        return rtnval;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getSKS_EFT_RF_File</Name>
				<Source><![CDATA[
    public SKS_EFT_RF_File getSKS_EFT_RF_File()
    {

        if(mSKS_EFT_RF_SentFile != null)
        {
            mSKS_EFT_RF_File.addSentFile(mSKS_EFT_RF_SentFile);
            mSKS_EFT_RF_SentFile = null;
        }

        return mSKS_EFT_RF_File;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processNode</Name>
				<Source><![CDATA[
    private void processNode(container locRecord)
    {
        SKS_EFT_RF_SentFile locSKS_EFT_RF_SentFile;
        System.String fileKey, fileStatus;
        ;

        fileKey = strLRTrim(conPeek(locRecord, 6));
        fileStatus = strLRTrim(conPeek(locRecord, 15));

        locSKS_EFT_RF_SentFile = new SKS_EFT_RF_SentFile();

        //Set the File Reference Number
        if(!System.String::IsNullOrEmpty(fileKey))
        {
            locSKS_EFT_RF_SentFile.setID(fileKey);
        }

        //Set the File Status
        if(!System.String::IsNullOrEmpty(fileStatus))
        {
            locSKS_EFT_RF_SentFile.setStatus(fileStatus);
        }
        mSKS_EFT_RF_File.addSentFile(locSKS_EFT_RF_SentFile);
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFileKey</Name>
				<Source><![CDATA[
    protected void setFileKey(str _InboundFileKey)
    {
        mSKS_EFT_RF_SentFile.setID(_InboundFileKey);
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundFileKeyGen</Name>
				<Source><![CDATA[
    public static str outboundFileKeyGen(str _OutFilename, SKS_EFTAP_File locSKS_EFTAP_File = null, System.IO.StreamReader _StreamReader = null)
    {
        SKS_EFT_FileData locSKS_EFT_FileData;
        System.Text.StringBuilder sb;
        ;
    
        sb = new System.Text.StringBuilder();

        Select firstonly FieldValue from locSKS_EFT_FileData
            where locSKS_EFT_FileData.RefRecId == locSKS_EFTAP_File.RecId
            && locSKS_EFT_FileData.FieldName == 'FileCreationNumber';

        sb.Append(locSKS_EFT_FileData.FieldValue);
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundTransKeyGen</Name>
				<Source><![CDATA[
    public static str outboundTransKeyGen(str _FileName = '', SKS_EFT_FileDetail _SKS_EFT_FileDetail = null, SKS_EFTAP_File _SKS_EFTAP_FILE = null)
    {
        //THIS FORMAT IS ONLY FOR FILE LEVEL.  There will never be trans keys.
        return '';
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>