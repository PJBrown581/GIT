<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_RF_Format_WF_PM_WIRE_CONF_062013</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_EFT_RF_Format_WF_PM_WIRE_CONF_062013 extends SKS_EFT_RF_Format
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFile</Name>
				<Source><![CDATA[
    //OUTBOUND:  US_WF_PMS_XML_R1201212
    public SKS_EFT_RF_File getFile(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        //OUTBOUND:  US_WF_PMS_XML_R1201212
        SKS_EB_FileHub locSKS_EB_FileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        System.Xml.XmlDocument locXmlDocument;
        System.Xml.XmlNode locXMLNode;
        str locTempStr;

        locXmlDocument = new System.Xml.XmlDocument();
        locSKS_EB_FileHub.runFileLoad(_filename);
        locXmlDocument.LoadXML(locSKS_EB_FileHub.fileBody());

        if(locXmlDocument.get_HasChildNodes())
        {
            locXMLNode = locXmlDocument.get_FirstChild(); //<?XML ... node.
            locXMLNode = locXMLNode.get_NextSibling();//<Document xmlns...

            locTempStr = locXMLNode.get_Name();
            locTempStr = strUpr(locTempStr);
            if(locTempStr == 'MESSAGE')
            {
                this.processNode(locXMLNode);
            }
        }

        return mSKS_EFT_RF_File;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFormatId</Name>
				<Source><![CDATA[
    public str getFormatId(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        //OUTBOUND:  US_WF_PMS_XML_R1201212
        SKS_EB_FileHub locSKS_EB_FileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        str rtnval = '';
        int classId;
        //BEGIN VALIDATION VARIABLES
        //BEGIN QUALIFIERS
        boolean locXmlTagFound = false;
        boolean locMESSAGEFound = false;
        boolean locTRNFound = false;
        boolean locTRN_REFFound = false;
        boolean locTRANSACTION_TYPEFound = false;
        //none
        //END VALIDATION VARIABLES

        System.String locString;

        locSKS_EB_FileHub.setFileName(_filename);
        locSKS_EB_FileHub.runFileInquire();
        if(!locSKS_EB_FileHub.attrFileExists())
        {
            // The file %1 does not exist.
            error(strfmt("@SYS62207", _filename));
            return null;
        }
        locSKS_EB_FileHub.runFileLoad(_filename);
        locSKS_EB_FileHub.StreamInit();
        System.IO.StreamReader locStreamReader = new System.IO.StreamReader(locSKS_EB_FileHub.streamGet());
        
        //priming read
        locString = locStreamReader.ReadLine();
        locString = locString.ToUpper();
        while(locString != null)
        {
            locString = locString.Trim();
            if(!System.String::IsNullOrWhiteSpace(locString))
            {
                if(!locXMLTagFound)
                {
                    if(locString.StartsWith(strUpr('<?xml')))
                    locXmlTagFound = true;
                    else
                    {
                        //NOT AN XML DOCUMENT
                        break;
                    }
                }
                if(locString.Contains(strUpr('<MESSAGE>')))
                {
                    locMESSAGEFound = true;
                }
                else if(locMESSAGEFound && locString.Contains(strUpr('<TRN>')))
                {
                    locTRNFound = true;
                }
                else if(locTRNFound && locString.Contains(strUpr('<TRN_REF>')))
                {
                    locTRN_REFFound = true;
                }
                else if(locTRN_REFFound && locString.Contains(strUpr('<TRANSACTION_TYPE>')))
                {
                    locTRANSACTION_TYPEFound = true;
                }
                if(locXmlTagFound
                && locMESSAGEFound
                && locTRNFound
                && locTRN_REFFound
                && locTRANSACTION_TYPEFound)
                {
                    break;
                }
            }
            locString = locStreamReader.ReadLine();
            if(!System.String::IsNullOrEmpty(locString))
            locString = locString.ToUpper();
        }
        if(locXmlTagFound
        && locMESSAGEFound
        && locTRNFound
        && locTRN_REFFound
        && locTRANSACTION_TYPEFound)
        {
            //ALl Qualifers show this a PSR-ISO20022-00200102 file
            classId = classIdGet(this);
            rtnval = classId2Name(ClassId);
        }
        locStreamReader.Close();
        return rtnval;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processNode</Name>
				<Source><![CDATA[
    public void processNode(System.Xml.XmlNode _XmlNode)
    {
        //OUTBOUND:  US_WF_PMS_XML_R1201212
        System.Xml.XmlNode locXMLNode;
        System.Xml.XmlNodeList locXMLNodeList;
        System.Collections.IEnumerator locXMLNodeListEnum;
        str locNodeName;
        str locTempStr;

        locTempStr = _XMLNode.get_Name();
        locTempStr = strUpr(locTempStr);
        switch(locTempStr)
        {
            case 'MESSAGE':
                mSKS_EFT_RF_File = new SKS_EFT_RF_File();
                mSKS_EFT_RF_SentFile = new SKS_EFT_RF_SentFile();
                break;
            case 'TRN':
                mSKS_EFT_RF_Transaction = new SKS_EFT_RF_Transaction();
                break;
            case 'ORF_ID':
                mSKS_EFT_RF_Transaction.setID(_XmlNode.get_InnerText());
                break;
            case 'TRANSACTION_TYPE':
                mSKS_EFT_RF_Transaction.setStatus(_XmlNode.get_InnerText());
                break;
            case  'BNF_NAME1':
                mSKS_EFT_RF_Transaction.setAdditionalData(_XmlNode.get_InnerText());
                break;
        }

        if(_XmlNode.get_HasChildNodes())
        {
            //priming read
            locXMLNodeList = _XmlNode.get_ChildNodes();
            locXMLNodeListEnum = locXMLNodeList.GetEnumerator();
            locXMLNodeListEnum.Reset();
            while(locXMLNodeListEnum.MoveNext())
            {
                locXMLNode = locXMLNodeListEnum.get_Current();
                locNodeName = locXMLNode.get_Name();
                if(strUpr(locNodeName) != '#TEXT')
                this.processNode(locXMLNode);
            }

        }
        switch(locTempStr)
        {
            case 'MESSAGE':
                mSKS_EFT_RF_File.addSentFile(mSKS_EFT_RF_SentFile);
                break;
            case 'TRN':
                mSKS_EFT_RF_SentFile.addTransaction(mSKS_EFT_RF_Transaction);
                mSKS_EFT_RF_Transaction = null;
                break;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFilekey</Name>
				<Source><![CDATA[
    protected void setFilekey(str _InboundFileKey)
    {
        mSKS_EFT_RF_SentFile.setID(_InboundFileKey);
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundFileKeyGen</Name>
				<Source><![CDATA[
    public static str outboundFileKeyGen(str _OutFilename, SKS_EFTAP_File locSKS_EFTAP_File = null)
    {
        //OUTBOUND:  US_WF_PMS_XML_R1201212
        //we don't actually tie this format together on the file-level.  but, for diagnostic reasons, we'll keep the
        return int642str(locSKS_EFTAP_File.RecId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundTransKeyGen</Name>
				<Source><![CDATA[
    public static str outboundTransKeyGen(str _FileName = '', SKS_EFT_FileDetail _SKS_EFT_FileDetail = null, SKS_EFTAP_File _SKS_EFTAP_FILE = null)
    {
        //OUTBOUND:  US_WF_PMS_XML_R1201212
        str rtnval;
        SKS_EFT_FileFormatPaymSpec locSKS_EFT_FileFormatPaymSpec;
    
        select TransactionCode from locSKS_EFT_FileFormatPaymSpec
            where  locSKS_EFT_FileFormatPaymSpec.FormatId == _SKS_EFT_FileDetail.FormatId
                && locSKS_EFT_FileFormatPaymSpec.PaymSpec == _SKS_EFT_FileDetail.PaymSpec;
    
        if(strUpr(locSKS_EFT_FileFormatPaymSpec.TransactionCode) == strUpr('CHK'))
        {
            rtnval = int642Str(_SKS_EFT_FileDetail.PaymentLineRecId);
        }
        else
        {
            rtnval = _SKS_EFT_FileDetail.PaymentReference;
        }
        return rtnval;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>