<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Common</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_EFT_Common
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>buildFileName</Name>
				<Source><![CDATA[
    public server static SKS_EB_FileName buildFileName(SKS_EFT_FileNbr _SKS_EFT_FileNbr, SKS_EB_FileName _ebFileName, str _connector, boolean _addFileNbr, boolean _addFileDate)
    {
        SKS_EB_FileName locFileName;
        str locExtension;
        int Pos;
        ;
    
    
    
        if (_addFileNbr == NoYes::Yes || _addFileDate == NoYes::Yes)
        {
            Pos = strFind(_ebFileName, '.', 80, -80);
            if (Pos != 0)
            {
                locFileName = substr(_ebFileName,0,Pos-1);
                locExtension = substr(_ebFileName, Pos, strlen(_ebFileName));
            }
            else
            {
                locFileName = _ebFileName;
                locExtension ='';
            }
    
            if (_addFileDate == NoYes::Yes)
            {
                locFileName = locFileName + _connector + Date2Str(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone()),321,2,0,2,0,4,DateFlags::None);
            }
    
            if (_addFileNbr == NoYes::Yes)
            {
                locFileName = locFileName + _connector + _SKS_EFT_FileNbr;
            }
    
            locFileName =  locFileName + locExtension;
        }
        else
        {
            locFileName = _ebFileName;
        }
        return locFileName;
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>getAccountNum</Name>
				<Source><![CDATA[
    static DimensionDisplayValue getAccountNum(DimensionDynamicAccount _ledgerDimension)
    {
        DimensionAttributeValueCombination  ledgerDimension;
        ;
    
        select firstOnly DisplayValue from ledgerDimension where
                ledgerDimension.RecId == _ledgerDimension;
    
        return ledgerDimension.DisplayValue;
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustomerName</Name>
				<Source><![CDATA[
    static DirPartyName getCustomerName(SelectableDataArea _company, CustVendAC _custAccount)
    {
        CustName        custName;
        CustTable       custTable;
        ;
    
        changecompany(_company)
        {
            if (_custAccount != '')
            {
                select firstonly Party from custTable
                    where custTable.AccountNum == _custAccount;
            }
        }
    
        custName = SKS_EFT_Common::getCustomerNameByParty(custTable.Party);
    
        return custName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getCustomerNameByParty</Name>
				<Source><![CDATA[
    static DirPartyName getCustomerNameByParty(RecId _party)
    {
        CustName        custName;
        ;
    
        custName = DirPartyTable::getName(_party);
    
        return custName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFileName</Name>
				<Source><![CDATA[
    public server static SKS_EB_FileName getFileName(SKS_EFT_FileNbr _SKS_EFT_FileNbr, SKS_EB_FileName _ebFileName, SKS_EFT_Module _module)
    {
        SKS_EB_FileName locFileName;
        SKS_EFT_Setup locSKS_EFT_Setup;
        NoYes addFileNbr;
        NoYes addFileDate;
        str connector;
        ;
    
        if(_module == SKS_EFT_Module::AR)
        {
            select firstonly ARFileAddEBNbr, ARFileAddDate, ARFileNameConnector from  locSKS_EFT_Setup;
            addFileNbr = locSKS_EFT_Setup.ARFileAddEBNbr;
            addFileDate = locSKS_EFT_Setup.ARFileAddDate;
            connector = locSKS_EFT_Setup.ARFileNameConnector;
        }
        else if(_module == SKS_EFT_Module::BTB)
        {
            select firstonly BTBFileAddEBNbr, BTBFileAddDate, BTBFileNameConnector from  locSKS_EFT_Setup;
            addFileNbr = locSKS_EFT_Setup.BTBFileAddEBNbr;
            addFileDate = locSKS_EFT_Setup.BTBFileAddDate;
            connector = locSKS_EFT_Setup.BTBFileNameConnector;
        }
        else
        {
            select firstonly APFileAddEBNbr, APFileAddDate, APFileNameConnector from  locSKS_EFT_Setup;
            addFileNbr = locSKS_EFT_Setup.APFileAddEBNbr;
            addFileDate = locSKS_EFT_Setup.APFileAddDate;
            connector = locSKS_EFT_Setup.APFileNameConnector;
        }
    
        locFileName = SKS_EFT_Common::buildFileName(_SKS_EFT_FileNbr, _ebFileName, connector, addFileNbr, addFileDate);
    
        return locFileName;
    
    }

]]></Source>
			</Method>
			<Method>
				<Name>getListOfRegisteredModules</Name>
				<Source><![CDATA[
    public static List getListOfRegisteredModules()
    {
        str locFormatFieldPrefix;
        str locName;
        List locListOfModules = new List(Types::String);
        TreeNode locTreeNode;
        TreeNode locChildNode;
    
    
        //This block of code determines how many key fields exist in a single record
        locFormatFieldPrefix = 'REG';
        locTreeNode = TreeNode::findNode(@"\Data Dictionary\Tables\SKS_PP_Setup\Fields");
        if(locTreeNode.AOTchildNodeCount() > 0)
        {
            locChildNode = locTreeNode.AOTfirstChild();
            while(locChildNode != null)
            {
                locName = locChildNode.treeNodeName();
                if  (strStartsWith(strUpr(locName), strUpr(locFormatFieldPrefix)) && 
                        (
                            strUpr(locName) != 'REGAUTHORIZEDUSERS'
                            && strUpr(locName) != 'REGCUSTOMERID'
                            && strUpr(locName) != 'REGUNLOCKKEY'
                            && strUpr(locName) != 'REGVERSION'
                        )
                    )
                {
                    locListOfModules.addEnd(locName);
                }
                locChildNode = locChildNode.AOTnextSibling();
            }
        }
        return locListOfModules;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getNumberOfFormats</Name>
				<Source><![CDATA[
    public static int getNumberOfFormats()
    {
        str locFormatFieldPrefix;
        str locName;
        int locNumberOfFormats;
        TreeNode locTreeNode;
        TreeNode locChildNode;
    
    
        //This block of code determines how many key fields exist in a single record
        locFormatFieldPrefix = 'INBOUNDFORMATID';
        locNumberOfFormats = 0;
        locTreeNode = TreeNode::findNode(@"\Data Dictionary\Tables\SKS_EFT_InboundFileGroups\Fields");
        if(locTreeNode.AOTchildNodeCount() > 0)
        {
            locChildNode = locTreeNode.AOTfirstChild();
            while(locChildNode != null)
            {
                locName = locChildNode.treeNodeName();
                if(strStartsWith(str2Capital(locName), str2Capital(locFormatFieldPrefix)))
                {
                    locNumberOfFormats++;
                }
                locChildNode = locChildNode.AOTnextSibling();
            }
        }
        return locNumberOfFormats;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVendorName</Name>
				<Source><![CDATA[
    static DirPartyName getVendorName(SelectableDataArea _company, CustVendAC _vendAccount)
    {
        VendName        vendName;
        VendTable       vendTable;
        ;
    
        changecompany(_company)
        {
            if (_vendAccount != '')
            {
                select firstonly Party from vendTable
                    where vendTable.AccountNum == _vendAccount;
            }
        }
    
        vendName = SKS_EFT_Common::getVendorNameByParty(vendTable.Party);
    
        return vendName;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVendorNameByParty</Name>
				<Source><![CDATA[
    static DirPartyName getVendorNameByParty(RecId _party)
    {
        VendName        vendName;
        ;
    
        vendName = DirPartyTable::getName(_party);
    
        return vendName;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>