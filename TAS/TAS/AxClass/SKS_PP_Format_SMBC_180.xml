<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_SMBC_180</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_PP_Format_SMBC_180
/// </summary>
class SKS_PP_Format_SMBC_180 extends SKS_PP_Format_BankGroup
{
    int     chkCount;
    real    chkSum;
    Str     BankIdent;
    Str     BnkBankAct;

    // SMBC Bank 180 - Detail Record
    
    // Pos 01-01    1        Record Type 'D'
    // Pos 02-04    3        Bank Market Identifier
    // Pos 05-14    10       Bank Account number
    // Pos 15-15    1        Void check indicator ('V'oid, 'I' otherwise)
    // Pos 16-23    8        Issue/Void Date MMDDYYYY
    // Pos 24-33    10       Check Number
    // Pos 34-45    12       Check Amount
    // Pos 46-60    15       Additional data
    // Pos 61-80    20       Filler
    // Pos 81-130   50       Payee Line 1
    // Pos 131-180  50       Payee Line 2

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        int Mult = 1;
        str ItemType;
        Container company;
        DirPartyNamePrimaryAddressView DirPartyNamePrimaryAddressView;
        VendTable   vendtable;
        ;
    
        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = 'V';
            if(mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = 'I';
        }
    
        chkCount++;
        chkSum = chkSum + mSKS_PP_FileDetail.ChkCuryAmt;    
        
        BnkBankAct = mSKS_PP_FileDetail.BnkBankAcct;
    
        sb.Append('D');
        sb.Append(SKS_PP_StringFormats::strFill(3, BankIdent, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(ItemType);
    
        if (ItemType == 'I')
        {
            sb.Append(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_PP_FileDetail.ChkDocDate));
        }
        else
        {
            sb.Append(SKS_PP_StringFormats::strDateMMDDYYYY(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone())));
        }
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 12));
        sb.Append(SKS_PP_StringFormats::strFill(15, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(20, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(50, mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        company = [mSKS_PP_FileDetail.ChkCpnyId];
        select firstonly crossCompany:company Street               
            from DirPartyNamePrimaryAddressView
            join vendtable
            where DirPartyNamePrimaryAddressView.Party == vendtable.Party &&
                vendtable.AccountNum == mSKS_PP_FileDetail.VendorAccount;

        sb.Append(SKS_PP_StringFormats::strFill(50, strReplace(DirPartyNamePrimaryAddressView.Street,'\n',' '), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupHeader</Name>
				<Source><![CDATA[
    // SMBC Bank 180 - Header Record
    
    // Pos 01-01    1        Record Identifier
    // Pos 02-04    3        Bank Market Identifier
    // Pos 05-14    10       Bank Account Number
    // Pos 15-22    8        File Creation Date MMDDCCYY
    // Pos 23-47    25       Client Name
    // Pos 48-25    5        Format ID '03121'
    // Pos 53-180   128      Space Filler

    protected str txtGroupHeader()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        SKS_PP_Bank bSKS_PP_Bank_1;
        Container company;
        ;
    
        company = [mSKS_PP_FileDetail.ChkCpnyId];
        select firstonly crossCompany:company PP01, PP02 from bSKS_PP_Bank_1 where bSKS_PP_Bank_1.BankAccountId == mSKS_PP_FileDetail.ChkBankAcct;
        BankIdent =  bSKS_PP_Bank_1.PP01;
    
        sb.Append('H');
        sb.Append(SKS_PP_StringFormats::strFill(3, BankIdent, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10,mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strDateMMDDYYYY(DateTimeUtil::getSystemDate(DateTimeUtil::getUserPreferredTimeZone())));
        sb.Append(SKS_PP_StringFormats::strFill(25,bSKS_PP_Bank_1.PP02, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append('03121');
        sb.Append(SKS_PP_StringFormats::strFill(128,'', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    // SMBC Bank 180 - Trailer Record
    
    // Pos 01-01    1        Record Type 'T'
    // Pos 02-04    3        Bank Market Identifier
    // Pos 05-14    10       Bank Account Number
    // Pos 15-21    7        Filler 
    // Pos 22-31    10       Total Detail Records
    // Pos 32-43    12       Total Dollar Amount
    // Pos 44-180   137      Filler Spaces

    protected str txtGroupTrailer()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('T');
        sb.Append(SKS_PP_StringFormats::strFill(3, BankIdent, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10, BnkBankAct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(7, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(chkCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(chkSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(137, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        chkSum = 0;
        chkCount = 0;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>