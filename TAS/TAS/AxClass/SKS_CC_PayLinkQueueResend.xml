<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_CC_PayLinkQueueResend</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_CC_PayLinkQueueResend extends RunBase
{
    SKS_CC_PayLinkQueue payLinkQueueCaller;
    CustAccount custAccount;
    RefTableId oneTimeCustRefTableId;
    RefRecId oneTimeCustRefRecId;
    String50 payFabricCustomerNbr;    
    const str constEmail = 'Email';
    const str constSMS = 'SMS';
    boolean isSilent;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>run</Name>
				<Source><![CDATA[
    public void run()
    {
        boolean resendQueueRecordResponse;
        if (payLinkQueueCaller)
        {
            resendQueueRecordResponse = this.ResendQueueNotification();
        }        

        if (resendQueueRecordResponse)
        {
            info(strFmt("@SKS:SKGCCQueueRecordResendSuccess",
                payLinkQueueCaller.Email ? constEmail : constSMS, payLinkQueueCaller.Email ? payLinkQueueCaller.Email : payLinkQueueCaller.SMS));
        }        
    }

]]></Source>
			</Method>
			<Method>
				<Name>dialog</Name>
				<Source><![CDATA[
    public Object dialog()
    {
        DialogRunbase dialog;

        dialog = super();        

        dialog.addText(strFmt("@SKS:SKGCCResendNotificationMsg", enum2Str(payLinkQueueCaller.TransType),
            payLinkQueueCaller.Email ? constEmail : constSMS, payLinkQueueCaller.Email ? payLinkQueueCaller.Email : payLinkQueueCaller.SMS), 500);

        return dialog;
    }

]]></Source>
			</Method>
			<Method>
				<Name>ResendQueueNotification</Name>
				<Source><![CDATA[
    public boolean ResendQueueNotification()
    {
        boolean retStatus;
        SKS_CC_PayFabricWalletHandler pfWalletHandler = SKS_CC_PayFabricWalletHandler::construct();
              
        pfWalletHandler.parmCustAccount(custAccount);
        if (oneTimeCustRefTableId && oneTimeCustRefRecId)
        {
            pfWalletHandler.parmOneTimeCustRefTableId(oneTimeCustRefTableId);
            pfWalletHandler.parmOneTimeCustRefRecId(oneTimeCustRefRecId);
        }
        pfWalletHandler.setPayFabricCustomerNbr();

        retStatus = pfWalletHandler.ResendLinkNotification(payLinkQueueCaller, isSilent);

        return retStatus;
    }

]]></Source>
			</Method>
			<Method>
				<Name>setValuesFromCallingRecord</Name>
				<Source><![CDATA[
    public void setValuesFromCallingRecord(SKS_CC_PayLinkQueue _callerPayLinkQueue, boolean _isSilent = true)
    {
        payLinkQueueCaller = _callerPayLinkQueue;
        if (payLinkQueueCaller.Status != SKS_CC_PayLinkProcessStatus::Pending &&
            payLinkQueueCaller.Status != SKS_CC_PayLinkProcessStatus::Error)
        {
            throw error(strFmt("@SKS:SKGCCInvalidStatusForResending", enum2Str(payLinkQueueCaller.Status)));
        }

        custAccount = payLinkQueueCaller.AccountNum;
        payFabricCustomerNbr = payLinkQueueCaller.PFCustomerNbr;
        
        if (CustTable::find(custAccount).OneTimeCustomer)
        {
            oneTimeCustRefTableId = payLinkQueueCaller.RefTableId;
            oneTimeCustRefRecId = payLinkQueueCaller.RefRecId;           
        }

        isSilent = _isSilent;
    }

]]></Source>
			</Method>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    public static void main(Args _args)
    {
        SKS_CC_PayLinkQueueResend payLinkQueueResend = SKS_CC_PayLinkQueueResend::construct();

        SKS_PP_Setup sKS_PP_Setup = SKS_PP_Setup::find();

        if (sks_pp_setup.ModCC == NoYes::No ||
            (sks_pp_setup.CCPLEnableWalletLinks == NoYes::No &&
             sks_pp_setup.CCPLEnablePayLinks == NoYes::No))
        {
            throw error("@SKS:SKGCCResendLinkIsNotAllowedDueToParameters");
        }

        if (!sKS_PP_Setup.CCPayLinkEnumUpgraded)
        {
            throw error("@SKS:SKGCCRefreshLinkIsNotAllowedDueToUpgrade");
        }        

        if (!_args)
        {
            throw error(Error::wrongUseOfFunction(funcName()));
        }

        if (!_args.record())
        {
            throw error(Error::missingRecord(funcName()));
        }

        if (_args.dataset() != tableNum(SKS_CC_PayLinkQueue))
        {
            throw error(Error::missingRecord(funcName()));
        }

        payLinkQueueResend.setValuesFromCallingRecord(_args.record(), false);

        if (payLinkQueueResend.prompt())
        {
            payLinkQueueResend.run();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>construct</Name>
				<Source><![CDATA[
    public static SKS_CC_PayLinkQueueResend construct()
    {
        return new SKS_CC_PayLinkQueueResend();
    }

]]></Source>
			</Method>
			<Method>
				<Name>description</Name>
				<Source><![CDATA[
    public server static ClassDescription description()
    {
        return "@SKS:SKGCCResendNotification";
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>