<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_CA_BMO_1464</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_CA_BMO_1464 extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_CA_BMO_1464 extends SKS_EFT_Format
{
    #define.FormatVersion('20221116')
    #define.PAYMT('PMT')
    #define.LineEnd('\r\n')
    str mControlID;
    System.Text.StringBuilder msb;

    int mLogicRecCount;
    int mCRecordCount;
    int mDRecordCount;

    AmountMST mPaymentAmount;
    AmountMST mWithdrawalAmount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {
        int rec = 1;
        RecId nextRecId;
        msb = new System.Text.StringBuilder();
        mLogicRecCount = 0;
        AmountMST nextAmount;
        AmountMST prevAmount;
        boolean firstnegetiveAmount = true;
    
        // Format header
        this.rec_File_Header();
    
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();
    
            if(mSKS_EFT_FileDetail.Module == SKS_EFT_Module::AP)
            {
                // Get SKS_EFT_VendorPaym
                this.getSKS_EFT_VendorPaym();
    
                // Detail Record
                this.rec_Detail_C(rec == 1);
            }
            else
            {
                // Get SKS_EFT_CustomerPaym
                this.getSKS_EFT_CustomerPaym();
    
                // Detail Record
                this.rec_Detail_D(rec == 1);
            }
            prevAmount = mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur ;
            next mSKS_EFT_FileDetail;
            nextRecId = mSKS_EFT_FileDetail.recId;
            nextAmount = mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;

            if(rec < 6)
            {
                if(nextRecId != 0)
                {
                    if(nextAmount > 0 && prevAmount < 0)
                    {
                        if(firstnegetiveAmount)
                        {
                            firstnegetiveAmount = false;
                            this.padRecord(rec);
                            msb.Append(#LineEnd);
                            rec = 1;
                        }
                        else
                        {
                            rec++;
                        }
                    }
                    else
                    {
                        rec++;
                    }
                }
                else
                {
                    this.padRecord(rec);
                    msb.Append(#LineEnd);
                }
            }
            else
            {
                rec = 1;
                msb.Append(#LineEnd);
            }
        }
    
        // Format trailer
        this.rec_Trailer();
    
        // Write to file
        this.fileWrite(msb.ToString());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJulianDate</Name>
				<Source><![CDATA[
    private str getJulianDate(date _Date)
    {
        str value;
    
        value = '0' + subStr(date2julian(_Date),3,5);
    
        return value;
    }

]]></Source>
			</Method>
			<Method>
				<Name>padRecord</Name>
				<Source><![CDATA[
    boolean padRecord(int _records)
    {
        //Fill remaining segments with blanks
        int x;
    
        for(x = _records; x < 6; x++)
        {
            msb.Append(SKS_PP_StringFormats::strFill(240, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Detail_C</Name>
				<Source><![CDATA[
    boolean rec_Detail_C(boolean _newRec)
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Content                                    Position    Size
        // 1    Record Type                             (        1)       1
        // 2    Logical Record Count                    (   2-  10)       9
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        //--
        // 4    Transaction Type Code                   (  25-  27)       3
        // 5    Amount                                  (  28-  37)      10
        // 6    Payable Date                            (  38-  43)       6
        // 7    Payee Institutional ID                  (  44-  52)       9
        // 8    Payee Account Number                    (  53-  64)      12
        // 9    Filler                                  (  65-  89)      25
        //10    Originator's Short Name                 (  90- 104)      15
        //11    Payee Name                              ( 105- 134)      30
        //12    Originator's Long Name                  ( 135- 164)      30
        //13    Filler                                  ( 165- 174)      10
        //14    Cross Reference No.                     ( 175- 193)      19
        //15    Institutional return ID                 ( 194- 202)       9
        //16    Account Number for return               ( 203- 214)      12
        //17    Filler                                  ( 215- 253)      39
        //18    Filler                                  ( 254- 264)      11
        //--    Repeat 4-18 for each additional deposit segment
        //------------------------------------------------------------------------------------------------------
        if(_newRec)
        {
            // 1    Record Type                             (        1)       1
            // 2    Logical Record Count                    (   2-  10)       9
            // 3    Originator ID + File Creation Number    (  11-  24)      14
            this.rec_FileLead('C');
        }
        mCRecordCount++;
    
        // 4    Transaction Type Code                   (  25-  27)       3
        msb.Append(mSKS_EFT_FileFormatPaymSpec.TransactionCode);
    
        // 5    Amount                                  (  28-  37)      10
        mPaymentAmount += mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;
        msb.Append(SKS_PP_StringFormats::strMoney(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1, 10));
    
        // 6    Payable Date                            (  38-  43)       6
        msb.Append(this.getJulianDate(mSKS_EFT_File.EffectiveDate));
    
        // 7    Payee Institutional ID                  (  44-  52)       9
        msb.Append(SKS_PP_StringFormats::strFill(9,mSKS_EFT_FileDetail.DepBankTransit, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 8    Payee Account Number                    (  53-  64)      12
        msb.Append(SKS_PP_StringFormats::strFill(12, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 9    Filler                                  (  65-  89)      25
        msb.Append(SKS_PP_StringFormats::strFill(25, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //10    Originator's Short Name                 (  90- 104)      15
        str shortName;

        shortName = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('ShortName', mSKS_EFT_FileDetail.RecId, SKS_EFT_CodeLookupType::CodeLookupBank));
        if(shortName != '')
        {
            msb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(shortName),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
        }
        else
        {
            msb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_Bank.WOrdPartyName),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
        }
    
        //11    Payee Name                              ( 105- 134)      30
        msb.Append(SKS_PP_StringFormats::strFill(30, this.CharacterReplacement(mSKS_EFT_VendorPaym.WBeneName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //12    Originator's Long Name                  ( 135- 164)      30
        msb.Append(SKS_PP_StringFormats::strFill(30, this.CharacterReplacement(mSKS_EFT_Bank.WOrdPartyName),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
    
        //13    Filler                                  ( 165- 174)      10
        msb.Append(SKS_PP_StringFormats::strFill(10, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //14    Cross Reference No.                     ( 175- 193)      19
        msb.Append(SKS_PP_StringFormats::strFill(19, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //15    Institutional return ID                 ( 194- 202)       9
        msb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //16    Account Number for return               ( 203- 214)      12
        msb.Append(SKS_PP_StringFormats::strFill(12, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //17    Filler                                  ( 215- 253)      39
        msb.Append(SKS_PP_StringFormats::strFill(39, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //18    Filler                                  ( 254- 264)      11
        msb.Append(SKS_PP_StringFormats::strFill(11, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Detail_D</Name>
				<Source><![CDATA[
    boolean rec_Detail_D(boolean _newRec)
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Content                                    Position    Size
        // 1    Record Type                             (        1)       1
        // 2    Logical Record Count                    (   2-  10)       9
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        //--
        // 4    Transaction Type Code                   (  25-  27)       3
        // 5    Amount                                  (  28-  37)      10
        // 6    Withdrawal Date                         (  38-  43)       6
        // 7    Drawee Institutional ID                 (  44-  52)       9
        // 8    Drawee Account Number                   (  53-  64)      12
        // 9    Filler                                  (  65-  89)      25
        //10    Originator's Short Name                 (  90- 104)      15
        //11    Drawee Name                             ( 105- 134)      30
        //12    Originator's Long Name                  ( 135- 164)      30
        //13    Filler                                  ( 165- 174)      10
        //14    Cross Reference No.                     ( 175- 189)      15
        //15    Filler                                  ( 190- 193)       4
        //16    Institutional return ID                 ( 194- 202)       9
        //17    Account Number for return               ( 203- 214)      12
        //18    MICR Voucher                            ( 215- 229)      15
        //19    Filler                                  ( 230- 238)       9
        //20    Filler                                  ( 239- 247)       9
        //21    Filler                                  ( 248- 250)       3
        //22    Filler                                  ( 251- 253)       3
        //23    Filler                                  ( 254- 264)      11
        //--    Repeat 4-23 for each additional deposit segment
        //------------------------------------------------------------------------------------------------------

        Amount amount;

        if(_newRec)
        {
            // 1    Record Type                             (        1)       1
            // 2    Logical Record Count                    (   2-  10)       9
            // 3    Originator ID + File Creation Number    (  11-  24)      14
            if(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur < 0)//SKS_VSTS_13707
            {
                this.rec_FileLead('C');
            }
            else
            {
                this.rec_FileLead('D');
            }
        }

        if(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur < 0)//SKS_VSTS_13707
        {
            amount = mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1;
            mCRecordCount++;
            mPaymentAmount += mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;
        }
        else
        {
            amount = mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur;
            mDRecordCount++;
            mWithdrawalAmount += amount;
        }
    
        // 4    Transaction Type Code                   (  25-  27)       3
        msb.Append(mSKS_EFT_FileFormatPaymSpec.TransactionCode);
    
        // 5    Amount                                  (  28-  37)      10
        msb.Append(SKS_PP_StringFormats::strMoney(amount, 10));//SKS_VSTS_13707
    
        // 6    Withdrawal Date                         (  38-  43)       6
        msb.Append(this.getJulianDate(mSKS_EFT_File.EffectiveDate));
    
        // 7    Drawee Institutional ID                 (  44-  52)       9
        msb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.DepBankTransit, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 8    Drawee Account Number                   (  53-  64)      12
        msb.Append(SKS_PP_StringFormats::strFill(12, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 9    Filler                                  (  65-  89)      25
        msb.Append(SKS_PP_StringFormats::strFill(25, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //10    Originator's Short Name                 (  90- 104)      15
        str shortName;

        shortName = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('ShortName', mSKS_EFT_FileDetail.RecId));
        if(shortName != '')
        {
            msb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(shortName),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
        }
        else
        {
            msb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFTAR_Bank.ACHCompanyName),SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
        }
    
        //11    Drawee Name                             ( 105- 134)      30
        msb.Append(SKS_PP_StringFormats::strFill(30, this.CharacterReplacement(mSKS_EFT_FileDetail.AccountName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //12    Originator's Long Name                  ( 135- 164)      30
        msb.Append(SKS_PP_StringFormats::strFill(30, this.CharacterReplacement(mSKS_EFTAR_Bank.ACHCompanyName), SKS_PP_FillCharacter::Space,SKS_PP_FillSide::Right));
    
        //13    Filler                                  ( 165- 174)      10
        msb.Append(SKS_PP_StringFormats::strFill(10, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //14    Cross Reference No.                     ( 175- 189)      15
        msb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //15    Filler                                  ( 190- 193)       4
        msb.Append(SKS_PP_StringFormats::strFill(4, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //16    Institutional return ID                 ( 194- 202)       9
        msb.Append(SKS_PP_StringFormats::strFill(9, mSKS_EFT_FileDetail.BankBankTransit, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //17    Account Number for return               ( 203- 214)      12
        msb.Append(SKS_PP_StringFormats::strFill(12, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //18    MICR Voucher                            ( 215- 229)      15
        msb.Append(SKS_PP_StringFormats::strFill(15, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));//SKS_VSTS_13707
    
        //19    Filler                                  ( 230- 238)       9
        msb.Append(SKS_PP_StringFormats::strFill(9, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));//SKS_VSTS_13707
    
        //20    Filler                                  ( 239- 247)       9
        msb.Append(SKS_PP_StringFormats::strFill(9, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));//SKS_VSTS_13707
    
        //21    Filler                                  ( 248- 250)       3
        msb.Append(SKS_PP_StringFormats::strFill(3, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //22    Filler                                  ( 251- 253)       3
        msb.Append(SKS_PP_StringFormats::strFill(3, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //23    Filler                                  ( 254- 264)      11
        msb.Append(SKS_PP_StringFormats::strFill(11, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_File_Header</Name>
				<Source><![CDATA[
    boolean rec_File_Header()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Content                                                 Position      Size
        // 1    Record Type                                             (        1)      1
        // 2    Logical Record Count                                    (   2-  10)      9
        // 3    Originator ID Number                                    (  11-  20)     10
        // 4    File Creation Number                                    (  21-  24)      4
        // 5    File Creation Date                                      (  25-  30)      6
        // 6    Destination Date Centre Code                            (  31-  35)      5
        // 7    Filler                                                  (  36-1464)   1429
        //------------------------------------------------------------------------------------------------------
        str OrigID;
        str FileCN;
        //Nbr   Content
        // 1    Record Type                                             (        1)      1
        msb.Append('A');
    
        // 2    Logical Record Count                                    (   2-  10)      9
        mLogicRecCount = 1;
        msb.Append(SKS_PP_StringFormats::strFill(9, int2str(mLogicRecCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 3    Originator ID Number                                    (  11-  20)     10
        if(mSKS_EFT_FileDetail.Module == SKS_EFT_Module::AP)
        {
            OrigID = SKS_PP_StringFormats::strFill(10,strUpr(mSKS_EFT_Bank.WOrdPartyId), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right);
        }
        else
        {
            OrigID = SKS_PP_StringFormats::strFill(10,strUpr(this.CharacterReplacement(mSKS_EFTAR_Bank.ACHCompanyID)), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right);
        }
    
        msb.Append(OrigID);
    
        // 4    File Creation Number                                    (  21-  24)      4
        FileCN = SKS_PP_StringFormats::strFill(4,SKS_EFT_Format::getCodelookupvalue('NextSequenceNumber', mSKS_EFT_FileDetail.RecId), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left);
        msb.Append(FileCN);
    
        // 5    File Creation Date                                      (  25-  30)      6
        msb.Append(this.getJulianDate(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone())));
    
        // 6    Destination Date Centre Code                            (  31-  35)      5
        str dataCentCode;

        dataCentCode = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('DataCentCode', mSKS_EFT_FileDetail.RecId, SKS_EFT_CodeLookupType::CodeLookupBank));
        if(mSKS_EFT_FileDetail.Module == SKS_EFT_Module::AP)
        {
            msb.Append(SKS_PP_StringFormats::strFill(5, this.CharacterReplacement(dataCentCode), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        }
        else
        {
            msb.Append(SKS_PP_StringFormats::strFill(5, this.CharacterReplacement(dataCentCode), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        }
    
    
        // 7    Filler                                                  (  36-  55)     20
        msb.Append(SKS_PP_StringFormats::strFill(20, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        // 8    Currency Code Indicator                                (  56-   58)      3
        msb.Append(SKS_PP_StringFormats::strFill(3, BankAccountTable::findByCompany(mSKS_EFT_FileDetail.AccountCompany, mSKS_EFT_FileDetail.BankAccountId).CurrencyCode, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        // 9    Filler                                                  ( 59- 1464)     1406
        msb.Append(SKS_PP_StringFormats::strFill(1406, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        mControlID = OrigID + FileCN;
    
        msb.Append(#LineEnd);
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_FileLead</Name>
				<Source><![CDATA[
    boolean rec_FileLead(str _Type)
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Content                                    Position    Size
        // 1    Record Type                             (        1)       1
        // 2    Logical Record Count                    (   2-  10)       9
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        //------------------------------------------------------------------------------------------------------
    
        // 1    Record Type                             (        1)       1
        msb.Append(_Type);
    
        // 2    Logical Record Count                    (   2-  10)       9
        mLogicRecCount++;
        msb.Append(SKS_PP_StringFormats::strFill(9, int2str(mLogicRecCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        msb.Append(SKS_PP_StringFormats::strFill(14, mControlID, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Trailer</Name>
				<Source><![CDATA[
    boolean rec_Trailer()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Content                                    Position    Size
        // 1    Record Type                             (        1)       1
        // 2    Logical Record Count                    (   2-  10)       9
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        // 4    Total Value  of D Records               (  25-  38)      14
        // 5    Total Number of D Records               (  39-  46)       8
        // 6    Total Value of C Records                (  47-  60)      14
        // 7    Total Number of C Records               (  61-  68)       8
        // 8    Filler                                  (  69-1464)    1396
        //------------------------------------------------------------------------------------------------------
    
        // 1    Record Type                             (        1)       1
        // 2    Logical Record Count                    (   2-  10)       9
        // 3    Originator ID + File Creation Number    (  11-  24)      14
        this.rec_FileLead('Z');
    
        // 4    Total Value  of D Records               (  25-  38)      14
        msb.Append(SKS_PP_StringFormats::strMoney(mWithdrawalAmount, 14));
    
        // 5    Total Number of D Records               (  39-  46)       8
        msb.Append(SKS_PP_StringFormats::strFill(8, int2str(mDRecordCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 6    Total Value of C Records                (  47-  60)      14
        msb.Append(SKS_PP_StringFormats::strMoney(mPaymentAmount * -1, 14));
    
        // 7    Total Number of C Records               (  61-  68)       8
        msb.Append(SKS_PP_StringFormats::strFill(8, int2str(mCRecordCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        // 8    Filler                                  (  69-1464)    1396
        msb.Append(SKS_PP_StringFormats::strFill(1396, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    [SuppressBPWarning('BPErrorSelectUsingFirstOnly', 'The buffer is used in next statement or in a datasource query loop.')]
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
          order by mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur,
            mSKS_EFT_FileDetail.RecordSection,
                    mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>