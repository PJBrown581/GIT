<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_CCR_MatchAll</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_CCR_MatchAll
{        
    

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>main</Name>
				<Source><![CDATA[
    /// <summary>
    /// Runs the class with the specified arguments.
    /// </summary>
    /// <param name = "_args">The specified arguments.</param>
    public static void main(Args _args)
    {
        SKS_CCR_Matching     locSKS_CCR_Matching = new SKS_CCR_Matching();
        SKS_PP_500PPBas     locSKS_PP_500PPBas;
        BankAccountStatement locBankAccountStatement;
        BankAccountStatement FilteredBankAccountStatement;
        SKS_CCR_BankTran     locSKS_CCR_BankTran;
        FromDate            locFromDate;
        SKS_BR_ClickNMatchSessions locSKS_BR_ClickNMatchSessions;
        container           locCrossCompanyList;
        SKS_CCR_BankAccountStatementFilter   StatementFilter;
        FormRun             processCCForm;

        StatementFilter = _args.parmobject();

        locCrossCompanyList = StatementFilter.GetSetCompany() == '' ? SKS_EB_VirtualCompany::getCCRRecCompanies() : str2con(StatementFilter.GetSetCompany());


        if(StatementFilter.GetSetBankAccount() == '')
        {
            select crosscompany:locCrossCompanyList * from locBankAccountStatement
                order by locBankAccountStatement.AccountStatementDate asc, locBankAccountStatement.AccountStatementNum, locBankAccountStatement.DataAreaId
                where locBankAccountStatement.ReconcileDate == dateNull()
                && locBankAccountStatement.SKS_CCR_BankHeaderRecId != 0;
        }
        else
        {
            select crosscompany:locCrossCompanyList * from locBankAccountStatement
                order by locBankAccountStatement.AccountStatementDate asc, locBankAccountStatement.AccountStatementNum, locBankAccountStatement.DataAreaId
                where locBankAccountStatement.ReconcileDate == dateNull()
                && locBankAccountStatement.AccountId == StatementFilter.GetSetBankAccount()
                && locBankAccountStatement.SKS_CCR_BankHeaderRecId != 0;
        }

        while(locBankAccountStatement)
        {
            //Different entities will have different Registrations states.
            //In order to maint consistant behavior, matching should be 
            //done in the context of the statement's company.
            changecompany(locBankAccountStatement.company())
            {
                locSKS_BR_ClickNMatchSessions = SKS_CCR_MatchAll::getClickNMatchSession(locBankAccountStatement);
                if(locSKS_BR_ClickNMatchSessions)
                {
                    warning(strFmt("@SKS:SKGCurrentlyInClickNMatch"
                            ,locBankAccountStatement.company()
                            ,locBankAccountStatement.AccountId
                            ,SKS_PP_StringFormats::strDateMMDDYYYY(locBankAccountStatement.AccountStatementDate,DateSeparator::Slash)
                            ,locBankAccountStatement.AccountStatementNum
                            ,new xSession(locSKS_BR_ClickNMatchSessions.SessionId).userId()));
                    continue;
                }
                locSKS_PP_500PPBas = new SKS_PP_500PPBas();
                if(locSKS_PP_500PPBas.IsRegistered('AB') != 1)
                {
                    continue;
                }
                //this buffer changes companies.  It is imperitive it be emptied prior to being refilled
                // to avoid problems that occur when a buffer is reused in multiple companies.
                locSKS_CCR_BankTran = null;
                // Only one SKS_BR_BankTran is needed for the file info.  this logic is based on the SKS_BR_StatementReconciliation Form
                select firstonly FileDateTime, Filename, OriginalStatementRecId, RecId from locSKS_CCR_BankTran
                    where locSKS_CCR_BankTran.OriginalStatementRecId == locBankAccountStatement.RecId;
                if(locSKS_CCR_BankTran)
                {
                    locSKS_CCR_Matching.matchTransactions(locBankAccountStatement, dateNull(), locSKS_CCR_BankTran.FileDateTime,
                                                            locSKS_CCR_BankTran.Filename, 0, locSKS_CCR_BankTran.OriginalStatementRecId);
                }
            }
            next locBankAccountStatement;
        }
        processCCForm = _args.caller();
        processCCForm.dataSource(2).research();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getClickNMatchSession</Name>
				<Source><![CDATA[
    //this method will return null, unless someone has a currentl click-n-match sessions, in which case you get that session
    public static SKS_BR_ClickNMatchSessions getClickNMatchSession(BankAccountStatement _BankAccountStatement)
    {
        SKS_BR_ClickNMatchSessions locSKS_BR_ClickNMatchSessions;
        boolean autoUnlockClickNMatch()
        {
            boolean rtnval = false;
                
            if(SKS_LB_JournalInUse::canBeAutoUnlocked(locSKS_BR_ClickNMatchSessions))
            {
                ttsBegin;
                SKS_CCR_Matching::removeGuidMatch(locSKS_BR_ClickNMatchSessions.ManualReconciliationID);
                locSKS_BR_ClickNMatchSessions = SKS_LB_JournalInUse::updateBlockServer(locSKS_BR_ClickNMatchSessions,
                                                                JournalBlockLevel::InUse,
                                                                JournalBlockLevel::None,
                                                                strFmt("@SKS3735",
                                                                        _BankAccountStatement.AccountId,
                                                                        new xSession(locSKS_BR_ClickNMatchSessions.SessionId).userId())
                                                                );
                ttsCommit;
                rtnval = true;
            }
            return rtnval;
        }
        select forUpdate locSKS_BR_ClickNMatchSessions where locSKS_BR_ClickNMatchSessions.AccountID == _BankAccountStatement.AccountId;
        if(!locSKS_BR_ClickNMatchSessions)
        {
            return null;
        }
        if (!locSKS_BR_ClickNMatchSessions.SessionId || autoUnlockClickNMatch())
        {
            ttsbegin;
            locSKS_BR_ClickNMatchSessions.delete();
            ttscommit;
            locSKS_BR_ClickNMatchSessions = null;    
        }
        return locSKS_BR_ClickNMatchSessions;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>