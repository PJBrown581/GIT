<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_FIRSTREPUBLIC</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_FIRSTREPUBLIC extends SKS_PP_Format
{
    #define.Separator(',')

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        //  Field number                  Field Desc
        //      1                       Bank Routing Number
        //      2                       Account #
        //      3                       Check #
        //      4                       Amt NUMERIC ONLY
        //      5                       Issue Date
        //      6                       Issue/Void (I/V)
        //      7                      Payee
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        int locMult = 1;
        str locTransCode = 'I';

        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            locTransCode = 'V';
            if(mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                locMult = -1;
            }
        }
        
        //      Bank Routing Number
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.BnkBankTransit,1,9)));
        

        //      Account Number
        sb.Append(this.wrapField(subStr(mSKS_PP_FileDetail.BnkBankAcct,1,19)));
        
        //      Check Number
        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(12, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left)));
        
        //     Amount
        sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(13, num2Str((locMult * mSKS_PP_FileDetail.ChkCuryAmt), 1, 2, 1, 0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left)));
        
        //      Issue Date MMDDYY
        sb.Append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_PP_FileDetail.ChkDocDate, DateSeparator::Slash)));

        //      Issue/Void (I/V)
        sb.Append(this.wrapField(locTransCode));

        //      Payee(stripped of commas)
        sb.Append(SKS_PP_StringFormats::strSub(strReplace(mSKS_PP_FileDetail.VendName,',',''),1,100));

        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '', boolean _addSeparator = true, int _fieldLength = 0)
    {
        str text;
        ;
        if(_fieldLength > 0)
        {
            text = subStr(_field, 1, _fieldLength);
        }
        else
        {
            text = _field;
        }
        if(_addSeparator)
        {
            text += #Separator;
        }
        return text;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>