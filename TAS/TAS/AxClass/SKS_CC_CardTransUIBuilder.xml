<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_CC_CardTransUIBuilder</Name>
	<SourceCode>
		<Declaration><![CDATA[
public class SKS_CC_CardTransUIBuilder extends SysOperationAutomaticUIBuilder
{
    DialogField digFldCust;
    DialogField digFldCCNo;
    DialogField digFldCCType;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>lookupCCNumber</Name>
				<Source><![CDATA[
    public  void lookupCCNumber(FormStringControl _formStringControl)
    {
        SysTableLookup          sysTableLookup;
        QueryBuildDataSource    qbd;
        Query                   query  = new Query();

        sysTableLookup = SysTableLookup::newParameters(tableNum(CreditCardCust), _formStringControl);
        sysTableLookup.addLookupfield(fieldNum(CreditCardCust, CardNumber));
        qbd  = query.addDataSource(tableNum(CreditCardCust));
        qbd.addRange(fieldNum(CreditCardCust,CustAccount)).value(SysQuery::value(digFldCust.value()));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>lookupCCType</Name>
				<Source><![CDATA[
    public void lookupCCType( FormStringControl _formStringControl)
    {
        SysTableLookup sysTableLookup;
        QueryBuildDataSource    qbd;
        Query                   query  = new Query();

        sysTableLookup = SysTableLookup::newParameters(tableNum(CreditCardCust), _formStringControl);
        sysTableLookup.addLookupfield(fieldNum(CreditCardCust, CreditCardTypeName));
        qbd  = query.addDataSource(tableNum(CreditCardCust));
        qbd.addRange(fieldNum(CreditCardCust,CustAccount)).value(SysQuery::value(digFldCust.value()));
        qbd.addGroupByField(fieldNum(CreditCardCust, CreditCardTypeName));
        sysTableLookup.parmQuery(query);
        sysTableLookup.performFormLookup();
    }

]]></Source>
			</Method>
			<Method>
				<Name>modifyCustAcc</Name>
				<Source><![CDATA[
    public boolean modifyCustAcc( FormStringControl _formStringControl)
    {

        if(digFldCust.value())
        {
            digFldCCNo.enabled(true);
        }
        else
        {
            digFldCCNo.enabled(false);
        }
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>postBuild</Name>
				<Source><![CDATA[
    public void postBuild()
    {
        SKS_CC_CardTransContract contract = this.dataContractObject();

        super();
        digFldCust = this.bindInfo().getDialogField(contract, methodstr(SKS_CC_CardTransContract, parmCustAccount));
        digFldCCNo = this.bindInfo().getDialogField(contract, methodstr(SKS_CC_CardTransContract, parmCCNumber));

        if(!digFldCust.value())
        {
            digFldCCNo.enabled(false);
        }

        digFldCCType = this.bindInfo().getDialogField(contract, methodStr(SKS_CC_CardTransContract, parmCreditCardType));
        digFldCCType.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(SKS_CC_CardTransUIBuilder, lookupCCType), this);
        digFldCCNo.registerOverrideMethod(methodStr(FormStringControl, lookup), methodStr(SKS_CC_CardTransUIBuilder, lookupCCNumber), this);
        digFldCust.registerOverrideMethod(methodStr(FormStringControl, modified), methodStr(SKS_CC_CardTransUIBuilder, modifyCustAcc), this);

    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>