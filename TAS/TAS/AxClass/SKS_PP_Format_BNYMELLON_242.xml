<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_BNYMELLON_242</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_BNYMELLON_242 Extends SKS_PP_Format_BankGroup
{
    int mVoidChkCount;
    real mVoidChkSum;
    int mChkCount;
    real mChkSum;
    int mRecordCount;
    int mBankAcctCount;
    
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    // Detail Record ('6' record - '1', '2', '5 are header records, '8', '9' are trailers)
    // Pos 1-1       1       Record Type '6'
    // Pos 2-2       1       Check Record Status, '2'=Add, '6'=Void
    // Pos 3-12      10      Check Number
    // Pos 13-22     10      Check Amount 
    // Pos 23-28     6       Issue Date - YYMMDD
    // Pos 29-38     10      Additional Data (Optional)
    // Pos 39-43     5       Register Information (Optional)
    // Pos 44-122    79      spaces
    // Pos 123-182   60      Payee Line 1 - Payee name (Required)*
    // Pos 183-242   60      Payee Line 2 - Payee name or first line of address (Required)*
    protected str txtDetail()
    {
        int Mult = 1;
        str ItemType;
        DirPartyNamePrimaryAddressView DirPartyNamePrimaryAddressView;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        VendTable VendTable_1;
        ;
    
        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = '6';
            if(mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
            mVoidChkCount++;
            mVoidChkSum = mVoidChkSum + (Mult * mSKS_PP_FileDetail.ChkCuryAmt);
        }
        else
        {
            ItemType = '2';
            mChkCount++;
            mChkSum = mChkSum + mSKS_PP_FileDetail.ChkCuryAmt;
        }
    
        mRecordCount++;
    
        select * from DirPartyNamePrimaryAddressView where DirPartyNamePrimaryAddressView.Party == VendTable_1.Party;
    
        sb.Append('6');
        sb.Append(ItemType);
        sb.Append(SKS_PP_StringFormats::strFill(10, strRtrim(strLtrim(mSKS_PP_FileDetail.ChkRefNbr)), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 10));
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append(SKS_PP_StringFormats::strFill(10, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(5, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(79, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(60, strLRTrim(strupr(this.getVenderNameFromSKS_PP_FileDetail(mSKS_PP_FileDetail))), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(60, strLRTrim(strupr(DirPartyNamePrimaryAddressView.Street)), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatHeader</Name>
				<Source><![CDATA[
    // Header Record
    // Pos 1-1       1       Record Type '1'
    // Pos 2-3       2       Filler - space
    // Pos 4-13      10      'MELLONBANK'
    // Pos 14-23     10      Company Name
    // Pos 24-31     8       Transmission Date YYMMDD
    // Pos 32-35     4       HHMM
    // Pos 36-242    207     Filler - space
    protected str txtFormatHeader()
    {
        Container company;
        SKS_PP_Bank SKS_PP_Bank_1;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        company = [mSKS_PP_FileDetail.ChkCpnyId];
        select crossCompany:company PP01, PP02 from SKS_PP_Bank_1 where SKS_PP_Bank_1.BankAccountId == mSKS_PP_FileDetail.ChkBankAcct;
    
        sb.Append('1');
        sb.Append(SKS_PP_StringFormats::strFill(2, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append('MELLONBANK');
        sb.Append(SKS_PP_StringFormats::strFill(10, SKS_PP_Bank_1.PP01, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone())));
        sb.Append(SKS_PP_StringFormats::strTimeHHMM());
        sb.Append(SKS_PP_StringFormats::strFill(209, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatHeaderLine2</Name>
				<Source><![CDATA[
    // Service Record
    // Pos 1-1       1       Record Type '2'
    // Pos 2-31      30      Filler - space
    // Pos 32-34     3       Service Type '100'
    // Pos 35-37     3       Record Size '242'
    // Pos 38-41     4       Blocking Factor '0242'
    // Pos 42        1       Format Code '1'
    // Pos 43-242    200     Filler - space
    protected str txtFormatHeaderLine2()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('2');
        sb.Append(SKS_PP_StringFormats::strFill(30, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append('100');
        sb.Append('242');
        sb.Append('0242');
        sb.Append('1');
        sb.Append(SKS_PP_StringFormats::strFill(200, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatTrailer</Name>
				<Source><![CDATA[
    // Pos 1-1       1       Record Type '9'
    // Pos 02-7      6       Total Number of Records Transmitted - includes (1,2,5,6,8,9)
    // Pos 8-242     235     Filler - space    
    protected str txtFormatTrailer()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        int totalRecs;
        ;
    
        totalRecs = 3 + (2 * mBankAcctCount) + mRecordCount;
    
        sb.Append('9');
        sb.Append(SKS_PP_StringFormats::strFill(6, num2Str(totalRecs,1,0,1,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(235, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupHeader</Name>
				<Source><![CDATA[
    // Detail Header Record
    // Pos 1-1       1       Record Type '5'
    // Pos 2-11      10      Origin - Company Name
    // Pos 12-21     10      Destination
    // Pos 22-31     10      Checking Account Number
    // Pos 32-242    211     Filler - space
    protected str txtGroupHeader()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        Container company;
        SKS_PP_Bank SKS_PP_Bank_1;
        ;
    
        company = [mSKS_PP_FileDetail.ChkCpnyId];
        select crossCompany:company PP01, PP02 from SKS_PP_Bank_1 where SKS_PP_Bank_1.BankAccountId == mSKS_PP_FileDetail.ChkBankAcct;
    
        sb.Append('5');
        sb.Append(SKS_PP_StringFormats::strFill(10, strUpr(SKS_PP_Bank_1.PP01), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10, strUpr(SKS_PP_Bank_1.PP02), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(211, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        mBankAcctCount++;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    // Service Total Record
    // Pos 1-1       1       Record Type '8'
    // Pos 02-11     10      Total Number of Add Issues
    // Pos 12-23     12      Total Dollar Amount of Add Issues
    // Pos 24-33     10      Total Number of Voids
    // Pos 34-45     12      Total Dollar of Voids
    // Pos 46-242    197     Filler
    protected str txtGroupTrailer()
    {    
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('8');
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(mChkCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(mChkSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(mVoidChkCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(mVoidChkSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(197, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        mChkCount = 0;
        mChkSum = 0;
        mVoidChkCount = 0;
        mVoidChkSum = 0;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>getVenderNameFromSKS_PP_FileDetail</Name>
				<Source><![CDATA[
    /// <summary>
    /// Return name from the Filedetail table
    /// </summary>
    /// <param name = "_sks_PP_FileDetail">_sks_PP_FileDetail</param>
    /// <returns>Filedetail table vendor name</returns>
    public VendName getVenderNameFromSKS_PP_FileDetail(SKS_PP_FileDetail _sks_PP_FileDetail)
    {
        return _sks_PP_FileDetail.VendName;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>