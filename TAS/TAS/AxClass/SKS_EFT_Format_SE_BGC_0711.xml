<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_SE_BGC_0711</Name>
	<SourceCode>
		<Declaration><![CDATA[
// Bankgirot - Leverantörsbetalningar in Swedish Kronor and Euros (Supplier Payments)
// July 2011
//
// Supports transaction types: Domestic payments
//
// Structure:
/// <summary>
/// SKS_EFT_Format_SE_BGC_0711 extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_SE_BGC_0711 extends SKS_EFT_Format
{
    #define.FormatVersion('20220531')
    int mPaymentCount;
    AmountMST mPaymentAmount;
    str mSenderBankGiro;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {
        SKS_EFT_FileDetail invSKS_EFT_FileDetail;
        BankAccountId prevBankAccount;
        CurrencyCode prevCurrencyCode;
        ;
    
        while(mSKS_EFT_FileDetail)
        {
            if(prevBankAccount != mSKS_EFT_FileDetail.BankAccountId || prevCurrencyCode != mSKS_EFT_FileDetail.PaymentCurrencyCode)
            {
                // Header
                this.rec_TK11();
    
                // Reset counters
                mPaymentCount = 0;
                mPaymentAmount = 0;
            }
    
            // Detail lines
            this.rec_TK14();
    
            if(mSKS_EFT_FileDetail.RecordSummary == SKS_EFT_RecordSummary::Invoice)
            {
                this.rec_TK25(mSKS_EFT_FileDetail);
            }
            else
            {
                while select invSKS_EFT_FileDetail where invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
                {
                    this.rec_TK25(invSKS_EFT_FileDetail);
                }
            }
    
            prevBankAccount = mSKS_EFT_FileDetail.BankAccountId;
            prevCurrencyCode = mSKS_EFT_FileDetail.PaymentCurrencyCode;
    
            next mSKS_EFT_FileDetail;
    
            if(prevBankAccount != mSKS_EFT_FileDetail.BankAccountId || prevCurrencyCode != mSKS_EFT_FileDetail.PaymentCurrencyCode)
            {
                // Format trailer
                this.rec_TK29();
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_TK11</Name>
				<Source><![CDATA[
    //Nbr   Description                                         Max     Start   Type    Comment
    //1     Transaction code                                    2       1       N
    //2     Sender's bankgiro number                            10      3       N       Zero fill
    //3     Sender's date written                               6       13      N       YYMMDD
    //4     Product                                             22      19      A       Upper case
    //5     Payment date                                        6       41      N       YYMMDD (left blank so detail lines are used)
    //6     Reserved field                                      13      47      A       Blank
    //7     Currency code                                       3       60      A       SEK or EUR or blank
    //8     Reserved field                                      18      63      A       Blank
    boolean rec_TK11()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        //1     Transaction code
        sb.Append('11');
    
        //2     Sender's bankgiro number
        mSenderBankGiro = SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left);
        sb.Append(mSenderBankGiro);
    
        //3     Sender's date written
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(DateTimeUtil::getToday(DateTimeUtil::getUserPreferredTimeZone())));
    
        //4     Product
        sb.Append('LEVERANTÖRSBETALNINGAR');
    
        // 5    Payment Date
        sb.Append(SKS_PP_StringFormats::strFill(6, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //6     Reserved field
        sb.Append(SKS_PP_StringFormats::strFill(13, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //7     Currency code
        sb.Append(SKS_PP_StringFormats::strFill(3, mSKS_EFT_FileDetail.PaymentCurrencyCode, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //8     Reserved field
        sb.Append(SKS_PP_StringFormats::strFill(18, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        this.fileWrite(sb.ToString());
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_TK14</Name>
				<Source><![CDATA[
    //Nbr   Description                                         Max     Start   Type    Comment
    //1     Transaction code                                    2       1       N
    //2     Payee's bankgiro or credit transfer number          10      3       N       Zero fill
    //3     OCR reference or invoice number                     25      13      A
    //4     Amount                                              12      38      N       Zero fill
    //5     Payment date                                        6       50      N       YYMMDD
    //6     Reserved field                                      5       56      A       Blank
    //7     Information to sender                               20      61      A
    boolean rec_TK14()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        AmountMST amt;
        ;
    
        //1     Transaction code
        sb.Append('14');
    
        //2     Payee's bankgiro or credit transfer number
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //3     OCR reference or invoice number
        sb.Append(SKS_PP_StringFormats::strFill(17, mSKS_EFT_FileDetail.PaymentVoucher, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(8, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //4     Amount
        amt = this.getPositiveAmount(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur);
        sb.Append(SKS_PP_StringFormats::strMoney(this.getPositiveAmount(amt), 12));
    
        // 5    Payment Date
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(mSKS_EFT_File.EffectiveDate));
    
        //6     Reserved field
        sb.Append(SKS_PP_StringFormats::strFill(5, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //7     Information to sender
        sb.Append(SKS_PP_StringFormats::strFill(20, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        this.fileWrite(sb.ToString());
    
        mPaymentCount++;
        mPaymentAmount += amt;
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_TK25</Name>
				<Source><![CDATA[
    //Nbr   Description                                         Max     Start   Type    Comment
    //1     Transaction code                                    2       1       N
    //2     Payee's bankgiro or credit transfer number          10      3       N       Zero fill
    //3     Information text                                    50      13      A
    //4     Reserved field                                      18      50      A       Blank
    boolean rec_TK25(SKS_EFT_FileDetail _SKS_EFT_FileDetail)
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        //1     Transaction code
        sb.Append('25');
    
        //2     Payee's bankgiro or credit transfer number
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //3     Information text
        sb.Append(SKS_PP_StringFormats::strFill(50, _SKS_EFT_FileDetail.InvoiceNumber, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //4     Reserved field
        sb.Append(SKS_PP_StringFormats::strFill(18, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        this.fileWrite(sb.ToString());
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_TK29</Name>
				<Source><![CDATA[
    //Nbr   Description                                         Max     Start   Type    Comment
    //1     Transaction code                                    2       1       N
    //2     Sender's bankgiro number                            10      3       N       Zero fill
    //3     Number of payment records                           8       13      N       Zero fill
    //4     Total amount                                        12      21      N       Zero fill
    //5     Negative total amount                               1       33      A       Minus sign or blank
    //6     Reserved field                                      47      34      A       Blank
    boolean rec_TK29()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        //1     Transaction code
        sb.Append('29');
    
        //2     Sender's bankgiro number
        sb.Append(mSenderBankGiro);
    
        //3     Number of payment records
        sb.Append(SKS_PP_StringFormats::strFill(8, num2str(mPaymentCount,0,0,0,0), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        //4     Total amount
        sb.Append(SKS_PP_StringFormats::strMoney(mPaymentAmount, 12));
    
        //5     Negative total amount
        sb.Append(SKS_PP_StringFormats::strFill(1, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //6     Reserved field
        sb.Append(SKS_PP_StringFormats::strFill(47, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        this.fileWrite(sb.ToString());
    
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    protected void selectPaymentRecords()
    {
        select * from mSKS_EFT_FileDetail
          order by mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary;
    
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>