<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_US_SVB_DT</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_US_SVB_DT extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_US_SVB_DT extends SKS_EFT_Format
{
    #define.FormatVersion('20220730')
    boolean firstRec;
    #define.DOM('CHK-DOM')
    #define.FedX('CHK-FedX')
    #define.UPS('CHK-UPS')

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    [SuppressBPWarningAttribute("BPCheckNestedLoopinCode","Necessary to allow the processing of every invoice(inner loop) for every relevant parent file detail record(outer loop)")]
    protected void formatStructure()
    {
        SKS_EFT_FileDetail invSKS_EFT_FileDetail;
        firstRec = true;
    
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_VendorPaym
            this.getSKS_EFT_VendorPaym();

            this.getSKS_EFT_FileFormatPaymSpec();

            //Get Remittance Information
            this.getRemittanceAddress(mSKS_EFT_FileFormatPaymSpec.UseCheckNumber);

            this.rec_Data();

            while select invSKS_EFT_FileDetail where invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
            {
                this.rec_InvDetails(invSKS_EFT_FileDetail);
            }
    
            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Data</Name>
				<Source><![CDATA[
    //Field Name                    Length
    //Client account number         10
    //Client company name           40
    //Client Company Address 1      40
    //Client Company Address 2      40
    //Client Company Address City   15
    //Client Address State          15
    //Client Address Zip            10
    //Client Address Country        15
    //Payment Code                   1
    //Check Date                    10
    //Check Number                  10
    //Check Amount                  14
    //Payee/Vendor Name             60
    //Payee/Vendor Number           20
    //Payee address 1               40
    //Payee address 2               40
    //Payee address 3               40
    //Payee address City            15
    //Payee address State           15
    //Payee address ZIP             10
    //Payee address Country         20
    //Mail Code                     1
    //Handling Code                 1

    void rec_Data()
    {
        str mailCode;
        str pmtIndicator;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        
        pmtIndicator = SKS_EFT_Format::getCodelookupvalue('PmtIndicator', mSKS_EFT_FileDetail.RecId, SKS_EFT_CodeLookupType::CodeLookupBank);
        if(pmtIndicator)//SKS_DevOps_15001
        {
            sb.Append(SKS_PP_StringFormats::strFill(1, this.CharacterReplacement(pmtIndicator), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        }
        //Client account number         10
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail.BankBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //Client company name           40
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Company Address 1      40
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyAddr), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Company Address 2      40
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyAddr2), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Company Address City   15
        sb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyCity), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Address State          15
        sb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyState), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Address Zip            10
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_EFT_FileDetail_BankAccount.WOrdPartyZipCode, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Client Address Country        15
        sb.Append(SKS_PP_StringFormats::strFill(15, LogisticsAddressCountryRegion::findByISOCode(this.getLogisticsAddressCountryISO(mSKS_EFT_FileDetail_BankAccount.WOrdPartyCountryRegionId)).displayName(), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Payment Code                   1
        sb.Append('C');
    
        //Check Date                    10
        sb.Append(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_EFT_FileDetail.EffectiveDate, DateSeparator::Slash));
    
        //Check Number                  10
        sb.Append(SKS_PP_StringFormats::strFill(10, this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Check Amount                  14
        sb.Append(SKS_PP_StringFormats::strFill(14, num2str(-1 * mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 0, 2, 1, 0), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee/Vendor Name             60
        sb.Append(SKS_PP_StringFormats::strFill(60, mRemittanceName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee/Vendor Number           20
        sb.Append(SKS_PP_StringFormats::strFill(20, mSKS_EFT_FileDetail.AccountNum, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address 1               40
        sb.Append(SKS_PP_StringFormats::strFill(40, mRemittanceAdrLine1, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address 2               40
        sb.Append(SKS_PP_StringFormats::strFill(40, mRemittanceAdrLine2, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address 3               40
        sb.Append(SKS_PP_StringFormats::strFill(40, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address City            15
        sb.Append(SKS_PP_StringFormats::strFill(15, mRemittanceCity, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address State           15
        sb.Append(SKS_PP_StringFormats::strFill(15, mRemittanceState, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Payee address ZIP             10
        sb.Append(SKS_PP_StringFormats::strFill(10, mRemittanceZipCode, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        
        //Payee address Country         20
        sb.Append(SKS_PP_StringFormats::strFill(20, LogisticsAddressCountryRegion::findByISOCode(this.getLogisticsAddressCountryISO(mRemittanceCountryRegionId)).displayName(), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        
        //Mail Code
        mailCode = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('MailCode', mSKS_EFT_FileDetail.RecId));
        if(mailCode)
        {
            sb.Append(mailCode);
        }

        //Handling Code                 1
        switch(mSKS_EFT_FileFormatPaymSpec.TransactionCode)
        {
            case #DOM:
                sb.Append('1');
                break;
            case #FedX:
                sb.Append('2');
                break;
            case #UPS:
                sb.Append('4');
                break;
        }
    
        this.fileWrite(sb.ToString());
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_InvDetails</Name>
				<Source><![CDATA[
    //Field Name                    Length
    //Payment Code                   1
    //Invoice Number                30
    //Invoice Date                  10
    //Invoice Description           30
    //Invoice Discount              14
    //Invoice Gross                 14
    //Invoice Net                   14
    void rec_InvDetails(SKS_EFT_FileDetail _invSKS_EFT_FileDetail)
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        AmountCur grossAmt;
    
        //Payment Code      1
        sb.Append('I');
    
        //Invoice Number    30
        sb.Append(SKS_PP_StringFormats::strFill(this.getInvoiceNumberLength(), _invSKS_EFT_FileDetail.InvoiceNumber, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //Invoice Date      10
        sb.Append(SKS_PP_StringFormats::strFill(10, SKS_PP_StringFormats::strDateMMDDYYYY(_invSKS_EFT_FileDetail.InvoiceDate, DateSeparator::Slash), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        //Invoice Description       30
        sb.Append(SKS_PP_StringFormats::strFill(30, this.CharacterReplacement(_invSKS_EFT_FileDetail.InvoiceTransactionTxt), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        //Invoice Discount      14
        sb.Append(SKS_PP_StringFormats::strFill(14, num2str(-1 * _invSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur, 0, 2, 1, 0), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        //Invoice Gross     14
        grossAmt = _invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur + _invSKS_EFT_FileDetail.InvoiceDiscAmountPmtCur;
        sb.Append(SKS_PP_StringFormats::strFill(14, num2str(-1 * grossAmt, 0, 2, 1, 0), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        //Invoice Net       14
        sb.Append(SKS_PP_StringFormats::strFill(14, num2str(-1 * _invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 0, 2, 1, 0), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
    
        this.fileWrite(sb.ToString());
    }

]]></Source>
			</Method>
			<Method>
				<Name>getInvoiceNumberLength</Name>
				<Source><![CDATA[
    public int getInvoiceNumberLength()
    {
        return 30;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>