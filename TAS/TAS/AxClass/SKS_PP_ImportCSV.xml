<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_ImportCSV</Name>
	<SourceCode>
		<Declaration><![CDATA[
Public class SKS_PP_ImportCSV
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>ImportBankCSV</Name>
				<Source><![CDATA[
    public void ImportBankCSV()
    {
        SKS_PP_FileFormat       sks;
        SKS_PP_FileFormat       orig_sks;
        CommaStreamIo           inFile;
        Filename                filename;
        Container               line;
        int linecnt;
        int totaladds;
        int totalupdates;
        int totaldeletes;
        str action;
        
        SKS_FileUploadDialogForm fileUploadDialog = new SKS_FileUploadDialogForm();
        fileUploadDialog.parmFileDialogCaptionForForm("@SKS247");
        fileUploadDialog.parmFileTypesAccepted('.csv');
        fileUploadDialog.parmStrategyClassName(classstr(SKS_PP_CSVFileTemporaryStorageStrategy));
        FileUploadTemporaryStorageResult fileUploadResult = fileUploadDialog.getFileUploadResult();
        if (fileUploadResult != null && fileUploadResult.getUploadStatus())
        {
            filename = fileUploadResult.getDownloadUrl();
            System.IO.Stream readStream = fileUploadResult.openResult(); // This is used to upload formats so it doesn't need to use FileHub
            inFile = CommaStreamIo::constructForRead(readStream);
        }
        else
        {
            return;
        }

        if (!inFile || infile.status() != IO_Status::Ok )
        {
            //file could not be opened
            throw error (strfmt("@SYS19312", filename));
        }

        ttsbegin;
        
        infile.inRecordDelimiter('\n');
        infile.inFieldDelimiter(',');
        linecnt = 0;
        //Checking status of last operation..
        while (infile.status() == IO_status::Ok)
        {
            line = infile.read();
            linecnt++;
            if (line && linecnt > 6)
            {
                sks.initValue();
                sks.BankPP04Mask = conpeek(line, 1);
                sks.BankPP04Descr = conpeek(line, 2);
                sks.BankPP03Mask = conpeek(line, 3);
                sks.BankPP02Mask = conpeek(line, 4);
                sks.BankPP01Mask = conpeek(line, 5);
                sks.BankPP03Descr = conpeek(line, 6);
                sks.BankPP02Descr = conpeek(line, 7);
                sks.BankPP01Descr = conpeek(line, 8);
                sks.VoidChkSeparate = conpeek(line, 9);
                sks.TrimRecord = conpeek(line, 10);
                sks.TrailerRecord = conpeek(line, 11);
                sks.Selected = conpeek(line, 12);
                sks.RecordLen = conpeek(line, 13);
                sks.MultiAcctAllowed = conpeek(line, 14);
                sks.LeadingSpace = conpeek(line, 15);
                sks.LastEffDate = str2date(conpeek(line, 16), 213);
                sks.FormatId = conpeek(line, 17);
                sks.Descr = conpeek(line, 18);
                sks.Crlf = conpeek(line, 19);
                sks.BlockFill = conpeek(line, 20);
                sks.BankTransitNoCD = conpeek(line, 21);
                action = subStr(conpeek(line, 22), 1, 1);   //Add, Replace, or Delete
                
                select * from orig_sks where orig_sks.FormatId == sks.FormatID;
                
                if(action == 'A' || action == '')
                {
                    if(orig_sks.FormatID == '') //not found so add it
                    {
                        sks.doInsert();
                        totaladds++;
                    }
                }
                else if (action == 'R')
                {
                    if(orig_sks.FormatID == '') //nothing to replace so add it
                    {
                        sks.doInsert();
                        totaladds++;
                    }
                    else
                    {
                        delete_from orig_sks where orig_sks.FormatId == sks.FormatID;
                        sks.doInsert();
                        totalupdates++;
                    }
                }
                else if(action == 'D')
                {
                    if(orig_sks.FormatID != '') //if the record was found then delete it
                    {
                        delete_from orig_sks where orig_sks.FormatId == sks.FormatID;
                        totaldeletes++;
                    }
                }
            }
        }
        ttscommit;
        
        //report total adds, updates, and deletes
        info("@SYS54695" + ' ' + "@SYS108583" + ' :' + int2str(totaladds));
        info("@SYS54695" + ' ' + "@SYS439" + ' :' + int2str(totalupdates));
        info("@SYS54695" + ' ' + "@SYS13435" + ' :'  + int2str(totaldeletes));
        
        
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>