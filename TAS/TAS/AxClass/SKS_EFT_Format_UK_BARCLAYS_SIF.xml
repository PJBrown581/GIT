<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_UK_BARCLAYS_SIF</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_UK_BARCLAYS_SIF extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_UK_BARCLAYS_SIF extends SKS_EFT_Format
{
    #define.FormatVersion('20220606')
    #define.LineEnd('\r\n')
    #define.IATRF('IATRF')
    #define.SDP('SDP')
    #define.FP('FP')
    #define.CIP('CIP')
    #define.SCT('SCT')
    
    System.Text.StringBuilder sb = new System.Text.StringBuilder();

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '')
    {
        _field = _field + #LineEnd;

        return _field;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {    
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();

            // Get SKS_EFT_Bank
            this.getSKS_EFT_Bank();

            // Get SKS_EFT_VendorPaym
            this.getSKS_EFT_VendorPaym();

            // Get RemittanceAddress
            this.getRemittanceAddress(mSKS_EFT_FileFormatPaymSpec.UseCheckNumber);

            // Details
            this.rec_Data();

            next mSKS_EFT_FileDetail;
        }
        sb.Append('-');

        this.fileWrite(sb.toString());
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Data</Name>
				<Source><![CDATA[
    void rec_Data()
    {
        str     value;

        //01          Transaction reference
        sb.Append(this.wrapField(subStr(this.CharacterReplacement(mSKS_EFT_FileDetail.PaymentReference), 1, 16)));

        //02          Value date
        sb.Append(this.wrapField(SKS_PP_StringFormats::strDateYYYYMMDD(mSKS_EFT_FileDetail.EffectiveDate)));

        //03          Currency code
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.PaymentCurrencyCode));

        //04          Amount
        sb.Append(this.wrapField(num2Str(this.getPositiveAmount(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur), 0, 2, 1, 0)));

        //05          Debit party name
        sb.Append(this.wrapField(subStr(this.CharacterReplacement(mSKS_EFT_Bank.WOrdPartyName), 1, 35)));

        //06          Debit account number
        sb.Append(this.wrapField(subStr(mSKS_EFT_FileDetail.BankBankAcct, 1, 34)));

        //07          SWIFT BIC
        sb.Append(this.wrapField(subStr(mSKS_EFT_FileDetail.DepSwiftNo, 1, 11)));

        //08          Routing code
        if(mSKS_EFT_FileFormatPaymSpec.TransactionCode == #CIP)
        {
            sb.Append(this.wrapField(mSKS_EFT_FileDetail.DepBankTransit));
        }
        else if((mSKS_EFT_FileFormatPaymSpec.TransactionCode == #IATRF ||
                 mSKS_EFT_FileFormatPaymSpec.TransactionCode == #SDP   ||
                 mSKS_EFT_FileFormatPaymSpec.TransactionCode == #FP)   &&
                 mSKS_EFT_FileDetail.DepBankTransit)
        {
            sb.Append(this.wrapField('SC' + subStr(mSKS_EFT_FileDetail.DepBankTransit, 1, 6)));
        }
        else
        {
            sb.Append(this.wrapField(''));
        }

        if((mSKS_EFT_FileDetail.DepSwiftNo == ''  && mSKS_EFT_FileDetail.DepBankTransit == ''             && 
            mSKS_EFT_FileDetail.DepBankIBAN == '' && mSKS_EFT_FileFormatPaymSpec.TransactionCode == #CIP) ||
            mSKS_EFT_FileFormatPaymSpec.TransactionCode != #CIP)
        {
            //09       Beneficiary bank name
            sb.Append(this.wrapField(subStr(this.CharacterReplacement(mSKS_EFT_VendorPaym.WBenBankName), 1, 35)));

            //10       Beneficiary bank address 1
            sb.Append(this.wrapField(subStr(this.CharacterReplacement(mSKS_EFT_VendorPaym.WBenBankAddr), 1, 35)));

            //11       Beneficiary bank address 2
            sb.Append(this.wrapField(subStr(this.CharacterReplacement(mSKS_EFT_VendorPaym.WBenBankCity), 1, 35)));

            //12       Beneficiary bank address 3
            sb.Append(this.wrapField(this.getLogisticsAddressCountryISO(mSKS_EFT_VendorPaym.WBenBankCountryRegionId)));
        }
        else
        {
            sb.Append(this.wrapField(''));
            sb.Append(this.wrapField(''));
            sb.Append(this.wrapField(''));
            sb.Append(this.wrapField(''));
        }
    
        //13          Beneficiary account number
        if((mSKS_EFT_FileFormatPaymSpec.TransactionCode == #SCT || mSKS_EFT_FileFormatPaymSpec.TransactionCode == #CIP) 
            && mSKS_EFT_FileDetail.DepBankIBAN != '')
        {
            sb.Append(this.wrapField(mSKS_EFT_FileDetail.DepBankIBAN));
        }
        else
        {
            sb.Append(this.wrapField(SKS_PP_StringFormats::strFill(8, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left)));
        }

        //14          Beneficiary name
        sb.Append(this.wrapField(subStr(this.CharacterReplacement(mRemittanceName), 1, 35)));

        //15          Beneficiary address 1
        sb.Append(this.wrapField(subStr(this.CharacterReplacement(mRemittanceAdrLine1), 1, 35)));

        //16          Beneficiary address 2
        sb.Append(this.wrapField(subStr(this.CharacterReplacement(mRemittanceAdrLine2), 1, 35)));

        //17          Beneficiary address 3
        sb.Append(this.wrapField(this.CharacterReplacement(mRemittanceCountryRegionId)));

        //18          Payment details 1
        sb.Append(this.wrapField(''));

        //19          Payment details 2
        sb.Append(this.wrapField(''));

        //20          Payment details 3
        sb.Append(this.wrapField(''));

        //21          Payment details 4
        sb.Append(this.wrapField(''));

        //22          Charging instructions
        value = SKS_EFT_Format::getCodelookupvalue('Charges', mSKS_EFT_FileDetail.RecId);

        if(value)
        {
            sb.Append(this.wrapField(value));
        }
        else
        {
            sb.Append(this.wrapField(''));
        }

        //23          Sender to receiver information
        if(mSKS_EFT_FileFormatPaymSpec.TransactionCode == #SCT)
        {
            sb.Append(this.wrapField('/REC/SCT'));
        }
        else
        {
            sb.Append(this.wrapField(''));
        }

        //24          Deal number
        sb.Append(this.wrapField(''));

        //25          Deal rate
        sb.Append(this.wrapField(''));
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>