<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_HUNTINGTON_ARP</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_HUNTINGTON_ARP Extends SKS_PP_Format_BankGroup
{
    int    issCount;
    real   issSum;
    int    voidCount;
    real   voidSum;
    // HUNTINGTON-ARP POSITIVE PAY FORMAT - Detail Record
    
    // Pos 01-02     2       2 0's
    // Pos 03-14    12       Account Number
    // Pos 15-17     3       3 0's
    // Pos 18-20     3       Record Type (IS = Issued Check, CN = Cancelled Check)
    // Pos 21-30    10       Check Amount with no decimal point
    // Pos 31-40    10       Serial Number
    // Pos 41-48     8       Issue Date YYYYMMDD
    // Pos 49-50     2       2 0's
    // Pos 51-80    30       Additional Data
    // Pos 81-86     6       Bank Number
    // Pos 87-165   80       Payee
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str ItemType;
        ;
    
        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = 'CN';
            voidCount ++;
            voidSum = voidSum + mSKS_PP_FileDetail.ChkCuryAmt;
        }
        else
        {
            ItemType = 'IS';
            issCount ++;
            issSum = issSum + mSKS_PP_FileDetail.ChkCuryAmt;
        }
    
        sb.Append('00');
        sb.Append(SKS_PP_StringFormats::strFill(12, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append('000');
        sb.Append(SKS_PP_StringFormats::strFill(3, itemtype, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(SKS_PP_StringFormats::strMoney(mSKS_PP_FileDetail.ChkCuryAmt, 10));
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strDateYYYYMMDD(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append('00');
        sb.Append(SKS_PP_StringFormats::strFill(30, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append('000101');
        sb.Append(SKS_PP_StringFormats::strFill(80, mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append('');
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatTrailer</Name>
				<Source><![CDATA[
    // HUNTINGTON-ARP POSITIVE PAY FORMAT - Trailer Record
    // Pos 01-10     10       10 9's
    // Pos 11-18      8       Issue Count
    // Pos 19-30     12       Issue Amount
    // Pos 31-38      8       Cancel Count
    // Pos 39-50     12       Cancel Amount
    // Pos 51-166   116       116 0's
    
    
    protected str txtFormatTrailer()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        
        sb.Append(SKS_PP_StringFormats::strFill(10, '', SKS_PP_FillCharacter::Nine, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(8, int2str(issCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(issSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(8, int2str(voidCount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(voidSum, 12));
        sb.Append(SKS_PP_StringFormats::strFill(116, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        return sb.ToString();

    
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>