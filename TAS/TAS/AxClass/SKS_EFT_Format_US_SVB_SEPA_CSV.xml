<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_US_SVB_SEPA_CSV</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_US_SVB_SEPA_CSV extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_US_SVB_SEPA_CSV extends SKS_EFT_Format
{
    #define.FormatVersion('20220627')
    #define.Separator(',')
    #define.DoubleQuote('"')

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();

            //Payment Record
            this.rec_Payment();

            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Payment</Name>
				<Source><![CDATA[
    public boolean rec_Payment()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();

        // 1    Payment Type
        sb.Append(this.wrapField('SEPA'));

        // 2    Debit Account number
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.BankBankAcct, 8));

        // 3    Debit Account Currency
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.CurrencyCode, 3));

        // 4    Debit Account Type code
        sb.Append(this.wrapField('DNMCNC'));

        // 5    Debit Account Bank ID
        sb.Append(this.wrapField('621000'));

        // 6   Credit currency
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.PaymentCurrencyCode));

        // 7   Credit amount
        sb.Append(this.wrapField(num2str(this.getPositiveAmount(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur),0,2,1,0), 13));

        // 8   Debit currency
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.PaymentCurrencyCode));

        // 9   Date
        sb.Append(this.wrapField(SKS_PP_StringFormats::strDateMMDDYYYY(mSKS_EFT_FileDetail.EffectiveDate)));

        // 10   Payee Name
        sb.Append(this.wrapField(this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneName),35, true));

        // 11    Payee Account Number
        sb.Append(this.wrapField(mSKS_EFT_FileDetail.DepBankIBAN,42));

        // 12    IBAN Flag
        sb.Append(this.wrapField('Y'));

        // 13    Payee bank id type
        sb.Append(this.wrapField(''));

        // 14   Payee bank id
        sb.Append(this.wrapField(''));

        // 15   Charges flag
        sb.Append(this.wrapField('SHA'));

        // 16   Payee address line 1
        sb.Append(this.wrapField(this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneAddr), 35, true));

        // 17   Payee address line 2
        sb.Append(this.wrapField(this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneAddr2), 35, true));

        // 18   Payee address line 3
        sb.Append(this.wrapField(''));

        // 19   Payment details line 1
        sb.Append(this.wrapField(this.CharacterReplacement(this.getJournalPaymentNotes()),35));

        // 20   Payment details line 2
        sb.Append(this.wrapField(''));

        // 21   Payment details line 3
        sb.Append(this.wrapField(''));

        // 24   Payment details line 4
        sb.Append(this.wrapField('', 0, false, false));

        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>wrapField</Name>
				<Source><![CDATA[
    private str wrapField(str _field = '', int _fieldLength = 0, boolean _Quote = false, boolean _addSeparator = true)
    {
        str text;

        if(_fieldLength > 0)
        {
            text = subStr(_field, 1, _fieldLength);
        }
        else
        {
            text = _field;
        }
        if(_addSeparator)
        {
            if(_Quote && text != '' || _Quote == true)
            {
                return #DoubleQuote + text + #DoubleQuote + #Separator;
            }
            else
            {
                return text + #Separator;
            }
        }
        else
        {
            if(_Quote && text != '' || _Quote == true)
            {
                return #DoubleQuote + text + #DoubleQuote;
            }
            else
            {
                return text;
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>getJournalPaymentNotes</Name>
				<Source><![CDATA[
    private str getJournalPaymentNotes()
    {
        LedgerJournalTrans ledgerJournalTrans;

        select crossCompany PaymentNotes from ledgerJournalTrans where
            ledgerJournalTrans.RecId == mSKS_EFT_FileDetail.PaymentLineRecId;

        return ledgerJournalTrans.PaymentNotes;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
            join mSKS_EFT_FileDetail_VendAddress
            join mSKS_EFT_FileDetail_BenBank
            join mSKS_EFT_FileDetail_Intermediary
            join mSKS_EFT_FileDetail_CreditParty
            join msKS_EFT_FileDetail_Instructions
            join mSKS_EFT_FileDetail_BankAccount
                order by mSKS_EFT_FileDetail.PaymentCompany, //SKS_VSTS_12809
        mSKS_EFT_FileDetail.RecordSection,
                   mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_VendAddress.RefRecId &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_BenBank.RefRecId &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_Intermediary.RefRecId &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_CreditParty.RefRecId &&
                mSKS_EFT_FileDetail.RecId == msKS_EFT_FileDetail_Instructions.RefRecId &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_BankAccount.RefRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>