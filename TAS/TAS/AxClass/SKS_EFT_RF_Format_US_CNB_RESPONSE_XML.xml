<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_RF_Format_US_CNB_RESPONSE_XML</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_RF_Format_US_CNB_RESPONSE_XML extends SKS_EFT_RF_Format
/// </summary>
class SKS_EFT_RF_Format_US_CNB_RESPONSE_XML extends SKS_EFT_RF_Format
{
    str transStatus;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFile</Name>
				<Source><![CDATA[
    public SKS_EFT_RF_File getFile(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        SKS_EB_FileHub locSKS_EB_FileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        System.Xml.XmlDocument locXmlDocument;
        System.Xml.XmlNode locXMLNode;
        str locTempTagName;
        locXmlDocument = new System.Xml.XmlDocument();
        locSKS_EB_FileHub.runFileLoad(_filename);
        locXmlDocument.LoadXML(locSKS_EB_FileHub.fileBody());
    
        if(locXmlDocument.get_HasChildNodes())
        {
            locXMLNode = locXmlDocument.get_FirstChild();
            locTempTagName = locXMLNode.get_Name();
            if(locTempTagName == 'cnb:CNBEASI')
            {
                this.processNode(locXMLNode);
            }
            //cleanup an pending nodes
            if(mSKS_EFT_RF_Transaction != null)
            {
                mSKS_EFT_RF_SentFile.addTransaction(mSKS_EFT_RF_Transaction);
            }
            if(mSKS_EFT_RF_SentFile != null)
            {
                mSKS_EFT_RF_File.addSentFile(mSKS_EFT_RF_SentFile);
            }
        }
    
        return mSKS_EFT_RF_File;
    }

]]></Source>
			</Method>
			<Method>
				<Name>getFormatId</Name>
				<Source><![CDATA[
    public str getFormatId(str _filename, boolean _serverPath, SKS_BF_BankIntType _SKS_BF_BankIntType = SKS_BF_BankIntType::FileHubLocal)
    {
        str rtnval = '';
        int classId;
        //BEGIN VALIDATION VARIABLES
        //BEGIN QUALIFIERS
        boolean locXMLTagFound = false;
        boolean locIFXFound = false;
        boolean locPmtAddRsFound = false;
    
        //BEGIN DISQUALIFIER VARIABLES
        //none
    
        //END VALIDATION VARIABLES
        SKS_EB_FileHub locSKS_EB_FileHub = SKS_EB_FileHub::construct(_SKS_BF_BankIntType);
        System.String locString;
        System.IO.StreamReader locStreamReader;
    
        //Setup filestream
        locSKS_EB_FileHub.setFileName(_filename);
        locSKS_EB_FileHub.runFileInquire();
        if(!locSKS_EB_FileHub.attrFileExists())
        {
            // The file %1 does not exist.
            error(strfmt("@SYS62207", _filename));
            return '';
        }
        locSKS_EB_FileHub.runFileLoad(_filename);
        locSKS_EB_FileHub.StreamInit();
        locStreamReader = new System.IO.StreamReader(locSKS_EB_FileHub.streamGet());
    
        //priming read
        locString = locStreamReader.ReadLine();
        while(locString != null)
        {
            locString = locString.Trim();
            if(!System.String::IsNullOrWhiteSpace(locString))
            {
                if(!locXMLTagFound)
                {
                    if(locString.StartsWith('<cnb:CNBEASI'))
                    {
                        locXmlTagFound = true;
                    }
                    else
                    {
                        //NOT AN XML DOCUMENT
                        break;
                    }
                }
                if(locString.Contains('<ifx:IFX'))
                {
                    locIFXFound = true;
                }
                if(locString.Contains('<ifx:PmtAddRs'))
                {
                    locPmtAddRsFound = true;
                }
            }
            locString = locStreamReader.ReadLine();
        }
        if(locIFXFound 
           && locPmtAddRsFound)
        {
            classId = classIdGet(this);
            rtnval = classId2Name(ClassId);
        }
        locStreamReader.Close();
    
        return rtnval;
    }

]]></Source>
			</Method>
			<Method>
				<Name>processNode</Name>
				<Source><![CDATA[
    private void processNode(System.Xml.XmlNode _XmlNode)
    {
        System.Xml.XmlNode locXMLNode;
        System.Xml.XmlNodeList locXMLNodeList;
        System.Collections.IEnumerator locXMLNodeListEnum;
        System.Xml.XmlNode locParentXMLNode;
        str locTempNodeName, locNodeName, locTempParentNodeName;
        str value;
    
        locParentXMLNode = _XmlNode.get_ParentNode();
        locTempNodeName = _XmlNode.get_Name();
        switch(locTempNodeName)
        {
            case 'ifx:IFX':
                mSKS_EFT_RF_File = new SKS_EFT_RF_File();
                mSKS_EFT_RF_SentFile = new SKS_EFT_RF_SentFile();
                break;
            case 'ifx:PmtAddRs':                //SKS#DevOps:16542
                if(mSKS_EFT_RF_Transaction != null)
                {
                    mSKS_EFT_RF_SentFile.addTransaction(mSKS_EFT_RF_Transaction);
                    mSKS_EFT_RF_Transaction = null;
                }
                mSKS_EFT_RF_Transaction = new SKS_EFT_RF_Transaction();  
                break;
            case 'ifx:RqUID':
                locTempParentNodeName = locParentXMLNode.get_Name();
                locTempParentNodeName = locTempParentNodeName;
                if(locTempParentNodeName == 'ifx:PmtAddRs')         //SKS#DevOps:16542
                {
                    value = '{' + _XmlNode.get_InnerText() + '}';
                    mSKS_EFT_RF_Transaction.setID(value);
                }
                break;
            case 'ifx:PmtStatusCode':
                transStatus = _XmlNode.get_InnerText();
                mSKS_EFT_RF_Transaction.setStatus(transStatus);
                break;
        }
    
        if(_XmlNode.get_HasChildNodes())
        {
            //priming read
            locXMLNodeList = _XmlNode.get_ChildNodes();
            locXMLNodeListEnum = locXMLNodeList.GetEnumerator();
            locXMLNodeListEnum.Reset();
            while(locXMLNodeListEnum.MoveNext())
            {
                locXMLNode  = locXMLNodeListEnum.get_Current();
                locNodeName = locXMLNode.get_Name();
                if(locNodeName != '#TEXT')
                {
                    this.processNode(locXMLNode);
                }
            }
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>setFilekey</Name>
				<Source><![CDATA[
    protected void setFilekey(str _InboundFileKey)
    {
        mSKS_EFT_RF_SentFile.setID(_InboundFileKey);
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundFileKeyGen</Name>
				<Source><![CDATA[
    [SuppressBPWarning("BPParameterNotUsed", "Parameter required by the event interface")]
    public static str outboundFileKeyGen(str _OutFilename, SKS_EFTAP_File locSKS_EFTAP_File = null)
    {
        ;
    
        return locSKS_EFTAP_File.FileNbr;
    }

]]></Source>
			</Method>
			<Method>
				<Name>outboundTransKeyGen</Name>
				<Source><![CDATA[
    [SuppressBPWarning("BPParameterNotUsed", "Parameter required by the event interface")]
    public static str outboundTransKeyGen(str _FileName = '', SKS_EFT_FileDetail _SKS_EFT_FileDetail = null, SKS_EFTAP_File _SKS_EFTAP_FILE = null)
    {
        SKS_EFT_FileData locSKS_EFT_FileData;
        System.Text.StringBuilder sb;

        sb = new System.Text.StringBuilder();

        Select firstonly FieldValue from locSKS_EFT_FileData
            where locSKS_EFT_FileData.RefRecId == _SKS_EFT_FileDetail.RecId
                    && locSKS_EFT_FileData.FieldName == 'RqUIDT';
        sb.Append(locSKS_EFT_FileData.FieldValue);

        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>