<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_BMO_194</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_PP_Format_BMO_194 extends SKS_PP_Format_BankGroup
/// </summary>
class SKS_PP_Format_BMO_194 Extends SKS_PP_Format_BankGroup
{
    SKS_PP_Bank gSKS_PP_Bank;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    protected str txtDetail()
    {
        // Bank of Montreal - 194
        //Payee Positive Pay Service — Standard Issued Check Record Layout w/Payee Verification Service
        //Field #    Position    Length    Characteristics    Description
        //Detail Record
        //001    001-001    1    Alpha/Numeric Pic X    Constant Value = C
        //002    002-004    3    Numeric Pic 9 (3)    Bank Number = 373
        //003    005-006    2    Numeric Pic 9 (2)    Filler – Zeros
        //004    007-016    10   Numeric Pic (10)    Account Number
        //005    017-017    1    Alpha/Numeric Pic X    Filler – Blanks/Spaces
        //006    018-018    1    Alpha/Numeric Pic X    Transaction Type
        //                                          R = Register
        //                                          V = Void
        //007    019-019    1     Alpha/Numeric Pic X    Action Indicator
        //                                          A = Add
        //                                          D = Delete
        //008    020-020    1     Alpha/Numeric Pic X    Filler – Blanks/Spaces
        //009    021-030    10    Numeric Pic 9 (10)    Check Serial Number
        //010    031-040    10    Numeric Pic 9 (8) V99    Check Amount $$$$$$$$¢¢
        //011    041-046    6     Numeric Pic 9 (6)    Issue Date (MMDDYY)
        //012    047-066    20    Alpha/Numeric Pic X (20)    User Information (Space Filled)
        //013    067-194    14    Alpha/Numeric Pic X (128)   Payee Info
    
    
        int Mult;
        str ItemType;
        BankChequeTable bankChequeTable;
        Container company;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        //Convert negative void numbers to positives
        Mult = 1;
        if(mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = 'V';
            if(mSKS_PP_FileDetail.chkcuryamt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = 'R';
        }
    
        //CONSTANT 'C'
        sb.Append('C');
        //Bank Number
        if(!gSKS_PP_Bank || mSKS_PP_FileDetail.BnkBankAcct != gSKS_PP_Bank.BankAccountId)
        {
            select PP01, BankAccountID from gSKS_PP_Bank where gSKS_PP_Bank.BankAccountId == mSKS_PP_FileDetail.ChkBankAcct;
        }
        sb.Append(SKS_PP_StringFormats::strFill(3, gSKS_PP_Bank.PP01, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        //Filler 2 zeros
        sb.Append(SKS_PP_StringFormats::strFill(2,'',SKS_PP_FillCharacter::ZERO, SKS_PP_FillSide::Right));
        //Bank Acct Number
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        //Filelr 1 space
        sb.Append(SKS_PP_StringFormats::strFill(1,'',SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        //Item Type
        sb.Append(ItemType);
        //Action Type
        sb.Append('A');
        //Filler 1 space
        sb.Append(SKS_PP_StringFormats::strFill(1,'',SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        //Check Serial Number
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        //Check Amount $$$$$$$$¢¢
        sb.Append(SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 10));
        //Check Issue Date MMDDYY
        sb.Append(SKS_PP_StringFormats::strDateMMDDYY(mSKS_PP_FileDetail.ChkDocDate));
        //User Information (space filled)
        company = [mSKS_PP_FileDetail.ChkCpnyId];
        changeCompany(mSKS_PP_FileDetail.ChkCpnyId)         //SKS#devOps:17070
        {
            select firstonly SKS_PP_FILENBR, ChequeNum, AccountID, RecipientAccountNum from bankChequeTable where bankChequeTable.SKS_PP_FILENBR == mSKS_PP_FileDetail.FileNbr
                && bankChequeTable.ChequeNum == mSKS_PP_FileDetail.ChkRefNbr
                && bankChequeTable.AccountID == mSKS_PP_FileDetail.ChkBankAcct;
        }
        sb.Append(SKS_PP_StringFormats::strFill(20, bankChequeTable.RecipientAccountNum, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        //Payee info.  can be truncated.  filled for SK standardization purposes.
        sb.Append(SKS_PP_StringFormats::strFill(128,mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>