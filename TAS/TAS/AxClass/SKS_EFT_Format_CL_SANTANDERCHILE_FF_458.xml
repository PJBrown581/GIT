<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_EFT_Format_CL_SANTANDERCHILE_FF_458</Name>
	<SourceCode>
		<Declaration><![CDATA[
/// <summary>
/// SKS_EFT_Format_CL_SANTANDERCHILE_FF_458 extends SKS_EFT_Format
/// </summary>
class SKS_EFT_Format_CL_SANTANDERCHILE_FF_458 extends SKS_EFT_Format
{
    #define.FormatVersion('20220330')
    int invoiceCount;

}
]]></Declaration>
		<Methods>
			<Method>
				<Name>getFormatVersion</Name>
				<Source><![CDATA[
    protected str getFormatVersion()
    {
        return #FormatVersion;
    }

]]></Source>
			</Method>
			<Method>
				<Name>formatStructure</Name>
				<Source><![CDATA[
    protected void formatStructure()
    {
        while(mSKS_EFT_FileDetail)
        {
            // Get SKS_EFT_FileFormatPaymSpec
            this.getSKS_EFT_FileFormatPaymSpec();

            mSKS_EFT_FileDetail_BankAccount = SKS_EFT_FileDetail_BankAccount::find(mSKS_EFT_FileDetail.RecId);

            // Detail record
            this.rec_Detail();

            next mSKS_EFT_FileDetail;
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>rec_Detail</Name>
				<Source><![CDATA[
    public boolean rec_Detail()
    {
        //------------------------------------------------------------------------------------------------------
        //Nbr   Description                                                 Length      Position
        //1     Supplier ID + digit                                             12       1-12
        //2     Name                                                            40       13-52
        //3     Address                                                         40       53-92 
        //4     Commune                                                         15       93-107
        //5     City                                                            15       108-122
        //6     Branch withdrawal                                               3        123-125
        //7     Payment method                                                  2        126-127
        //8     Bank code                                                       4        128-131
        //9     Credit account                                                  18       132-149
        //10    Ruth from the one who withdraws                                 12       150-161
        //11    Picker's name                                                   40       162-201
        //12    Document Number Nº1                                             8        202-209
        //13    Document sign N ° 1                                             1        210-210
        //14    Amount Document No. 1                                           13       211-223
        //15    Document Number Nº2                                             8        224-231
        //16    Sign of document N ° 2                                          1        232-232
        //17    Amount Document No. 2                                           13       233-245
        //18    Document Number Nº3                                             8        246-253
        //19    Sign of document N ° 3                                          1        254-254
        //20    Amount Document No. 3                                           13       255-267
        //21    Document Number Nº4                                             8        268-275
        //22    Sign of document N ° 4                                          1        276-276
        //23    Amount Document No. 4                                           13       277-289
        //24    Document Number Nº5                                             8        290-297
        //25    Sign of document N ° 5                                          1        298-298
        //26    Amount Document No. 5                                           13       299-311
        //27    Document Number Nº6                                             8        312-319
        //28    Sign of document N ° 6                                          1        320-320
        //29    Amount Document No. 6                                           13       321-333
        //30    Document Number Nº7                                             8        334-341
        //31    Sign of document N ° 7                                          1        342-342
        //32    Amount Document No. 7                                           13       343-355
        //33    Document Number Nº8                                             8        356-363
        //34    Sign of document N ° 8                                          1        364-364
        //35    Amount Document No. 8                                           13       365-377
        //36    Document Number Nº9                                             8        378-385
        //37    Sign of document N ° 9                                          1        386-386
        //38    Amount Document No. 9                                           13       387-399
        //39    Document Number Nº10                                            8        400-407
        //40    Sign of document N ° 10                                         1        408-408
        //41    Amount Document No. 10                                          13       409-421
        //42    Document Number Nº11                                            8        422-429
        //43    Sign of document N ° 11                                         1        430-430
        //44    Amount Document No. 11                                          13       431-433
        //45    Total to pay                                                    15       444-458
        //------------------------------------------------------------------------------------------------------
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str supplierID, taxId;
        int i, repeatRecords = 1;
        SKS_EFT_FileDetail invSKS_EFT_FileDetail;
        SKS_EFT_FileDetail tempSKS_EFT_FileDetail;

        //1     Supplier ID + digit                                             12       1-12
        supplierID = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('SupplierID', mSKS_EFT_FileDetail.RecId, SKS_EFT_CodeLookupType::CodeLookupBank));
        sb.Append(SKS_PP_StringFormats::strFill(12, supplierID, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //2     Name                                                            40       13-52
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //3     Address                                                         40       53-92
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyAddr), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //4     Commune                                                         15       93-107
        sb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyAddr2), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //5     City                                                            15       108-122
        sb.Append(SKS_PP_StringFormats::strFill(15, this.CharacterReplacement(mSKS_EFT_FileDetail_BankAccount.WOrdPartyCity), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //6     Branch withdrawal                                               3        123-125
        sb.Append(SKS_PP_StringFormats::strFill(3, this.CharacterReplacement(mSKS_EFT_FileDetail_BenBank.BenBankBranch), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //7     Payment method                                                  2        126-127
        sb.Append(SKS_PP_StringFormats::strFill(2, '3', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //8     Bank code                                                       4        128-131
        sb.Append(SKS_PP_StringFormats::strFill(4, mSKS_EFT_FileDetail.DepBankTransit, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //9     Credit account                                                  18       132-149
        sb.Append(SKS_PP_StringFormats::strFill(18, mSKS_EFT_FileDetail.DepBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

        //10    Ruth from the one who withdraws                                 12       150-161
        taxId = this.CharacterReplacement(SKS_EFT_Format::getCodelookupvalue('RUTID', mSKS_EFT_FileDetail.RecId));
        sb.Append(SKS_PP_StringFormats::strFill(12, taxId, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));

        //11    Picker's name                                                   40       162-201
        sb.Append(SKS_PP_StringFormats::strFill(40, this.CharacterReplacement(mSKS_EFT_FileDetail_VendAddress.BeneName), SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));        
        
        select count(RecordSummaryRecId) from tempSKS_EFT_FileDetail
            where tempSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId
            && tempSKS_EFT_FileDetail.InvoiceNumber != '';

        invoiceCount = int642int(tempSKS_EFT_FileDetail.RecordSummaryRecId);
        
        while select invSKS_EFT_FileDetail where invSKS_EFT_FileDetail.RecordSummaryRecId == mSKS_EFT_FileDetail.RecId 
            && invSKS_EFT_FileDetail.InvoiceNumber != ''            
        {
            if(repeatRecords <= 11)
            {
                //12    Document Number Nº1                                             8        202-209
                sb.Append(SKS_PP_StringFormats::strFill(8, invSKS_EFT_FileDetail.InvoiceNumber, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));

                //13    Document sign N ° 1                                             1        210-210
                if(mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur * -1 > 0)
                {
                    sb.Append('+');
                }
                else
                {
                    sb.Append('-');
                }
                //14    Amount Document No. 1                                           13       211-223
                sb.Append(SKS_PP_StringFormats::strMoney(-1 * invSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 13));

                repeatRecords++;
            }
        }
        
        for(i = invoiceCount; i < 11; i++)
        {
            sb.Append(SKS_PP_StringFormats::strFill(8, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
            sb.Append(SKS_PP_StringFormats::strFill(1, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
            sb.Append(SKS_PP_StringFormats::strFill(13, '', SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        }
        
        //45    Total to pay                                                    15       444-458
        sb.Append(SKS_PP_StringFormats::strMoney(-1 * mSKS_EFT_FileDetail.InvoiceSettleAmountPmtCur, 15));
        
        this.fileWrite(sb.ToString());
        return true;
    }

]]></Source>
			</Method>
			<Method>
				<Name>selectPaymentRecords</Name>
				<Source><![CDATA[
    protected void selectPaymentRecords()
    {
        //------------------------------------------------------------------------------------------------------
        // Standard payment selction
        // Override if different selection is need for a format
        //------------------------------------------------------------------------------------------------------
    
        //Order by clause must match the clause in the SKS_EFT_Processing.SumByPayment
        select * from mSKS_EFT_FileDetail
            join mSKS_EFT_FileDetail_VendAddress
            join mSKS_EFT_FileDetail_BenBank
              order by mSKS_EFT_FileDetail.RecordSection,
                   mSKS_EFT_FileDetail.PaymMode,
                   mSKS_EFT_FileDetail.BankAccountId,
                   mSKS_EFT_FileDetail.PaymentCurrencyCode,
                   mSKS_EFT_FileDetail.PaymSpec,
                   mSKS_EFT_FileDetail.EffectiveDate,
                   mSKS_EFT_FileDetail.DepCountryRegionId,
                   mSKS_EFT_FileDetail.RecordType,
                   mSKS_EFT_FileDetail.AccountNum,
                   mSKS_EFT_FileDetail.PaymentReference,
                   mSKS_EFT_FileDetail.RecId
          where mSKS_EFT_FileDetail.FileRecId == mSKS_EFT_File.RecId &&
                mSKS_EFT_FileDetail.RecordSection == SKS_EFT_RecordSection::Payments    &&
                mSKS_EFT_FileDetail.RecordSummary != SKS_EFT_RecordSummary::NonSummary  &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_VendAddress.RefRecId   &&
                mSKS_EFT_FileDetail.RecId == mSKS_EFT_FileDetail_BenBank.RefRecId;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>