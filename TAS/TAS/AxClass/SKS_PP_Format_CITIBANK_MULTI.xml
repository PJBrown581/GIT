<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_CITIBANK_MULTI</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_CITIBANK_MULTI Extends SKS_PP_Format_BankGroup
{
    int mIssueCnt;
    real mVoidAmt;
    int mVoidCnt;
    real mTrailVoidAmt;
    real mTrailIssueAmt;
    int mTrailerCnt;
    int mTotalCnt;
    real mTotalIssueAmt;
    real mTotalVoidAmt;
    //
        // Citibank - Multi Account Format - Detail Record
    
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    // Pos 01-08     8       Account Number (RJ-LZF)
        // Pos 09-18     10      Check Number (RJ-LZF)
        // Pos 19-29     11      Check Amount (RJ-LZF), Voids - zero amount
        // Pos 30-35     6       Issue Date of Check (YYMMDD)
        // Pos 26-36     1       Void Indicator ('V' - void)
        // Pos 37-76     30      Additional Data - blank
        // Pos 77-77     1       Plan Number
        // Pos 78-80     3       Filler
    protected str txtDetail()
    {
        str ItemType;
        int Mult;
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        Mult = 1;
        if (mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            ItemType = 'V';
            if (mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = ' ';
        }
    
        mIssueCnt++;
        sb.Append(SKS_PP_StringFormats::strFill(8, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
    
        if (mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            sb.Append(SKS_PP_StringFormats::strMoney(0, 11));
    
            mVoidAmt = mVoidAmt + mSKS_PP_FileDetail.ChkCuryAmt;
            mVoidCnt++;
            mTrailVoidAmt = mTrailVoidAmt + mSKS_PP_FileDetail.ChkCuryAmt;
        }
        else
        {
            sb.Append(SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 11));
    
            mTrailIssueAmt = mTrailIssueAmt + mSKS_PP_FileDetail.ChkCuryAmt;
        }
    
        sb.Append(SKS_PP_StringFormats::strDateYYMMDD(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append(ItemType);
        sb.Append(SKS_PP_StringFormats::strFill(30, mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        mTrailerCnt++;
        mTotalCnt++;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatTrailer</Name>
				<Source><![CDATA[
    //
        // Citibank - Multi Account Format - Footer Record
    
        // Pos 01-01     1       Indicator ('F')
        // Pos 02-08     7       Filler
        // Pos 09-18     10      Item Total (RJ-LZF)
        // Pos 19-29     11      Amount Total (RJ-LZF)
        // Pos 30-80     49      Filler
        //
    protected str txtFormatTrailer()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
    
        sb.Append('F');
        sb.Append(SKS_PP_StringFormats::strFill(7, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(mTotalCnt), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(mTotalIssueAmt + mTotalVoidAmt, 11));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    // Citibank - Multi Account Format - Trailer Record
    
        // Pos 01-01     1       Indicator ('T')
        // Pos 02-08     7       Filler
        // Pos 09-18     10      Item Total (RJ-LZF)
        // Pos 19-29     11      Amount Total (RJ-LZF)
        // Pos 30-80     49      Filler
    protected str txtGroupTrailer()
    {
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str result;
        ;
    
        sb.Append('T');
        sb.Append(SKS_PP_StringFormats::strFill(7, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(10, int2str(mTrailerCnt), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strMoney(mTrailIssueAmt + mTrailVoidAmt, 11));
    
        result = sb.ToString();
    
        mTotalIssueAmt = mTotalIssueAmt + mTrailIssueAmt;
        mTotalVoidAmt = mTotalVoidAmt + mTrailVoidAmt;
    
        mTrailerCnt = 0;
        mTrailIssueAmt = 0;
        mTrailVoidAmt = 0;
    
        return result;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>