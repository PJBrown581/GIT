<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_PP_Format_WELLSFARGO_165</Name>
	<SourceCode>
		<Declaration><![CDATA[
class SKS_PP_Format_WELLSFARGO_165 Extends SKS_PP_Format_BankGroup
{
    real Chksum;
    int Chkcount;
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>txtDetail</Name>
				<Source><![CDATA[
    // Wells Fargo 165
    
    // Pos 01-10    Check Number
    // Pos 11-16    Issue Date (mmddyy)
    // Pos 17-31    Account Number
    // Pos 32-34    Trancode (320 = issued check, 370 = cancel old register - void)
    // Pos 35-44    Amount (zeroes for void)
    // Pos 45-165   Payee Name
    protected str txtDetail()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        int Mult = 1;
        str ItemType;
        str Amt;
        ;
    
    
        Mult = 1;
        if (mSKS_PP_FileDetail.ChkType != SKS_PP_ChequeType::Cheque)
        {
            if (mSKS_PP_FileDetail.ChkType == SKS_PP_ChequeType::CancelledCheque)
            {
                ItemType = '370';
            }
            else
            {
                ItemType = '430';
                Mult = 0;
            }
    
            if (mSKS_PP_FileDetail.ChkCuryAmt < 0)
            {
                Mult = -1;
            }
        }
        else
        {
            ItemType = '320';
        }
    
        Amt =  SKS_PP_StringFormats::strMoney(Mult * mSKS_PP_FileDetail.ChkCuryAmt, 10);
    
        Chksum = Chksum + ABS(mSKS_PP_FileDetail.ChkCuryAmt);
        Chkcount++;
    
        sb.Append(SKS_PP_StringFormats::strFill(10, mSKS_PP_FileDetail.ChkRefNbr, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strDateMMDDYY(mSKS_PP_FileDetail.ChkDocDate));
        sb.Append(SKS_PP_StringFormats::strFill(15, mSKS_PP_FileDetail.BnkBankAcct, SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(ItemType);
        sb.Append(Amt);
        sb.Append(SKS_PP_StringFormats::strFill(121, mSKS_PP_FileDetail.VendName, SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatHeader</Name>
				<Source><![CDATA[
    /* Header record
    ' WELLS FARGO POSITIVE PAY FORMAT 165
    
    
    ' Pos 01-03    3        Literal - *03
    ' Pos 04-08    5        Bank ID (RJLZF) SKS_PP_Bank.PP01
    ' Pos 09-23    15       Account number - (RJLZF)
    ' Pos 24-24    1        File Status - 0
    */
    protected str txtFormatHeader()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        SKS_PP_Bank bSKS_PP_Bank;
        Container company;
        ;
    
        company = [mSKS_PP_FileDetail.ChkCpnyId];
        select crossCompany:company PP01, PP02 from bSKS_PP_Bank where bSKS_PP_Bank.BankAccountId == mSKS_PP_FileDetail.ChkBankAcct;
    
        sb.Append('*03');
        sb.Append(SKS_PP_StringFormats::strFill(5, strltrim(strrtrim(bSKS_PP_Bank.PP01)), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(15, strltrim(strrtrim(bSKS_PP_Bank.PP02)), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append('0');
        sb.Append(SKS_PP_StringFormats::strFill(141, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtFormatTrailer</Name>
				<Source><![CDATA[
    // Remove method if not overriding
    protected str txtFormatTrailer()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        ;
        sb.Append('');
    
        return sb.ToString();
    }

]]></Source>
			</Method>
			<Method>
				<Name>txtGroupTrailer</Name>
				<Source><![CDATA[
    //Wells Fargo 165
    
    // Pos 01-01     Literal '&'
    // Pos 02-15     Spaces
    // Pos 16-22     Record Count
    // Pos 23-25     Spaces
    // Pos 26-38     Total Amount
    // Pos 39-165     Spaces
    protected str txtGroupTrailer()
    {
        //BP Deviation Documented
        System.Text.StringBuilder sb = new System.Text.StringBuilder();
        str Amt;
        ;
    
        Amt =  SKS_PP_StringFormats::strMoney(Chksum, 13);
    
        sb.Append('&');
        sb.Append(SKS_PP_StringFormats::strFill(14, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(7, int2str(Chkcount), SKS_PP_FillCharacter::Zero, SKS_PP_FillSide::Left));
        sb.Append(SKS_PP_StringFormats::strFill(3, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
        sb.Append(Amt);
        sb.Append(SKS_PP_StringFormats::strFill(127, '', SKS_PP_FillCharacter::Space, SKS_PP_FillSide::Right));
    
        ChkSum = 0.00;
        Chkcount = 0;
    
        return sb.ToString();
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>