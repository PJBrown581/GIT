<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKSBankAccountStatement_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(tablestr(BankAccountStatement))]
final class SKSBankAccountStatement_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>statementTransNotMatched</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST statementTransNotMatched(BankAccountStatement _this)
    {
        return _this.data_statementTransNotMatched(_this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>data_statementTransNotMatched</Name>
				<Source><![CDATA[
    public AmountMST data_statementTransNotMatched(BankAccountStatement _this)
    {
        SKS_BR_BankTran bankTrans;

        select sum(TranAmount) from bankTrans
        where bankTrans.AccountId == _this.AccountId
            && bankTrans.AccountStatementNum == _this.AccountStatementNum
            && bankTrans.Matched == NoYes::No;

        return bankTrans.TranAmount;
    }

]]></Source>
			</Method>
			<Method>
				<Name>difference</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST difference(BankAccountStatement _this)
    {
        AmountMST   difference;

        difference = (_this.data_CalcEndingBalance(_this) - _this.data_CalcGLBalance(_this));
        return difference;
    }

]]></Source>
			</Method>
			<Method>
				<Name>bankAccountName</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display Name bankAccountName(BankAccountStatement _this)
    {
        return BankAccountTable::name(_this.AccountId);
    }

]]></Source>
			</Method>
			<Method>
				<Name>bankTransNotCleared</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST bankTransNotCleared(BankAccountStatement _this)
    {
        return _this.data_bankTransNotCleared(_this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>data_bankTransNotCleared</Name>
				<Source><![CDATA[
    //Bug 10799 -->
    public AmountMST data_bankTransNotCleared(BankAccountStatement _this)
    {
        AmountCur           balanceCur;
        BankAccountTrans    bankAccountTrans;
                              
        // posted date is less than or equal to statement date and bank account date is empty or greater than account statement date.
        select sum(AmountCur) from bankAccountTrans
            where bankAccountTrans.AccountId == _this.AccountId &&            
            bankAccountTrans.TransDate <= _this.AccountStatementDate &&
            bankAccountTrans.AccountStatementDate != _this.AccountStatementDate &&
            (bankAccountTrans.AccountStatementDate > _this.AccountStatementDate || bankAccountTrans.AccountStatementDate == dateNull());
            
        if(bankAccountTrans.AmountCur)
        {
            balanceCur = -(bankAccountTrans.AmountCur);
        }                
        // posted date is greater than statement date and bank account statement date is less than or equal to statement date.
        select sum(AmountCur) from bankAccountTrans
            where bankAccountTrans.AccountId == _this.AccountId &&            
            bankAccountTrans.TransDate > _this.AccountStatementDate &&
            bankAccountTrans.AccountStatementDate <= _this.AccountStatementDate &&
            bankAccountTrans.AccountStatementDate != dateNull();

        if(bankAccountTrans.AmountCur)
        {
            balanceCur += bankAccountTrans.AmountCur;
        }            
        return balanceCur;    
    }

]]></Source>
			</Method>
			<Method>
				<Name>data_CalcEndingBalance</Name>
				<Source><![CDATA[
    //Bug 10799 <--

    public AmountMST data_CalcEndingBalance(BankAccountStatement _this)
    {
        return (_this.EndingBalance - SKS_BR_BankAcctStmt::unreconciledAmount(_this));
    }

]]></Source>
			</Method>
			<Method>
				<Name>data_CalcGLBalance</Name>
				<Source><![CDATA[
    public AmountMST data_CalcGLBalance(BankAccountStatement _this)
    {
        AmountMST   calcGLbalance;

        //CalculateGLBalance = GLBalance + Bank transactions not cleared
        calcGLbalance = (_this.statementCalcBalance(_this) + _this.data_bankTransNotCleared(_this));
        return calcGLbalance;
    }

]]></Source>
			</Method>
			<Method>
				<Name>calcEndingBalance</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST calcEndingBalance(BankAccountStatement _this)
    {
        return _this.data_CalcEndingBalance(_this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>CalcGLBalance</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST CalcGLBalance(BankAccountStatement _this)
    {
        return _this.data_CalcGLBalance(_this);
    }

]]></Source>
			</Method>
			<Method>
				<Name>bankBalance</Name>
				<Source><![CDATA[
    [SysClientCacheDataMethodAttribute]
    public static display AmountMST bankBalance(BankAccountStatement _this) //Bug 10799
    {
        return (_this.statementCalcBalance(_this));
    }

]]></Source>
			</Method>
			<Method>
				<Name>statementCalcBalance</Name>
				<Source><![CDATA[
    public AmountMST statementCalcBalance(BankAccountStatement _this)
    {
        //Bug 10799 -->
        BankAccountTrans                    bankAccountTrans;       

        select sum(AmountCur) from bankAccountTrans
            order by TransDate
                where bankAccountTrans.AccountId == _this.AccountID
            && bankAccountTrans.TransDate <= _this.AccountStatementDate;                     
        
        return bankAccountTrans.AmountCur;
        //Bug 10799 <--
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>