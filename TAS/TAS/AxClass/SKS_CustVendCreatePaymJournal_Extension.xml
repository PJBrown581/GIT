<?xml version="1.0" encoding="utf-8"?>
<AxClass xmlns:i="http://www.w3.org/2001/XMLSchema-instance">
	<Name>SKS_CustVendCreatePaymJournal_Extension</Name>
	<SourceCode>
		<Declaration><![CDATA[
[ExtensionOf(classStr(CustVendCreatePaymJournal))]
final class SKS_CustVendCreatePaymJournal_Extension
{
}
]]></Declaration>
		<Methods>
			<Method>
				<Name>runPaymentProposalGenerationProcess</Name>
				<Source><![CDATA[
    protected void runPaymentProposalGenerationProcess()
    {
        SKS_DD_CustVendMandPaymValidate custVendMandPaymValidate;

        LedgerJournalId ledgerJournalIdLoc = ledgerJournalId;

        next runPaymentProposalGenerationProcess();
        
        if (!(this is SKS_CNP_CreatePaymReimbJournal) && this.updateQueryCompanyRangeExt())
        {
            custVendMandPaymValidate = SKS_DD_CustVendMandPaymValidate::construct(ledgerJournalIdLoc);
            custVendMandPaymValidate.validateCustPayment_PaymProposal();
        }
    }

]]></Source>
			</Method>
			<Method>
				<Name>updateQueryCompanyRangeExt</Name>
				<Source><![CDATA[
    /// <summary>
    ///    Updates the company range of the process query so that only companies that were selected and are
    ///    part of the shared service boundary are enabled.
    /// </summary>
    /// <returns>
    ///    true if the company range of the process query includes enabled companies that are part of the
    ///    shared service boundary; otherwise, false.
    /// </returns>
    /// <remarks>
    ///    The enabled companies in the company range of the process query may include companies that are not
    ///    part of the shared service boundary. The payment proposal process should only include companies
    ///    that are part of the shared service boundary.
    /// </remarks>
    public boolean updateQueryCompanyRangeExt()
    {
        Query query;
        container sharedServiceBoundaryCompanies;
        container queryRangeCompanies;
        container queryRangeCompany;
        boolean isQueryValidForProcess = false;
        int i;

        if (queryCreatePayment.allowCrossCompany())
        {
            query = queryCreatePayment.query();

            sharedServiceBoundaryCompanies = CustVendOpenTransManager::findSharedServiceCompanies(curext());
            queryRangeCompanies = query.getCompanyRange();

            // Disable companies that are not part of the shared service boundary
            // The company range is a collection of containers. The container structure is (CompanyId,boolean)
            // where the boolean is a flag specifying whether data for the associated CompanyId is included in
            // the query processing.

            query.clearCompanyRange();
            for (i = 1; i <= conLen(queryRangeCompanies); i++)
            {
                queryRangeCompany = conPeek(queryRangeCompanies, i);

                // if the company was enabled and is part of the shared service boundary then add it back to the company range
                if (conPeek(queryRangeCompany, 2) && conFind(sharedServiceBoundaryCompanies, conPeek(queryRangeCompany, 1)) != 0)
                {
                    query.addCompanyRange(conPeek(queryRangeCompany, 1));

                    // If we enable at least one company that is part of the shared service boundary
                    // the query is useful to the process.
                    isQueryValidForProcess = true;
                }
            }
        }
        else
        {
            // query is not cross company so company range is ignored and thus valid
            isQueryValidForProcess = true;
        }

        return isQueryValidForProcess;
    }

]]></Source>
			</Method>
		</Methods>
	</SourceCode>
</AxClass>